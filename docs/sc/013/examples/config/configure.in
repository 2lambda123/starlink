dnl  Autoconf script for configuring the Makefile for the
dnl  SC/13 demo programs.

AC_INIT(fpp.c)
AC_REVISION($Id$)dnl

AC_CONFIG_AUX_DIR(config)

AC_PROG_CC
AC_PROG_F77


dnl  What options do we need to pass to the compiler to get it to
dnl  include profiling code?  Allow this to be specified with
dnl  --with-profiling=<option>.  If that isn't present, then look
dnl  for the program gprof in the path, and set profilingoption to -pg 
dnl  if we find it.
AC_SUBST(PROFILINGOPTION)
PROFILINGOPTION=

AC_SUBST(NOUNDERSCORES)
NOUNDERSCORES=

AC_SUBST(ENABLENAN)
ENABLENAN=

dnl  Is gprof available?  If no value is given, see if we can find
dnl  gprof, and if so, set PROFILINGOPTION to -pg; otherwise, set it
dnl  to ""
AC_ARG_ENABLE(profiling,
[  --enable-profiling=opt  Give the compiler option to include profiling code],
  PROFILINGOPTION=$enableval,
  AC_CHECK_PROG(PROFILINGOPTION, gprof, -pg, ""))

dnl  If --enable-nounderscore is not present, set it to ""
AC_ARG_ENABLE(nounderscore,
[  --enable-nounderscore=opt  
                          Give the compiler option to suppress
                          trailing underscores in symbol names],
  NOUNDERSCORES=$enableval)

AC_ARG_ENABLE(nan,
[  --enable-nan=opt       Compiler switch to suppress trap on IEEE exceptions],
  ENABLENAN=$enableval)


dnl  We know what these options are for gcc/g77, and autoconf makes it
dnl  easy to test whether it's gcc/g77 we're using, so special-case these:
if test "$NOUNDERSCORES" != "no"; then
  if test "$G77" = "yes"; then
    if test -z "$PROFILINGOPTION"; then
      PROFILINGOPTION='-pg'
    fi
    if test -z "$NOUNDERSCORES"; then
      NOUNDERSCORES="-fno-underscoring"
    fi
  fi
fi

dnl  If we haven't set both of PROFILINGOPTION and NOUNDERSCORES by
dnl  now, then try to guess appropriate values based on the host
dnl  platform.  This isn't ideal, since it can't tell if you're using
dnl  a non-default compiler, but we'll try to use the options at the
dnl  end, and try to give a helpful error message if it doesn't work.
dnl
dnl  Both gcc and Sun cc permit IEEE exceptional values by default,
dnl  Alpha cc traps on exceptional values.
if test -z "$PROFILINGOPTION" -o -z "$NOUNDERSCORES"; then
    AC_CANONICAL_HOST
    echo "Target system $host"
    case "$host" in
      sparc-*-*) 
        if test -z "$PROFILINGOPTION" -o "$PROFILINGOPTION" != "no"; then 
            PROFILINGOPTION=-pg
        fi
        if test -z "$NOUNDERSCORES" -o "$NOUNDERSCORES" != "no"; then
            NOUNDERSCORES=-ext_names=plain
        fi
        ;;
      alpha*)
        if test -z "$PROFILINGOPTION" -o "$PROFILINGOPTION" != "no"; then 
            PROFILINGOPTION=-pg
        fi
        if test -z "$NOUNDERSCORES" -o "$NOUNDERSCORES" != "no"; then
            NOUNDERSCORES='-assume nounderscore'
        fi
        if test -z "$ENABLENAN" -o "$ENABLENAN" != "no"; then
            ENABLENAN=-ieee
        fi
        ;;
      *)
        AC_MSG_WARN([
  I can't work out how to specify the profiling,
  no-underscore and enable-nan options for your C/Fortran compilers.
  You can specify them with the --enable-profiling=<option>,
  --enable-nounderscore=<option>, and --enable-nan=<option> configure
  option.  If you have to do this, can you mail norman@astro.gla.ac.uk
  with the appropriate options, quoting the compiler type
      $host.
  Thanks.])
         ;;
    esac
fi

dnl  Now test the options we have set up, by compiling a simple
dnl  Fortran program
AC_LANG_FORTRAN77
dnl   Following triggers a bug in autoconf 2.13 (reported to
dnl  http://sources.redhat.com/autoconf/).
dnl
dnl safe_FFLAGS=$FFLAGS
dnl FFLAGS="FFLAGS $PROFILINGOPTION $NOUNDERSCORES"
dnl AC_TRY_COMPILE(,
dnl       I=1,
dnl   echo "Fortran flags $FFLAGS work",
dnl   [AC_MSG_WARN([
dnl   Oooh-err.  Based on your platform, $host, I thought that the Fortran
dnl   compiler options $FFLAGS would work, but they don't appear to.
dnl   Perhaps the Fortran compiler you've selected, $F77, isn't the default
dnl   one for this platform.  You can force the profiling options and
dnl   no-underscores options by using the configure flags
dnl    --enable-profiling=<option> and --enable-nounderscore=<option>])])
dnl FFLAGS=$safe_FFLAGS

dnl  So do it by hand.
cat >conftest.f <<EOF
      PROGRAM CONFTEST
      I=1
      END
EOF
$F77 $FFLAGS $PROFILINGOPTION $NOUNDERSCORES $ENABLENAN -c conftest.f
if test $? = 0; then
    echo "Fortran flags $FFLAGS $PROFILINGOPTION $NOUNDERSCORES $ENABLENAN work"
else
    AC_MSG_WARN([
    Oooh-err.  Based on your platform, 
        $host
    I thought that the Fortran compiler options 
        $F77 $FFLAGS $PROFILINGOPTION $NOUNDERSCORES $ENABLENAN -c conftest.f
    would work, but they don't appear to.  Perhaps the Fortran
    compiler you've selected, $F77, isn't the default one for this
    platform.  You can supply the compiler options yourself
    by using the configure flags --enable-profiling=<option>
    --enable-nounderscore=<option>, --enable-nan=<option>])
fi
rm -f conftest.*

AC_OUTPUT(Makefile)
