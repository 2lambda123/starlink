      SUBROUTINE CCG_SM1<T>( NSIGMA, NPIX, NLINES, STACK, VARS, MINPIX,
     :                     NMAT, COVEC, RESULT, RESVAR, WRK1, WRK2,
     :                     POINT, USED, NCON, NBAD, STATUS )
*+
*  Name:
*     CCG_SM1x

*  Purpose:
*     Combines data lines using a sigma-clipped median.

*  Language:
*     Starlink Fortran 77

*  Invocation:
*     CALL CCG_SM1x( NSIGMA, NPIX, NLINES, STACK, VARS, MINPIX, COVEC,
*                    NMAT, RESULT, RESVAR, WRK1, WRK2, POINT, USED,
*                    NCON, NBAD, STATUS )

*  Description:
*     This routine accepts an array consisting a series of (vectorised)
*     lines of data.  The weighted mean and standard deviation of each
*     input column in STACK is then used to estimate the range of
*     values which represent the required sigma clipping.  Values
*     outside of this range are then rejected and then the median
*     of the remaining values is returned in the array RESULT.  The
*     variances are propagated through the combination processand
*     returned in the RESVAR array.

*  Arguments:
*     NSIGMA = REAL (Given)
*        The number of sigma at which to reject data values.
*     NPIX = INTEGER (Given)
*        The number of pixels in a line of data.
*     NLINES = INTEGER (Given)
*        The number of lines of data in the stack.
*     STACK( NPIX, NLINES ) = ? (Given)
*        The array of lines which are to be combined into a single line.
*     VARS( NPIX, NLINES ) = ? (Given)
*        The data variances.
*     MINPIX = INTEGER (Given)
*        The minimum number of pixels required to contribute to an
*        output pixel.
*     NMAT = INTEGER (Given)
*        Size of the first dimension of COVEC.
*     COVEC( NMAT, NLINES ) = DOUBLE PRECISION (Given)
*        The packed variance-covariance matrix of the order statistics
*        from a normal distribution of sizes up to NLINES, produced by
*        CCG_ORVAR.
*     RESULT( NPIX ) = ? (Returned)
*        The output line of data.
*     RESVAR( NPIX ) = ? (Returned)
*        The output variances.
*     WRK1( NLINES ) = ? (Returned)
*        Workspace for calculations.
*     WRK2( NLINES ) = ? (Returned)
*        Workspace for calculations.
*     POINT( NLINES ) = INTEGER (Returned)
*        Workspace to hold pointers to the original positions of the
*        data before extraction and conversion in to the WRK1 array.
*     USED( NLINES ) = LOGICAL (Returned)
*        Workspace used to indicate which values have been used in
*        estimating a resultant value.
*     NCON( NLINES ) = DOUBLE PRECISION (Given and Returned)
*        The actual number of contributing pixels.
*     NBAD = INTEGER (Returned)
*        The number of bad values in the output array created while
*        forming the statistics.  It excludes those bad values whose
*        corresponding values along the collapse axis are all bad.
*     STATUS = INTEGER (Given and Returned)
*        The global status.

*  Notes:
*     -  There is a routine for real and double-precision: replace "x"
*     in the routine name by D or R as appropriate.  The STACK, VARS,
*     RESULT, RESVAR, WRK1, and WRK2 arguments supplied to the routine
*     must have the data type specified.

*  Copyright:
*     Copyright (C) 1998, 2004 Central Laboratory of the Research
*     Councils. Copyright (C) 2006 Particle Physics & Astronomy
*     Research Council. Copyright (C) 2009 Science & Technology
*     Facilities Council. All Rights Reserved.

*  Licence:
*     This program is free software; you can redistribute it and/or
*     modify it under the terms of the GNU General Public License as
*     published by the Free Software Foundation; either Version 2 of
*     the License, or (at your option) any later version.
*
*     This program is distributed in the hope that it will be
*     useful, but WITHOUT ANY WARRANTY; without even the implied
*     warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
*     PURPOSE. See the GNU General Public License for more details.
*
*     You should have received a copy of the GNU General Public License
*     along with this program; if not, write to the Free Software
*     Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
*     02110-1301, USA.

*  Authors:
*     PDRAPER: Peter Draper (STARLINK)
*     BRADC: Brad Cavanagh (JAC)
*     MJC: Malcolm J. Currie (STARLINK)
*     {enter_new_authors_here}

*  History:
*     30-JAN-1998 (PDRAPER):
*        Original version.
*     11-OCT-2004 (BRADC):
*        No longer use NUM_CMN.
*     2006 August 6 (MJC):
*        Exclude data with non-positive variance.
*     2009 July 4 (MJC):
*        Converted to generic and renamed from CCG1_SMR1R.  Added NBAD
*        argument.  Moved STACK, NMAT, and NCON to the normal order in
*        the API.  Made NCON Returned and therefore it is initialised.
*     2009 July 17 (MJC):
*        Do not include collapsed data comprising only bad values
*        in argument NBAD.
*     2009 September 6 (MJC):
*        Change to calculate true weighted variance not "population
*        variance" that gave the variance of the mean, and hence was
*        not the desired sample variance for clipping.  Hence the
*        range could be very narrow and in fact could exclude all data
*        leading to a bad pixel.
*     {enter_further_changes_here}

*-

*  Type Definitions:
      IMPLICIT NONE              ! No implicit typing

*  Global Constants:
      INCLUDE 'SAE_PAR'          ! Standard SAE constants
      INCLUDE 'PRM_PAR'          ! PRIMDAT constants

*  Arguments Given:
      REAL NSIGMA
      INTEGER NPIX
      INTEGER NLINES
      <TYPE> STACK( NPIX, NLINES )
      <TYPE> VARS( NPIX, NLINES )
      INTEGER MINPIX
      INTEGER NMAT
      DOUBLE PRECISION COVEC( NMAT, NLINES )

*  Arguments Returned:
      <TYPE> RESULT( NPIX )
      <TYPE> RESVAR( NPIX )
      <TYPE> WRK1( NLINES )
      <TYPE> WRK2( NLINES )
      INTEGER POINT( NLINES )
      LOGICAL USED( NLINES )
      DOUBLE PRECISION NCON( NLINES )
      INTEGER NBAD

*  Status:
      INTEGER STATUS             ! Global status

*  External References:
      EXTERNAL NUM_WASOK
      LOGICAL NUM_WASOK          ! Was numeric operation OK?

      EXTERNAL NUM_TRAP
      INTEGER NUM_TRAP           ! Numerical error handler

*  Local Variables:
      INTEGER I                  ! Loop variable
      INTEGER IGOOD              ! Number of unrejected pixels
      INTEGER J                  ! Loop variable
      INTEGER LBND               ! Position of lowest value higher than
                                 ! rejection threshold
      <LTYPE> LOWER              ! Lower limit
      INTEGER NGOOD              ! Number of good pixels
      DOUBLE PRECISION SAMVAR    ! Sample variance before rejection
      DOUBLE PRECISION SUM1      ! Weight sums
      DOUBLE PRECISION SUM2      ! Weighted value sums
      DOUBLE PRECISION SUM3      ! Weighted squared-value sums
      DOUBLE PRECISION SUM4      ! Sum of squared weights
      DOUBLE PRECISION SVAR      ! Population variance before rejection
      INTEGER UBND               ! Position of highest value lower than
                                 ! rejection threshold
      <LTYPE> UPPER              ! Upper limit
      DOUBLE PRECISION VALUE     ! Single value
      DOUBLE PRECISION VAR       ! Population variance after rejection
      DOUBLE PRECISION WEIGHT    ! Weight
      DOUBLE PRECISION WMEAN     ! Weighted mean

*  Internal References:
      INCLUDE 'NUM_DEC_CVT'      ! NUM_ type conversion functions
      INCLUDE 'NUM_DEF_CVT'      ! Define functions...

*.

*  Initialise returned values.
      NBAD = 0
      DO I = 1, NLINES
         NCON( I ) = 0.0D0
      END DO

*  Check inherited global status.
      IF ( STATUS .NE. SAI__OK ) RETURN

*  Set the numeric error and set error flag value.
      CALL NUM_HANDL( NUM_TRAP )

*  Loop over for all possible output pixels.
      DO 1 I = 1, NPIX
         NGOOD = 0
         CALL NUM_CLEARERR()
         SUM1 = 0.0D0
         SUM2 = 0.0D0

*  Loop over all possible contributing pixels.
         DO 2 J = 1, NLINES
            IF ( STACK( I, J ) .NE. VAL__BAD<T> .AND.
     :            VARS( I, J ) .NE. VAL__BAD<T> .AND.
     :            VARS( I, J ) .GT. VAL__SML<T> ) THEN

*  Increment good value counter.
               NGOOD = NGOOD + 1

*  Update the pointers to the positions of the unextracted data.
               POINT( NGOOD ) = J

*  Change variance to weight.
               WEIGHT = 1.0D0 / NUM_<T>TOD( VARS( I, J ) )
               VALUE = NUM_<T>TOD( STACK( I, J ) )

*  Increment sums.
               SUM1 = SUM1 + WEIGHT
               SUM2 = SUM2 + WEIGHT * VALUE
               SUM3 = SUM3 + WEIGHT * VALUE * VALUE
               SUM4 = SUM4 + WEIGHT * WEIGHT

*  Transfer values to working buffers for the current output pixel.
               WRK1( NGOOD ) = STACK( I, J )
               WRK2( NGOOD ) = NUM_DTO<T>( WEIGHT )

*  Finally trap numeric errors by rejecting all values.
               IF ( .NOT. NUM_WASOK() ) THEN

*  Decrement NGOOD, do not let it go below zero.
                  NGOOD = MAX( 0, NGOOD - 1 )
                  CALL NUM_CLEARERR()
               END IF
            END IF
    2    CONTINUE

*  Continue if more than one good value.
         IF ( NGOOD .GT. 0 ) THEN

*  Form the weighted mean and population variance.
            WMEAN = SUM2 / SUM1
            SVAR = 1.0D0 / SUM1

*  Find the weighted sample variance used for clipping outliers.
            SAMVAR = ( SUM3 * SUM1 - SUM2 * SUM2 ) / ( SUM1 * SUM1 - SUM4 )

*  Sort data prior to rejection -- this is necessary as clip may be
*  non-central for (noisy) skew distributions, so the order statitics
*  summation will be incorrect if we procede assuming that the mean is
*  central and so are the covariances.
            CALL CCG_IS3<T>( WRK1, WRK2, POINT, NGOOD, STATUS )

*  Set the clipping range.
            LOWER = NUM_DTO<T>( WMEAN - DBLE( NSIGMA ) * SQRT( SAMVAR ) )
            UPPER = NUM_DTO<T>( WMEAN + DBLE( NSIGMA ) * SQRT( SAMVAR ) )

*  Decide which orders of data are to be rejected. Pick out the least
*  bound greater than the lower bound and the highest bound less than
*  the upper bound.
            LBND = NGOOD
            UBND = 1
            DO 3 J = 1, NGOOD
               IF ( WRK1( J ) .GT. LOWER ) LBND = MIN( LBND, J )
               IF ( WRK1( J ) .LT. UPPER ) UBND = MAX( UBND, J )
    3       CONTINUE

*  Number of values retained.
            IGOOD = UBND - LBND + 1

*  If any values are retained then form the median.
            IF ( IGOOD .GT. 0 ) THEN
               CALL CCG_WTM4<T>( NGOOD, WRK1, WRK2, SVAR, LBND, UBND,
     :                         COVEC( 1, NGOOD ), USED, WMEAN, VAR,
     :                         STATUS )

*  Update the used line counters.
               DO 4 J = 1, NGOOD
                  IF ( USED( J ) ) THEN
                     NCON( POINT( J ) ) = NCON( POINT( J ) ) + 1.0D0
                  END IF
    4          CONTINUE

*  Trap occasions when all values are rejected.
            ELSE
               NGOOD = 0
            END IF
         END IF

*  If there are sufficient good pixels output the result.
         IF ( NGOOD .GE. MINPIX ) THEN
            RESULT( I ) = NUM_DTO<T>( WMEAN )
            RESVAR( I ) = NUM_DTO<T>( VAR )

*  Trap numeric errors.
            IF ( .NOT. NUM_WASOK() ) THEN
               RESULT( I ) = VAL__BAD<T>
               RESVAR( I ) = VAL__BAD<T>
               NBAD = NBAD + 1
            END IF
         ELSE

*  Not enough contributing pixels, set output invalid unless all of them
*  are bad.
            RESULT( I ) = VAL__BAD<T>
            RESVAR( I ) = VAL__BAD<T>
            IF ( NGOOD .GT. 0 ) NBAD = NBAD + 1
         END IF
    1 CONTINUE

*  Remove the numerical error handler.
      CALL NUM_REVRT

      END
