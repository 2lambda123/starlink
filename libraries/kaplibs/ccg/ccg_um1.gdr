      SUBROUTINE CCG_UM1<T>( NPIX, NLINES, STACK, VARS, MINPIX,
     :                     RESULT, RESVAR, NCON, NBAD, STATUS )
*+
*  Name:
*     CCG_UM1x

*  Purpose:
*     Combines data lines using an unweighted mean.

*  Language:
*     Starlink Fortran 77

*  Invocation:
*     CALL CCG_UM1x( NPIX, NLINES, STACK, VARS, MINPIX,
*                    RESULT, RESVAR, NCON, NBAD, STATUS )

*  Description:
*     This routine accepts an array consisting of a series of
*     (vectorised) lines of data.  The data values in the lines are then
*     combined to form an unweighted mean.  The output means
*     are returned in the array RESULT.  The output variances are
*     estimated from the VARS values and are returned in RESVAR.

*  Arguments:
*     NPIX = INTEGER (Given)
*        The number of pixels in a line of data.
*     NLINES = INTEGER (Given)
*        The number of lines of data in the stack.
*     STACK( NPIX, NLINES ) = ? (Given)
*        The array of lines which are to be combined into a single line.
*     VARS( NPIX, NLINES ) = ? (Given)
*        The data variances.
*     MINPIX = INTEGER (Given)
*        The minimum number of pixels required to contribute to an
*        output pixel or variance.
*     RESULT( NPIX ) = ? (Returned)
*        The output line of data.
*     RESVAR( NPIX ) = ? (Returned)
*        The output variances.
*     NCON( NLINES ) = DOUBLE PRECISION (Returned)
*        The actual number of contributing pixels from each input line
*        to the output line.
*     NBAD = INTEGER (Returned)
*        The number of bad values in the output array.
*     STATUS = INTEGER (Given and Returned)
*        The global status.

*  Notes:
*     -  There is a routine for real and double-precision: replace "x"
*     in the routine name by D or R as appropriate.  The STACK, VARS,
*     RESULT, and RESVAR arguments supplied to the routine must have the
*     data type specified.
*     -  This routine performs its work in double precision.

*  Copyright:
*     Copyright (C) 2001, 2004 Central Laboratory of the Research
*     Councils. Copyright (C) 2005-2006 Particle Physics & Astronomy
*     Research Council. Copyright (C) 2009 Science & Technology
*     Facilities Council. All Rights Reserved.

*  Licence:
*     This program is free software; you can redistribute it and/or
*     modify it under the terms of the GNU General Public License as
*     published by the Free Software Foundation; either Version 2 of
*     the License, or (at your option) any later version.
*
*     This program is distributed in the hope that it will be
*     useful, but WITHOUT ANY WARRANTY; without even the implied
*     warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
*     PURPOSE. See the GNU General Public License for more details.
*
*     You should have received a copy of the GNU General Public License
*     along with this program; if not, write to the Free Software
*     Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA
*     02111-1307, USA.

*  Authors:
*     DSB: David Berry (STARLINK)
*     BRADC: Brad Cavanagh (JAC)
*     MJC: Malcolm J. Currie (STARLINK)
*     {enter_new_authors_here}

*  History:
*     14-DEC-2001 (DSB):
*        Original version.
*     11-OCT-2004 (BRADC):
*        No longer use NUM_CMN.
*     2005 December 27 (MJC):
*        Remove unused variables.
*     2006 December 15 (MJC):
*        Check for valid variance and data value independently.
*     2009 July 4 (MJC):
*        Converted to generic and renamed from CCG_UM1x.  Added NBAD 
*        argument.  Moved STACK to the normal order in the API.  Made NCON 
*        Returned and therefore it is initialised.
*     {enter_further_changes_here}

*-

*  Type Definitions:
      IMPLICIT NONE              ! No implicit typing

*  Global Constants:
      INCLUDE 'SAE_PAR'          ! Standard SAE constants
      INCLUDE 'PRM_PAR'          ! PRIMDAT constants

*  Arguments Given:
      INTEGER NPIX
      INTEGER NLINES
      <TYPE> STACK( NPIX, NLINES )
      <TYPE> VARS( NPIX, NLINES )
      INTEGER MINPIX

*  Arguments Returned:
      <TYPE> RESULT( NPIX )
      <TYPE> RESVAR( NPIX )
      DOUBLE PRECISION NCON( NLINES )
      INTEGER NBAD

*  Status:
      INTEGER STATUS             ! Global status

*  External References:
      EXTERNAL NUM_WASOK
      LOGICAL NUM_WASOK          ! Was numeric operation OK?

      EXTERNAL NUM_TRAP
      INTEGER NUM_TRAP           ! Numerical error handler

*  Local Variables:
      INTEGER I                  ! Loop variable
      INTEGER J                  ! Loop variable
      INTEGER NGOOD              ! Number of good pixels
      INTEGER NGOODV             ! Number of good variances
      DOUBLE PRECISION SUMD      ! Sum of data values
      DOUBLE PRECISION SUMV      ! Sum of variances

*  Internal References:
      INCLUDE 'NUM_DEC_CVT'      ! NUM_ type conversion functions
      INCLUDE 'NUM_DEF_CVT'      ! Define functions...

*.

*  Initialise returned values.
      NBAD = 0
      DO I = 1, NLINES
         NCON( I ) = 0.0D0
      END DO

*  Check inherited global status.
      IF ( STATUS .NE. SAI__OK ) RETURN

*  Set the numeric error and set error flag value.
      CALL NUM_HANDL( NUM_TRAP )

      DO 1 I = 1, NPIX

*  Loop over for all possible output pixels.
         SUMV = 0.0D0
         SUMD = 0.0D0
         NGOOD = 0
         NGOODV = 0
         CALL NUM_CLEARERR()

*  Loop over all possible contributing pixels forming the required 
*  sums.
         DO 5 J = 1, NLINES
            IF ( STACK( I, J ) .NE. VAL__BAD<T> ) THEN

*  Conversion increment good value counter.
               NGOOD = NGOOD + 1

*  Sum the data values.
               SUMD = SUMD + NUM_<T>TOD( STACK( I, J ) )

*  Update the contribution buffer; all values contribute when forming
*  the mean.
               NCON( J ) = NCON( J ) + 1.0D0
            END IF

*  Count the good variances and sum them.
            IF ( VARS( I, J ) .NE. VAL__BAD<T> ) THEN
               NGOODV = NGOODV + 1
               SUMV = SUMV + NUM_<T>TOD( VARS( I, J ) )
            END IF

    5    CONTINUE

*  If there are sufficient good pixels output the result.
         IF ( NGOOD .GE. MINPIX ) THEN
            RESULT( I ) = REAL( SUMD / NGOOD )

*  Trap numeric errors.
            IF ( .NOT. NUM_WASOK() ) THEN
               RESULT( I ) = VAL__BAD<T>
               NBAD = NBAD + 1
            END IF
         ELSE

*  Not enough contributing pixels, set output invalid.
            RESULT( I ) = VAL__BAD<T>
            NBAD = NBAD + 1
         END IF

*  If there are sufficient good variances, form the output variance.
         IF ( NGOODV .GE. MINPIX ) THEN
            RESVAR( I ) = NUM_DTO<T>( SUMV / DBLE( NGOODV * NGOODV ) )

*  Trap numeric errors.
            IF ( .NOT. NUM_WASOK() ) THEN
               RESVAR( I ) = VAL__BAD<T>
            END IF
         ELSE

*  Not enough contributing pixels, set output invalid.
            RESVAR( I ) = VAL__BAD<T>
         END IF

    1 CONTINUE

*  Remove the numerical error handler.
      CALL NUM_REVRT

      END
