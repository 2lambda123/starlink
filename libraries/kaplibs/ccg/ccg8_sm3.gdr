      SUBROUTINE CCG8_SM3<T>( NSIGMA, NPIX, NLINES, STACK, VARS,
     :                     MINPIX, RESULT, WRK1, WRK2, POINT, USED,
     :                     NCON, NBAD, STATUS )
*+
*  Name:
*     CCG8_SM3x

*  Purpose:
*     Combines data lines using a sigma clipped median.

*  Language:
*     Starlink Fortran 77

*  Invocation:
*     CALL CCG8_SM3x( NSIGMA, NPIX, NLINES, STACK, VARS, MINPIX,
*                     RESULT, WRK1, WRK2, POINT, USED, NCON, NBAD,
*                     STATUS )

*  Description:
*     This routine accepts an array consisting a series of
*     (vectorised) lines of data.  The weighted mean and standard
*     deviation of each input column in STACK is then used to estimate
*     the range of values which represent the required sigma
*     clipping.  The standard deviation is derived from the population
*     of values at each position along the lines (cf. each image
*     pixel).  Values outside of this range are then rejected and the
*     resulting output mean values are returned in the array RESULT.
*
*     Note that clipping will not be used when only two or three
*     values are available (unless in the case of 3 values NSIGMA
*     is less than 1.0).

*  Arguments:
*     NSIGMA = REAL (Given)
*        The number of sigma at which to reject data values.
*     NPIX = INTEGER*8 (Given)
*        The number of pixels in a line of data.
*     NLINES = INTEGER*8 (Given)
*        The number of lines of data in the stack.
*     STACK( NPIX, NLINES ) = ? (Given)
*        The array of lines which are to be combined into a single line.
*     VARS( NLINES ) = ? (Given)
*        The variance to to used for each line of data. These are
*        used as inverse weights when forming the mean and do not
*        need to be real variances, as they are not propagated.
*     MINPIX = INTEGER*8 (Given)
*        The minimum number of pixels required to contribute to an
*        output pixel.
*     RESULT( NPIX ) = ? (Returned)
*        The output line of data.
*     WRK1( NLINES ) = ? (Returned)
*        Workspace for calculations.
*     WRK2( NLINES ) = ? (Returned)
*        Workspace for calculations.
*     POINT( NLINES ) = INTEGER*8 (Returned)
*        Workspace to hold pointers to the original positions of the
*        data before extraction and conversion in to the WRK1 array.
*     USED( NLINES ) = LOGICAL (Returned)
*        Workspace used to indicate which values have been used in
*        estimating a resultant value.
*     NCON( NLINES ) = DOUBLE PRECISION (Returned)
*        The actual number of contributing pixels from each input line
*        to the output line.
*     NBAD = INTEGER*8 (Returned)
*        The number of bad values in the output array created while
*        forming the statistics.  It excludes those bad values whose
*        corresponding values along the collapse axis are all bad.
*     STATUS = INTEGER (Given and Returned)
*        The global status.

*  Notes:
*     -  There is a routine for real and double-precision: replace "x"
*     in the routine name by D or R as appropriate.  The STACK, VARS,
*     RESULT, WRK1, and WRK2 arguments supplied to the routine must
*     have the data type specified.

*  Copyright:
*     Copyright (C) 1998, 2004 Central Laboratory of the Research
*     Councils. Copyright (C) 2006 Particle Physics & Astronomy
*     Research Council. Copyright (C) 2009 Science & Technology
*     Facilities Council. All Rights Reserved.

*  Licence:
*     This program is free software; you can redistribute it and/or
*     modify it under the terms of the GNU General Public License as
*     published by the Free Software Foundation; either Version 2 of
*     the License, or (at your option) any later version.
*
*     This program is distributed in the hope that it will be
*     useful, but WITHOUT ANY WARRANTY; without even the implied
*     warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
*     PURPOSE. See the GNU General Public License for more details.
*
*     You should have received a copy of the GNU General Public License
*     along with this program; if not, write to the Free Software
*     Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
*     02110-1301, USA.

*  Authors:
*     PDRAPER: Peter Draper (STARLINK)
*     BRADC: Brad Cavanagh (JAC)
*     MJC: Malcolm J. Currie (STARLINK)
*     {enter_new_authors_here}

*  History:
*     30-JAN-1998 (PDRAPER):
*        Original version.
*     31-JAN-1998 (PDRAPER):
*        Modified to derive a standard deviation estimate from the
*        data values rather than assuming that the input "variances"
*        are typical for each line. This was never the case.
*     11-OCT-2004 (BRADC):
*        No longer use NUM_CMN.
*     2006 August 6 (MJC):
*        Exclude data with non-positive or bad variance.
*     2009 July 3 (MJC):
*         Converted to generic and renamed from CCG8_SMR3R.  Added NBAD
*         argument.  Made VARS take the generic type.  Moved STACK to
*         the normal order in the API.
*     2009 July 4 (MJC):
*         Made NCON Returned and therefore it is initialised.  Move NCON
*         to just before NBAD in API for consistency.
*     2009 July 17 (MJC):
*        Do not include collapsed data comprising only bad values
*        in argument NBAD.
*     {enter_further_changes_here}

*-

*  Type Definitions:
      IMPLICIT NONE              ! No implicit typing

*  Global Constants:
      INCLUDE 'SAE_PAR'          ! Standard SAE constants
      INCLUDE 'PRM_PAR'          ! PRIMDAT constants

*  Arguments Given:
      REAL NSIGMA
      INTEGER*8 NPIX
      INTEGER*8 NLINES
      <TYPE> STACK( NPIX, NLINES )
      <TYPE> VARS( NLINES )
      INTEGER*8 MINPIX

*  Arguments Returned:
      <TYPE> RESULT( NPIX )
      <TYPE> WRK1( NLINES )
      <TYPE> WRK2( NLINES )
      INTEGER*8 POINT( NLINES )
      LOGICAL USED( NLINES )
      DOUBLE PRECISION NCON( NLINES )
      INTEGER*8 NBAD

*  Status:
      INTEGER STATUS             ! Global status

*  External References:
      EXTERNAL NUM_WASOK
      LOGICAL NUM_WASOK          ! Was numeric operation OK?

      EXTERNAL NUM_TRAP
      INTEGER NUM_TRAP           ! Numerical error handler

*  Local Variables:
      DOUBLE PRECISION DEV       ! Deviations from mean
      INTEGER*8 I                ! Loop variable
      INTEGER*8 IGOOD            ! Number of unrejected pixels
      INTEGER*8 J                ! Loop variable
      INTEGER*8 LBND             ! Position of lowest value higher than
                                 ! rejection threshold
      <LTYPE> LOWER              ! Lower limit
      INTEGER*8 NGOOD            ! Number of good pixels
      DOUBLE PRECISION SVAR      ! Population variance before rejection
      DOUBLE PRECISION SUM1      ! Weight sums
      DOUBLE PRECISION SUM2      ! Weighted value sums
      INTEGER*8 UBND             ! Position of highest value lower than
                                 ! rejection threshold
      <LTYPE> UPPER              ! Upper limit
      DOUBLE PRECISION WMEAN     ! Weighted mean

*  Internal References:
      INCLUDE 'NUM_DEC_CVT'      ! NUM_ type conversion functions
      INCLUDE 'NUM_DEF_CVT'      ! Define functions...

*.

*  Initialise returned values.
      NBAD = 0
      DO I = 1, NLINES
         NCON( I ) = 0.0D0
      END DO

*  Check inherited global status.
      IF ( STATUS .NE. SAI__OK ) RETURN

*  Set the numeric error and set error flag value.
      CALL NUM_HANDL( NUM_TRAP )

*  Loop over for all possible output pixels.
      DO 1 I = 1, NPIX
         NGOOD = 0
         CALL NUM_CLEARERR()
         SUM1 = 0.0D0
         SUM2 = 0.0D0

*  Loop over all possible contributing pixels.
         DO 2 J = 1, NLINES
            IF ( STACK( I, J ) .NE. VAL__BAD<T> .AND.
     :           VARS( J ) .NE. VAL__BAD<T> .AND.
     :           VARS( J ) .GT. VAL__SML<T> ) THEN

*  Increment good value counter.
               NGOOD = NGOOD + 1

*  Update the pointers to the positions of the unextracted data.
               POINT( NGOOD ) = J

*  Change variance to weight.
               SVAR = 1.0D0 / NUM_<T>TOD( VARS( J ) )

*  Increment sums.
               SUM2 = SUM2 + NUM_<T>TOD( STACK( I, J ) ) * SVAR
               SUM1 = SUM1 + SVAR

*  Transfer values to working buffers for the current output pixel.
               WRK1( NGOOD ) = STACK( I, J )
               WRK2( NGOOD ) = NUM_DTO<T>( SVAR )

*  Finally trap numeric errors by rejecting all values.
               IF ( .NOT. NUM_WASOK() ) THEN

*  Decrement NGOOD, do not let it go below zero.
                  NGOOD = MAX( 0, NGOOD - 1 )
                  CALL NUM_CLEARERR()
               END IF
            END IF
    2    CONTINUE

*  Continue if more than one good value.
         IF ( NGOOD .GT. 0 ) THEN
            IF ( NGOOD .EQ. 1 ) THEN
               WMEAN = NUM_<T>TOD( WRK1( 1 ) )
            ELSE

*  Form the weighted mean and population variance.
               WMEAN = SUM2 / SUM1
               SUM1 = 0.0D0
               DO 5 J = 1, NGOOD
                  DEV = WMEAN - NUM_<T>TOD( WRK1( J ) )
                  SUM1 = SUM1 + DEV * DEV
    5          CONTINUE
               SVAR = SUM1 / DBLE( NGOOD - 1 )

*  Sort data prior to rejection.  This is necessary as clip may be
*  non-central for (noisy) skew distributions, so the order statitics
*  summation will be incorrect if we procede assuming that the mean is
*  central and so are the covariances.
               CALL CCG8_IS3<T>( NGOOD, WRK1, WRK2, POINT, STATUS )

*  Set the clipping range.
               LOWER = NUM_DTO<T>( WMEAN - DBLE( NSIGMA ) *
     :                            SQRT( SVAR ) )
               UPPER = NUM_DTO<T>( WMEAN + DBLE( NSIGMA ) *
     :                            SQRT( SVAR ) )

*  Decide which orders of data are to be rejected. Pick out the least
*  bound greater than the lower bound and the highest bound less than
*  the upper bound.
               LBND = NGOOD
               UBND = 1
               DO 3 J = 1, NGOOD
                  IF ( WRK1( J ) .GT. LOWER ) LBND = MIN( LBND, J )
                  IF ( WRK1( J ) .LT. UPPER ) UBND = MAX( UBND, J )
    3          CONTINUE

*  Number of values retained.
               IGOOD = UBND - LBND + 1

*  If any values are retained then form the median.
               IF ( IGOOD .GT. 0 ) THEN
                  CALL CCG8_WTM2<T>( IGOOD, WRK1( LBND ), WRK2( LBND ),
     :                            USED, WMEAN, STATUS )

*  Update the used line counters.
                  DO 4 J = 1, IGOOD
                     IF ( USED( J ) ) THEN
                        NCON( POINT( J ) ) = NCON( POINT( J ) ) + 1.0D0
                     END IF
    4             CONTINUE

*  Trap occasions when all values are rejected.
               ELSE
                  NGOOD = 0
               END IF
            END IF
         END IF

*  If there are sufficient good pixels output the result.
         IF ( NGOOD .GE. MINPIX ) THEN
            RESULT( I ) = NUM_DTO<T>( WMEAN )

*  Trap numeric errors.
            IF ( .NOT. NUM_WASOK() ) THEN
               RESULT( I ) = VAL__BAD<T>
               NBAD = NBAD + 1
            END IF
         ELSE

*  Not enough contributing pixels, set output invalid unless all of them
*  are bad.
            RESULT( I ) = VAL__BAD<T>
            IF ( NGOOD .GT. 0 ) NBAD = NBAD + 1
         END IF
    1 CONTINUE

*  Remove the numerical error handler.
      CALL NUM_REVRT

      END
