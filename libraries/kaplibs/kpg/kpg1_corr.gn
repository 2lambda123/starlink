      SUBROUTINE KPG1_CORR<T>( EL, X, Y, XLO, XHI, YLO, YHI, R, N, STATUS )
*+
*  Name:
*     KPG1_CORRx

*  Purpose:
*     Calculates the correlation coefficient between two arrays.

*  Language:
*     Starlink Fortran 77

*  Invocation:
*     CALL KPG1_CORR<T>( EL, X, Y, XLO, XHI, YLO, YHI, R, N, STATUS )

*  Description:
*     The routine calculates the sample Pearson correlation coefficient
*     between the two supplied arrays. The values used from each array
*     may be limited to those falling within a given range.

*  Arguments:
*     EL = INTEGER (Given)
*        Number of array elements to process.
*     X( EL ) = ? (Given)
*        First input array.
*     Y( EL ) = ? (Given)
*        First input array.
*     XLO = ? (Given)
*        The lowest X value to include. See also XHI.
*     XHI = ? (Given)
*        The highest X value to include. All X values are used
*        if XHI is less than or equal to XLO.
*     YLO = ? (Given)
*        The lowest Y value to include. See also YHI.
*     YHI = ? (Given)
*        The highest Y value to include. All Y values are used
*        if YHI is less than or equal to YLO.
*     R = DOUBLE PRECISION (Returned)
*        The correlation coefficient.
*     N = INTEGER (Returned)
*        The number of points used to form the returned correlation
*        coefficient.
*     STATUS = INTEGER (Given and Returned)
*        The global status.

*  Notes:
*     -  There is a routine for each numeric data type: replace "x" in
*     the routine name by D, R, I, W, UW, B or UB as appropriate. The
*     arrays supplied to the routine must have the data type specified.

*  Copyright:
*     Copyright (C) 2011 Science & Technology Facilities Council.
*     All Rights Reserved.

*  Licence:
*     This programme is free software; you can redistribute it and/or
*     modify it under the terms of the GNU General Public License as
*     published by the Free Software Foundation; either Version 2 of
*     the License, or (at your option) any later version.
*
*     This programme is distributed in the hope that it will be
*     useful, but WITHOUT ANY WARRANTY; without even the implied
*     warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
*     PURPOSE.  See the GNU General Public License for more details.
*
*     You should have received a copy of the GNU General Public License
*     along with this programme; if not, write to the Free Software
*     Foundation, Inc., 51, Franklin Street, Fifth Floor, Boston, MA
*     02110-1301, USA.

*  Authors:
*     DSB: David S Berry (JAC, Hawaii)
*     {enter_new_authors_here}

*  History:
*     15-SEP-2011 (DSB):
*        Original version.
*     5-SEP-2013 (DSB):
*        Added argument N.
*     19-MAR-2020 (DSB):
*        Added arguments XLO, XHI, YLO and YHI.
*     {enter_further_changes_here}

*  Bugs:
*     {note_any_bugs_here}

*-

*  Type Definitions:
      IMPLICIT NONE              ! No implicit typing

*  Global Constants:
      INCLUDE 'SAE_PAR'          ! Standard SAE constants
      INCLUDE 'PRM_PAR'          ! PRIMDAT primitive data constants

*  Global Variables:

*  Arguments Given:
      INTEGER EL
      <TYPE> X( EL )
      <TYPE> Y( EL )
      <TYPE> XLO
      <TYPE> XHI
      <TYPE> YLO
      <TYPE> YHI

*  Arguments Returned:
      DOUBLE PRECISION R
      INTEGER N

*  Status:
      INTEGER STATUS             ! Global status

*  Local Variables:
      DOUBLE PRECISION ALIM      ! Largest safe value
      DOUBLE PRECISION DENOM     ! Denominator value
      DOUBLE PRECISION HIX       ! Highest acceptable X value
      DOUBLE PRECISION HIY       ! Highest acceptable Y value
      DOUBLE PRECISION LOX       ! Lowest acceptable X value
      DOUBLE PRECISION LOY       ! Lowest acceptable Y value
      DOUBLE PRECISION SX        ! Sum of X values
      DOUBLE PRECISION SXX       ! Sum of squared X values
      DOUBLE PRECISION SXY       ! Sum of X*Y values
      DOUBLE PRECISION SY        ! Sum of Y values
      DOUBLE PRECISION SYY       ! Sum of squared Y values
      DOUBLE PRECISION VX        ! X value
      DOUBLE PRECISION VY        ! Y value
      INTEGER I                  ! Loop counter for array elements

*.

*  Initialise the returned correlation coefficient and number of
*  points used.
      R = VAL__BADD
      N = 0

*  Check inherited global status.
      IF ( STATUS .NE. SAI__OK ) RETURN

*  Get the upper limit on the magnitude of the values that can be used
*  safely.
      ALIM = SQRT( DBLE( VAL__MAX<T> )/EL )

*  Get the X limits to use.
      HIX = ALIM
      LOX = -ALIM
      IF( XHI .GT. XLO ) THEN
         IF( DBLE( XHI ) .LT. HIX ) HIX = DBLE( XHI )
         IF( DBLE( XLO ) .GT. LOX ) LOX = DBLE( XLO )
      END IF

*  Check Y values?
      HIY = ALIM
      LOY = -ALIM
      IF( YHI .GT. YLO ) THEN
         IF( DBLE( YHI ) .LT. HIY ) HIY = DBLE( YHI )
         IF( DBLE( YLO ) .GT. LOY ) LOY = DBLE( YLO )
      END IF

*  Intialise sums
      SXY = 0.0D0
      SX = 0.0D0
      SY = 0.0D0
      SXX = 0.0D0
      SYY = 0.0D0

*  Loop round every pair of values
      DO I = 1, EL

*  Check both are good and within the acceptable range.
         IF( X( I ) .NE. VAL__BAD<T> .AND. Y( I ) .NE. VAL__BAD<T> ) THEN

*  Convert to double precision and check they are within the acceptable
*  range.
            VX = DBLE( X( I ) )
            VY = DBLE( Y( I ) )
            IF( VX .GE. LOX .AND. VX .LE. HIX .AND.
     :          VY .GE. LOY .AND. VY .LE. HIY ) THEN

*  Increment the sums
               SXY = SXY +VX*VY
               SX = SX +VX
               SY = SY +VY
               SXX = SXX +VX*VX
               SYY = SYY +VY*VY
               N = N + 1
            END IF
         END IF
      END DO

*  Calculate the returned correlation coefficient.
      DENOM = SQRT( N*SXX - SX*SX )*SQRT( N*SYY - SY*SY )
      IF( DENOM .NE. 0.0 ) R = ( N*SXY - SX*SY )/DENOM

      END
