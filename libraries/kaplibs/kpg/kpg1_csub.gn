      SUBROUTINE KPG1_CSUB<T>( BAD, EL, A, CONST, B, NERR, STATUS )
*+
*  Name:
*     KPG1_CSUBx
 
*  Purpose:
*     Subtracts a constant from each element of a vectorised array.
 
*  Language:
*     Starlink Fortran 77
 
*  Invocation:
*     CALL KPG1_CSUBx( BAD, EL, A, CONST, B, NERR, STATUS )
 
*  Description:
*     The routine subtracts a constant from each element of a
*     vectorised array to produce a new array. Bad value checking is
*     performed if required.
 
*  Arguments:
*     BAD = LOGICAL (Given)
*        Whether to check for bad values in the input array.
*     EL = INTEGER (Given)
*        Number of array elements to process.
*     A( EL ) = ? (Given)
*        Input array.
*     CONST = DOUBLE PRECISION (Given)
*        Constant to be subtracted from each array element.
*     B( EL ) = ? (Returned)
*        Output array.
*     NERR = INTEGER (Returned)
*        Number of numerical errors which occurred while processing the
*        array.
*     STATUS = INTEGER (Given and Returned)
*        The global status.
 
*  Notes:
*     -  There is a routine for each numeric data type: replace "x" in
*     the routine name by D, R, I, W, UW, B or UB as appropriate. The
*     arrays supplied to the routine must have the data type specified.
*     -  This routine will handle numerical errors (i.e. overflow) by
*     assigning the appropriate "bad" value to affected output array
*     elements. If the constant supplied cannot be converted to the
*     data type of the arrays without overflow, then all elements of
*     the output array will be assigned this bad value and NERR will
*     return the value of EL.
 
*  Copyright:
*     Copyright (C) 1990 Science & Engineering Research Council.
*     Copyright (C) 1996, 2004 Central Laboratory of the Research
*                   Councils.
*     All Rights Reserved.

*  Licence:
*     This programme is free software; you can redistribute it and/or
*     modify it under the terms of the GNU General Public License as
*     published by the Free Software Foundation; either Version 2 of
*     the License, or (at your option) any later version.
*     
*     This programme is distributed in the hope that it will be
*     useful, but WITHOUT ANY WARRANTY; without even the implied
*     warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
*     PURPOSE.  See the GNU General Public License for more details.
*     
*     You should have received a copy of the GNU General Public License
*     along with this programme; if not, write to the Free Software
*     Foundation, Inc., 59, Temple Place, Suite 330, Boston, MA
*     02111-1307, USA.

*  Authors:
*     RFWS: R.F. Warren-Smith (STARLINK)
*     MJC: Malcolm J. Currie (STARLINK)
*     TIMJ: Tim Jenness (JAC, Hawaii)
*     {enter_new_authors_here}
 
*  History:
*     12-APR-1990 (RFWS):
*        Original version.
*     1996 May 20 (MJC):
*        Replaced LIB$ESTABLISH and LIB$REVERT calls.
*     2004 Oct 1 (TIMJ):
*        Recreate GENERIC version
*        NUM_ERROR should no longer be accessed directly
*     {enter_further_changes_here}
 
*  Bugs:
*     {note_any_bugs_here}
 
*-
 
*  Type Definitions:
      IMPLICIT NONE              ! No implicit typing
 
*  Global Constants:
      INCLUDE 'SAE_PAR'          ! Standard SAE constants
      INCLUDE 'PRM_PAR'          ! PRIMDAT primitive data constants
 
*  Global Variables:
 
*  Arguments Given:
      LOGICAL BAD
      INTEGER EL
      <TYPE> A( EL )
      DOUBLE PRECISION CONST
 
*  Arguments Returned:
      <TYPE> B( EL )
      INTEGER NERR
 
*  Status:
      INTEGER STATUS             ! Global status
 
*  External References:
      EXTERNAL NUM_TRAP
      INTEGER NUM_TRAP           ! Numerical error handler
      <TYPE> VAL_DTO<T>          ! Convert from double precision
      EXTERNAL NUM_WASOK
      LOGICAL NUM_WASOK          ! Was numeric operation ok?
 
*  Local Variables:
      <LTYPE> <T>CON              ! Type converted constant
      INTEGER I                  ! Loop counter for array elements
      LOGICAL ALLBAD             ! Set output array all bad?
 
*  Internal References:
      INCLUDE 'NUM_DEC_CVT'      ! NUM_ type conversion functions
      INCLUDE 'NUM_DEC_<T>'      ! NUM_ arithmetic/maths functions
      INCLUDE 'NUM_DEF_CVT'      ! Define functions...
      INCLUDE 'NUM_DEF_<T>'
 
*.
 
*  Check inherited global status.
      IF ( STATUS .NE. SAI__OK ) RETURN
 
*  Open an error context.
      CALL ERR_MARK
 
*  Convert the constant to be subtracted to the appropriate data type.
      ALLBAD = .FALSE.
      <T>CON = VAL_DTO<T>( .FALSE., CONST, STATUS )
 
*  If conversion failed, then annul the error and note that all the
*  output array elements must be set to the bad value.
      IF ( STATUS .NE. SAI__OK ) THEN
         CALL ERR_ANNUL( STATUS )
         ALLBAD = .TRUE.
      END IF
 
*  End the error context.
      CALL ERR_RLSE
 
*  Set all output elements to the bad value if required.
      IF ( ALLBAD ) THEN
         DO 1 I = 1, EL
            B( I ) = VAL__BAD<T>
1        CONTINUE
         NERR = EL
 
*  Otherwise establish the numerical error handler and initialise the
*  error flag and error count.
      ELSE
         CALL NUM_HANDL( NUM_TRAP )
         CALL NUM_CLEARERR()
         NERR = 0
 
*  No bad values present:
*  =====================
         IF ( .NOT. BAD ) THEN
 
*  Subtract the constant from the array.
            DO 2 I = 1, EL
               B( I ) = NUM_SUB<T>( A( I ), <T>CON )
 
*  Check for numerical errors (i.e. overflow). If present, then assign
*  a bad value to the output array element and count the error. Reset
*  the numerical error flag.
               IF ( .NOT. NUM_WASOK() ) THEN
                  B( I ) = VAL__BAD<T>
                  NERR = NERR + 1
                  CALL NUM_CLEARERR()
               END IF
2           CONTINUE
 
*  Bad values present:
*  ==================
         ELSE
 
*  If the input array element is bad, then so is the output element.
            DO 3 I = 1, EL
               IF ( A( I ) .EQ. VAL__BAD<T> ) THEN
                  B( I ) = VAL__BAD<T>
 
*  Otherwise, subtract the constant, again checking for numerical
*  errors.
               ELSE
                  B( I ) = NUM_SUB<T>( A( I ), <T>CON )
                  IF ( .NOT. NUM_WASOK() ) THEN
                     B( I ) = VAL__BAD<T>
                     NERR = NERR + 1
                     CALL NUM_CLEARERR()
                  END IF
               END IF
3           CONTINUE
         END IF
 
*  Remove the numerical error handler.
         CALL NUM_REVRT
      END IF
 
      END
