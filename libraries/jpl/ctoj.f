C------------------------------- SUBROUTINE 'CTOJ' ---------------------
C
C++++++++++++++++++++++++++++
C
      SUBROUTINE CTOJ(C,JD)
C
C++++++++++++++++++++++++++++
C
C     THIS SUBROUTINE CONVERTS A GREGORIAN CALENDAR DATE TO JULIAN DATE
C
C     CALENDAR FORMAT:  2 INTEGERS:  YYYYMMDD,HHNNSSXX
C       WHERE  YYYY = CALENDAR YEAR (E.G. 1988)
C                MM = CALENDAR MONTH (E.G. 04 = APRIL)
C                DD = CALENDAR DAY (E.G. 15)
C                HH = HOUR OF DAY (00 .LE. HH .LE. 23)
C                NN = MINUTES (00 .LE. NN .LE. 59)
C                SS = SECONDS (00 .LE. SS .LE. 59)
C                XX = HUNDREDTHS OF SECONDS (00 .LE. XX .LE. 99)
C
C     CALLING SEQUENCE:     CALL CTOJ(C,JD)
C
C        C = INTEGER 2-WORD ARRAY CONTAINING THE INPUT CALENDAR DATE
C
C       JD = THE OUTPUT D.P. JULIAN DATE
C
C
C     CALLING SEQUENCE DECLARATIONS:
C
      INTEGER C(2)
      DOUBLE PRECISION JD
C
C
C       CALCULATE THE NUMBER OF FULL YEARS, MONTHS, AND DAYS ALREADY
C       ELAPSED SINCE 0H, MARCH 1, 4001 BC (UP TO MOST RECENT MIDNIGHT).
C       (THE '91' IS COMPLEMENT ARITHMETIC FOR ADDING 9 TO 'MM'.)
C
      NYMD=40000000+(MOD(IABS(C(1)),10000)-9101)+C(1)/10000*10000
      IF(C(1).LT.0) NYMD=NYMD+10000
C
C       CONVERT 2ND WORD OF CALENDAR DATE AND ELAPSED DAYS TO
C       D.P. DAYS, AND ADD .5 FOR JD CONVENTION
C
      JD=DBLE(MOD(C(2)/100,100)+MOD(C(2)/10000,100)*60
     * +(C(2)/1000000)*3600+MOD(NYMD,100)*86400)/86400.D0+.5D0
C
C       NUMBER OF FULL MONTHS ELAPSED SINCE THE CURRENT OR MOST RECENT MARCH
C
      MTH=MOD(MOD(NYMD/100,100),12)
C
C       NUMBER OF FULL 12-MONTH YEARS SINCE 3/1/-1
C
      IY=NYMD/10000+MOD(NYMD/100,100)/12
C
C       STARTING WITH MARCH AS MONTH 0 AND ENDING WITH THE FOLLOWING
C       FEBRUARY AS MONTH 11, THE CALCULATION OF THE NUMBER OF DAYS
C       PER MONTH REDUCES TO A SIMPLE FORMULA. THE FOLLOWING STATEMENT
C       DETERMINES THE NUMBER OF WHOLE DAYS ELAPSED SINCE 3/1/4001 BC AND
C       THEN ADDS THE 260149 DAYS BETWEEN THEN AND THE START OF THE
C       JULIAN DATE NUMBERING SYSTEM.
C
      JD=DBLE(MOD(C(2),100))/8640000.D0
     * +JD+DBLE((MTH+1+MTH/6+MTH/11)/2*31+(MTH-MTH/6-MTH/11)/2*30
     * +IY*365+IY/4-IY/100+IY/400+260149)
C
      RETURN
C
      END
