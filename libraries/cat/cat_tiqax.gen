      SUBROUTINE CAT_TIQA<T> (GI, ATTRIB, VALUE, STATUS)
*+
*  Name:
*     CAT_TIQA<T>
*  Purpose:
*     Inquire the value of a single attribute for a component.
*  Language:
*     Fortran 77.
*  Invocation:
*     CALL CAT_TIQA<T> (GI, ATTRIB; VALUE<T>; STATUS)
*  Description:
*     Inquire the value of a single attribute for a component.  If the
*     value of an array is inquired, the value of the first element is
*     returned.
*  Arguments:
*     GI  =  INTEGER (Given)
*        Generic component identifier.
*     ATTRIB  =  CHARACTER*(*) (Given)
*        Name of the attribute of the component.
*     VALUE  =  <TYPE> (Returned)
*        Value of the named attribute.
*     STATUS  =  INTEGER (Given and Returned)
*        The global status.
*  Algorithm:
*     If the identifier is valid then
*       Attempt to locate the attribute.
*       If it is found then
*         Determine its type.
*         Obtain its value.
*         Perform any necessary type conversion.
*         If an error occurred then
*           Set the status.
*         end if
*       else
*         Set the status.
*       end if
*     else
*       Set the status.
*     end if
*     Report any error.
*  Copyright:
*     Copyright (C) 1999 Central Laboratory of the Research Councils
*  Licence:
*     This program is free software; you can redistribute it and/or
*     modify it under the terms of the GNU General Public License as
*     published by the Free Software Foundation; either version 2 of
*     the License, or (at your option) any later version.
*
*     This program is distributed in the hope that it will be
*     useful,but WITHOUT ANY WARRANTY; without even the implied
*     warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
*     PURPOSE. See the GNU General Public License for more details.
*
*     You should have received a copy of the GNU General Public License
*     along with this program; if not, write to the Free Software
*     Foundation, Inc., 51 Franklin Street,Fifth Floor, Boston, MA
*     02110-1301, USA

*  Authors:
*     ACD: A C Davenhall (Leicester)
*  History:
*     3/5/93   (ACD): Prologue only.
*     24/6/93  (ACD): First implementation.
*     11/10/93 (ACD): First stable version.
*     24/1/94  (ACD): Modified error reporting.
*     8/2/94   (ACD): Changed parameters for data type codes.
*     14/8/96  (ACD): Remove non-printable characters from returned
*        CHARACTER attributes.
*     27/5/98  (ACD): Speeded up search for the attribute.
*  Bugs:
*     None known
*-
*  Type Definitions:
      IMPLICIT NONE
*  Global Constants:
      INCLUDE 'CAT_PAR'           ! External CAT constants.
      INCLUDE 'CAT1_PAR'          ! Internal CAT constants.
      INCLUDE 'CAT_ERR'           ! CAT error codes.
*  Global Variables:
      INCLUDE 'CAT1_IDS_CMN'      ! Identifiers common block.
      INCLUDE 'CAT1_ATTRB_CMN'    ! Attributes common block.
*  Arguments Given:
      INTEGER
     :  GI
      CHARACTER*(*)
     :  ATTRIB
*  Arguments Returned:
      <TYPE>
     :  VALUE
*  Status:
      INTEGER STATUS             ! Global status
*  Local Variables:
      INTEGER
     :  LOOP,   ! Loop index.
     :  ATELM,  ! Element in attribute arrays.
     :  ATYPE   ! Data type for attribute.
      LOGICAL
     :  FOUND,  ! Flag: has the attribute been found?
     :  MORE,   ! Flag; continue hunting for attribute?
     :  CONVOK  ! Flag: error in the type conversion?

*
*    The following variables hold the value obtained for the named
*    attribute in the data type of the attribute.  In a given invocation
*    only the variable corresponding to the data type of the attribute
*    is used.

      BYTE             VALUB ! Unsigned byte value.
      BYTE             VALB  ! Byte value.
      INTEGER*2        VALUW ! Unsigned word value.
      INTEGER*2        VALW  ! Word value.
      INTEGER          VALI  ! Integer value.
      REAL             VALR  ! Real value.
      DOUBLE PRECISION VALD  ! Double precision value.
      LOGICAL          VALL  ! Logical value.
      CHARACTER        VALC*(CAT1__SZATS)   ! Character value.

*
*    The following variables hold a local of the returned value,
*    converted to data type <T>.  In the instantiation of the routine
*    of type <T> only the variable LVAL<T> is used.  The purpose of
*    having the variables at all is to convert the local fixed length
*    string to the returned fixed length string.

      BYTE      LVALUB
      BYTE      LVALB
      INTEGER*2 LVALUW
      INTEGER*2 LVALW
      INTEGER   LVALI
      REAL      LVALR
      DOUBLE PRECISION LVALD
      LOGICAL   LVALL
      CHARACTER LVALC*(CAT1__SZATS)
*.

      IF (STATUS .EQ. CAT__OK) THEN

C        write(17, 1000) gi, attrib, status
C1000    format(1x, 'TIQA<T>: gi, attrib, status: ',
C    :     i5, 1x, a, 1x, i10 )

*
*       Check that the identifier is valid.

         IF (GI .GT. 0  .AND.  GI .LE. NIDS__CAT1) THEN

*
*          Attempt to locate the attribute.

            FOUND = .FALSE.
            MORE = .TRUE.
            LOOP = IDATT__CAT1(GI) - 1

            DO WHILE (MORE)
               LOOP = LOOP + 1

               IF (GI .EQ. ATTID__CAT1(LOOP)  .AND.
     :             ATTRIB .EQ. ATTNM__CAT1(LOOP) ) THEN
                  ATELM = LOOP
                  FOUND = .TRUE.
                  MORE = .FALSE.
               END IF

               IF (LOOP .GE. NATT__CAT1) THEN
                  MORE = .FALSE.
               END IF
            END DO

*
*          Proceed if the attribute has been found.

            IF (FOUND) THEN

*
*             Determine the type of the attribute and obtain its value.

               ATYPE = ATTYP__CAT1(ATELM)

               IF (ATYPE .EQ. CAT__TYPEUB) THEN
                  VALUB = ATTVV__CAT1(ATELM)

               ELSE IF (ATYPE .EQ. CAT__TYPEB) THEN
                  VALB = ATTVB__CAT1(ATELM)

               ELSE IF (ATYPE .EQ. CAT__TYPEUW) THEN
                  VALUW = ATTVU__CAT1(ATELM)

               ELSE IF (ATYPE .EQ. CAT__TYPEW) THEN
                  VALW = ATTVW__CAT1(ATELM)

               ELSE IF (ATYPE .EQ. CAT__TYPEI) THEN
                  VALI = ATTVI__CAT1(ATELM)

               ELSE IF (ATYPE .EQ. CAT__TYPER) THEN
                  VALR = ATTVR__CAT1(ATELM)

               ELSE IF (ATYPE .EQ. CAT__TYPED) THEN
                  VALD = ATTVD__CAT1(ATELM)

               ELSE IF (ATYPE .EQ. CAT__TYPEL) THEN
                  VALL = ATTVL__CAT1(ATELM)

               ELSE IF (ATYPE .EQ. CAT__TYPEC) THEN
                  VALC = ATTVC__CAT1(ATELM)
                  CALL CHR_CLEAN(VALC)

               END IF

*
*             Perform the type conversion.

               CALL CAT1_TCNVT (ATYPE, VALUB, VALB, VALUW, VALW, VALI,
     :           VALR, VALD, VALL, VALC,
     :           CAT__TYPE<T>, LVALUB, LVALB, LVALUW, LVALW, LVALI,
     :           LVALR, LVALD, LVALL, LVALC, CONVOK, STATUS)

               VALUE = LVAL<T>

               IF (STATUS .NE. CAT__OK) THEN
                  STATUS = CAT__TYPCV
               END IF

            ELSE
               STATUS = CAT__NOATT
            END IF

         ELSE
            STATUS = CAT__INVID
         END IF

*
*       Report any error.

         IF (STATUS .NE. CAT__OK) THEN
            CALL CAT1_ERREP ('CAT_TIQA<T>_ERR', 'CAT_TIQA<T>: error '/
     :        /'inquiring the value of an attribute.', STATUS)
         END IF

      END IF

      END
