      SUBROUTINE FIO1_SERR( SYSERR, STATUS )
*+
*  Name:
*     FIO1_SERR

*  Purpose:
*     Set error status

*  Language:
*     Starlink Fortran 77

*  Invocation:
*     CALL FIO1_SERR( SYSERR, STATUS )

*  Description:
*     Get the error number from the operating system and convert it
*     into an FIO error.

*  Arguments:
*     SYSERR = INTEGER (Given)
*        Variable containing the operating system error number.
*     STATUS = INTEGER (Given and Returned)
*        The global status.
*        This is set to the FIO status.

*  Notes:
*     -  This is the Linux version.
*     -  No errors are reported by this routine. They are reported by
*        FIO_SERR.
*     -  Consequently, this routine should only be called from FIO_SERR.

*  Algorithm:
*     -  Handle values of SYSERR of zero or minus one specially.
*     -  Otherwise, set STATUS to a value based on STATUS being an errno.

*  Machine-specific features used:
*     -  Under Linux IO status values <100 are the C errno value.
*        >=100 are Fortran specific errors defines in the libf2c source.

*  Copyright:
*     Copyright (C) 1996 Council for the Central Laboratory of the
*                        Research Councils

*  Authors:
*     BKM: Brian McIlwrath (Starlink, RAL)
*     AJC: Alan Chipperfield (Starlink, RAL)
*     {enter_new_authors_here}

*  History:
*     18-MAR-1996 (BKM):
*        Original Linux version.
*      8-NOV-2000 (AJC):
*        Change FIO_NOTFD to FIO_FILNF  (conform to other platforms)
*     {enter_further_changes_here}

*  Bugs:
*     {note_any_bugs_here}

*-

*  Type Definitions:
      IMPLICIT NONE              ! No implicit typing

*  Global Constants:
      INCLUDE 'SAE_PAR'          ! Standard SAE constants
      INCLUDE 'FIO_ERR'          ! FIO error numbers

*  Arguments Given:
      INTEGER SYSERR             ! Operating system error number

*  Status:
      INTEGER STATUS             ! Global status

*  Local Constants:
      INTEGER MXENO              ! Values <MXENO are Linux ERRNOs
      PARAMETER( MXENO = 99 )
      INTEGER MXFER              ! Maximum value for FORTRAN IO errors
      PARAMETER( MXFER = 129 )

*  Local Variables:
      INTEGER FIOERN( MXFER - MXENO ) ! Table of FIO error codes

*  Local Data:
      DATA FIOERN/
     :   FIO__ERFMT, FIO__ILLUN, FIO__FIONA, FIO__UIONA, FIO__DIONA,
     :   FIO__SIONA, FIO__CNTBF, FIO__NAMER, FIO__CNTSF, FIO__UNTNC,
     :   FIO__OFEOR, FIO__TRUNF, FIO__ILSTI, FIO__OOFSP, FIO__UNTNC,
     :   FIO__RUNCH, FIO__BLINP, FIO__FORVR, FIO__SYNAM, FIO__IINAM,
     :   FIO__UNKNO, FIO__FORVR, FIO__INVRV, FIO__INVRV, FIO__INVRV,
     :   FIO__INVRV, FIO__RDER,  FIO__WRTER, FIO__NFEXI, FIO__WRTER
     :/
*.

*  Check inherited global status.
      IF ( STATUS .NE. SAI__OK ) RETURN

*  If SYSERR is zero, return a status value of SAI__OK.
      IF ( SYSERR .EQ. 0 ) THEN
         STATUS = SAI__OK
         GOTO 999

*  Is the error number set to end of file?
      ELSE IF ( SYSERR .EQ. -1 ) THEN
         STATUS = FIO__EOF
         GOTO 999

*  Linux system errors.
      ELSE IF ( SYSERR .GT. 0 .AND. SYSERR .LE. MXENO ) THEN

*  This should really be a C routine and obtain the ERRNO translation
*  by proper means. In practice, however, the relevant ERRNO values
*  are unlikely to ever change meaning.

*  Check for specific values we can translate
         IF ( SYSERR .EQ. 2 ) THEN         ! No such file or directory
            STATUS = FIO__FILNF
         ELSE IF ( SYSERR .EQ. 13 ) THEN   ! Permission denied
            STATUS = FIO__PTAFD
         ELSE IF ( SYSERR .EQ. 17 ) THEN   ! File exists
            STATUS = FIO__NFEXI
         ELSE IF ( SYSERR .EQ. 19 ) THEN   ! No such device
            STATUS = FIO__NODEV
         ELSE IF ( SYSERR .EQ. 24 ) THEN   ! Too many open files
            STATUS = FIO__TOMNY
         ELSE IF ( SYSERR .EQ. 27 ) THEN   ! File too large
            STATUS = FIO__FTOOL
         ELSE IF ( SYSERR .EQ. 28 ) THEN   ! No space left on device
            STATUS = FIO__NSLOD
         ELSE IF ( SYSERR .EQ. 30 ) THEN   ! Read-only file system
            STATUS = FIO__REDON
         ELSE IF ( SYSERR .EQ. 36 ) THEN   ! File name too long
            STATUS = FIO__FNTL
         ELSE
            STATUS = FIO__OSERR            ! General OS error
         END IF

*   FIO errors
      ELSE IF (SYSERR .LE. MXFER ) THEN
         STATUS = FIOERN( SYSERR - MXENO )

      ELSE
*  No conversion was done.
      STATUS = FIO__ERROR

      END IF

  999 CONTINUE
      END
