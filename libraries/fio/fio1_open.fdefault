      SUBROUTINE FIO1_OPEN( UNIT, FILE, FSTAT, FORMAT, ACCESS, RDONLY,
     :   CCNTL, CC, RECLEN, RECSZ, STATUS )
*+
*  Name:
*     FIO1_OPEN

*  Purpose:
*     Do the actual Fortran OPEN statement

*  Language:
*     Starlink Fortran 77

*  Invocation:
*     CALL FIO1_OPEN( UNIT, FILE, FSTAT, FORMAT, ACCESS, RDONLY,
*    :   CCNTL, CC, RECLEN, RECSZ, STATUS )

*  Description:
*     Execute the actual Fortran OPEN statement. This is isolated in a
*     subroutine as it contains machine dependent code.

*  Arguments:
*     UNIT = INTEGER (Given)
*        The FORTRAN unit number to be opened
*     FILE = CHARACTER * ( * ) (Given)
*        The name of the file to be opened.
*     FSTAT = CHARACTER * ( * ) (Given)
*        The open status of the file, i.e. the "STATUS=" option.
*     FORMAT = CHARACTER * ( * ) (Given)
*        The format of the file (FORMATTED or UNFORMATTED)
*     ACCESS = CHARACTER * ( * ) (Given)
*        The access mode (SEQUENTIAL, DIRECT or APPEND)
*     RDONLY = LOGICAL (Given)
*        Is the file to be opened read-only?
*     CCNTL = LOGICAL (Given)
*        Is the CARRIAGECONTROL option to be used?
*     CC = character * ( * ) (Given)
*        The value of the CARRIAGECONTROL option.
*     RECLEN = LOGICAL (Given)
*        Is the RECLEN option to be used?
*     RECSZ = INTEGER (Given)
*        The record length used when opening the file.
*     STATUS = INTEGER (Given and Returned)
*        The global status.

*  Algorithm:
*     -  Open the file
*     -  If the access mode is APPEND, read to the end of the file.
*     -  Handle any error.
      
*  Machine-specific features used:
*     -  There is no machine specific code as this is the version
*        of the routine for use on previously unsupported architectures.

*  Copyright:
*     Copyright (C) 1993 Science & Engineering Research Council

*  Authors:
*     PMA: Peter Allan (Starlink, RAL)
*     {enter_new_authors_here}

*  History:
*     10-JUN-1993 (PMA):
*        Original version.
*     {enter_further_changes_here}

*  Bugs:
*     {note_any_bugs_here}

*-
      
*  Type Definitions:
      IMPLICIT NONE              ! No implicit typing

*  Global Constants:
      INCLUDE 'SAE_PAR'          ! Standard SAE constants

*  Arguments Given:
      CHARACTER * ( * ) ACCESS
      CHARACTER * ( * ) CC
      CHARACTER * ( * ) FILE
      CHARACTER * ( * ) FORMAT
      CHARACTER * ( * ) FSTAT
      INTEGER RECSZ
      INTEGER UNIT
      LOGICAL CCNTL
      LOGICAL RDONLY
      LOGICAL RECLEN

*  Status:
      INTEGER STATUS             ! Global status

*  Local Variables:
      INTEGER SYSERR             ! The Fortran I/O status value

*.

*  Check inherited global status.
      IF ( STATUS .NE. SAI__OK ) RETURN

*  Open the file.
      OPEN( UNIT=UNIT, FILE=FILE, STATUS=FSTAT, FORM=FORMAT,
     :   ACCESS='SEQUENTIAL', ERR=90, IOSTAT=SYSERR )

*  If ACCESS='APPEND' was requested, read to the end of the file.
      IF ( ACCESS .EQ. 'APPEND' ) THEN
         IF ( FORMAT .EQ. 'FORMATTED' ) THEN
   10       CONTINUE
               READ ( UNIT, *, END = 30 )
            GOTO 10
         ELSE
   20       CONTINUE
               READ ( UNIT, END = 30 )
            GOTO 20
         END IF
      END IF
   30 CONTINUE
      GOTO 999

*  Handle any error condition.
   90 CALL FIO_SERR( SYSERR, STATUS )
      CALL FIO_PUNIT( UNIT, STATUS )

  999 CONTINUE
      END
