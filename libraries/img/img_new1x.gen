      SUBROUTINE IMG_NEW1<T>( PARAM, NX, IP, STATUS )
*+
*  Name:
*     IMG_NEW1x

*  Purpose:
*     Creates a new 1-dimensional image of a specific type.

*  Language:
*     Starlink Fortran 77

*  Invocation:
*     CALL IMG_NEW1x( PARAM, NX, IP, STATUS )

*  Description:
*     This routine creates a new 1-dimensional image, with a specified
*     numeric type, and returns a pointer to its data.

*  Arguments:
*     PARAM = CHARACTER * ( * ) (Given)
*        Parameter name (case insensitive).
*     NX = INTEGER (Given)
*        Size of the image (in pixels).
*     IP = INTEGER (Returned)
*        Pointer to the image data.
*     STATUS = INTEGER (Given and Returned)
*        The global status.

*  Notes:
*     - There is a version of this routine for each numeric type,
*     obtained by replacing the "x" in the routine name by D, R, I, W,
*     UW, B or UB as appropriate.
*     
*     - This routine may create more than one new image at a time by
*     using multiple parameter names. Multiple names are specified by
*     supplying a comma separated list (i.e. 'NEW1,NEW2'). A pointer to
*     the data of each image is then returned (in this case the IP
*     argument must be passed as an array of size at least the number
*     of parameter names). An advantage of this method is that multiple
*     images can be made using a single invocation of this routine.

*  Copyright:
*     Copyright (C) 1992, 1994 Science & Engineering Research Council.
*     All Rights Reserved.

*  Licence:
*     This program is free software; you can redistribute it and/or
*     modify it under the terms of the GNU General Public License as
*     published by the Free Software Foundation; either version 2 of
*     the License, or (at your option) any later version.
*     
*     This program is distributed in the hope that it will be
*     useful,but WITHOUT ANY WARRANTY; without even the implied
*     warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
*     PURPOSE. See the GNU General Public License for more details.
*     
*     You should have received a copy of the GNU General Public License
*     along with this program; if not, write to the Free Software
*     Foundation, Inc., 59 Temple Place,Suite 330, Boston, MA
*     02111-1307, USA

*  Authors:
*     RFWS: R.F. Warren-Smith (STARLINK, RAL)
*     PDRAPER: Peter Draper (STARLINK - Durham University)
*     {enter_new_authors_here}

*  History:
*     25-FEB-1992 (RFWS):
*        Original version.
*     18-AUG-1994 (PDRAPER):
*        Extended to use multiple parameter names.
*     {enter_further_changes_here}

*  Bugs:
*     {note_any_bugs_here}

*-
      
*  Type Definitions:
      IMPLICIT NONE              ! No implicit typing

*  Global Constants:
      INCLUDE 'SAE_PAR'          ! Standard SAE constants
      INCLUDE 'IMG_CONST'        ! IMG_ private constants

*  Arguments Given:
      CHARACTER * ( * ) PARAM
      INTEGER NX

*  Arguments Returned:
      INTEGER IP( * )

*  Status:
      INTEGER STATUS             ! Global status

*  External References:
      EXTERNAL IMG1_OK
      LOGICAL IMG1_OK            ! Test if error status is OK

*  Local Constants:
      INTEGER NDIM               ! Number of NDF dimensions
      PARAMETER ( NDIM = 1 )

*  Local Variables:
      CHARACTER * ( 16 ) COMM    ! Numeric type description
      INTEGER DIM( NDIM )        ! NDF dimension sizes

*.

*  Set an initial null value for the first returned pointer.
      IP( 1 ) = IMG__NOPTR

*  Check inherited global status.
      IF ( STATUS .NE. SAI__OK ) RETURN

*  Assign the NDF dimension.
      DIM( 1 ) = NX

*  Create new NDF(s).
      CALL IMG1_NWNDF( PARAM, '<HTYPE>', NDIM, DIM, IP, STATUS )

*  If an error occurred, then report a contextual message.
      IF ( .NOT. IMG1_OK( STATUS ) ) THEN
         COMM = '<COMM>'
         CALL CHR_LCASE( COMM )
         CALL MSG_SETC( 'COMM', COMM )
         IF ( INDEX( PARAM, ',' ) .NE. 0 ) THEN 
            CALL ERR_REP( 'IMG_NEW1<T>_ERR',
     :           'IMG_NEW1<T>: Error creating new 1-dimensional ' //
     :           'images with a numeric type of ^COMM.', STATUS )
         ELSE
            CALL ERR_REP( 'IMG_NEW1<T>_ERR',
     :           'IMG_NEW1<T>: Error creating a new 1-dimensional ' //
     :           'image with a numeric type of ^COMM.', STATUS )
         END IF
      END IF

      END
* $Id$
