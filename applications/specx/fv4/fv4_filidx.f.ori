      SUBROUTINE FV4_FILIDX( IFILE, IFAIL )
      ENTRY          INDXFL( IFILE, IFAIL )
*+
*  Name:
*     FV4_FILIDX

*  Purpose:
*     List spectra in a file of spectra for Specx.

*  Language:
*     Starlink Fortran 77

*  Invocation:
*     CALL FV4_FILIDX( IFILE, IFAIL )

*  Description:
*     This routine serves the INDEX-FILE command of Specx. It lists
*     short or long format information for each (non-deleted) spectrum
*     in an open file of spectra (format version 4).

*  Arguments:
*     IFILE = INTEGER (Given)
*        The internal file number.
*     IFAIL = INTEGER (Given and Returned)
*        The global status.

*  Authors:
*     hme: Horst Meyerdierks (UoE, Starlink)
*     rpt: Remo Tilanus (JAC, Hilo)
*     {enter_new_authors_here}

*  History:
*     07 Dec 1993 (hme):
*        Original version.
*     10 May 1995 (rpt): 
*        Added support for ISEQ and FILHD
*     {enter_further_changes_here}

*  Bugs:
*     {note_any_bugs_here}

*-
      
*  Type Definitions:
      IMPLICIT NONE              ! No implicit typing

*  Global Constants:
      INCLUDE 'SAE_PAR'          ! Standard SAE constants
      INCLUDE 'DAT_PAR'          ! Standard DAT constants

*  Global Variables:
      INCLUDE 'FLAGCOMM'         ! List file unit ILOUT
      INCLUDE 'FILES'            ! Open files information

*  FILHD Variables:
      INCLUDE 'FILHD'            ! NSCAN, NAME, ID, VERSION, IREC1

*  Arguments Given:
      INTEGER IFILE

*  Status:
      INTEGER IFAIL              ! Global status

*  Local Variables:
      INTEGER ISCAN              ! Loop index
      INTEGER JDEF               ! Returned by GEN_*
      INTEGER LENGTH             ! 1/2 for short/long format
      INTEGER NTICKS             ! Current date and time
      INTEGER STATUS             ! Starlink status
      CHARACTER * ( 1 ) ILS      ! Short/long format switch
      CHARACTER * ( 24 ) TIMNOW  ! Current date and time
      CHARACTER * ( DAT__SZLOC ) TLOC( 3 ) ! HDS locators

*.

*  Check inherited global status.
      IF ( IFAIL .NE. 0 ) RETURN

*  Begin a new Starlink error context.
      STATUS = SAI__OK
      CALL ERR_MARK

*  Find out about short or long format.
      ILS = 'S'
      WRITE( *, * )
     :   'Listing of spectrum headers on logical unit ', ILOUT
      CALL GEN_GETSTR( 'Long or short listings? (L/S)',
     :   ILS, ' ', ILS, JDEF )
      CALL CHR_UCASE( ILS )
      IF ( ILS .EQ. 'L' ) THEN
         LENGTH = 2
      ELSE
         LENGTH = 1
      END IF

*  Get information from file header.
      CALL FV4_FILINF( IFILE, IFAIL ) 
      IF ( IFAIL .NE. 0 ) THEN
         IFAIL = 38
         GO TO 500
      END IF

*  Find out current time.
      CALL PSX_TIME( NTICKS, STATUS )
      CALL PSX_CTIME( NTICKS, TIMNOW, STATUS )
      IF ( STATUS .NE. SAI__OK ) THEN
         IFAIL = 18
         GO TO 500
      END IF

*  Write file header information.
      WRITE( ILOUT, 101 ) FILNAMS(IFILE), ID, NAME, NSCAN, TIMNOW
 101  FORMAT( // ' INDEX LISTING OF FILE: ', A //
     :   ' File title: ', A /
     :   ' Owner:      ', A /
     :   ' No. of scans in file =', I4 /
     :   ' Date of listing:      ', A // )

*  Write table heading.
      IF ( LENGTH .EQ. 2 ) THEN
         WRITE( ILOUT, 102 )
      ELSE
         WRITE( ILOUT, 103 )
      END IF
 102  FORMAT(
     :   '  Seq      Title                ',
     :   '  R.A.  (1950.0)  Dec.     Offsets     Date      ',
     :   'Time    IQ  NPT     FCen       FInc      INTT  El' /
     :   '  --- --------------------------',
     :   ' ----------------------    -------     ----      ',
     :   '----    --  ---     ----       ----      ----  --', / )
 103  FORMAT(
     :   '  Seq Mode  Title     NQD NPT  IQ      Fcen',
     :   '        Finc     INTT     El' /
     :   '  --- ----  -----     --- ---  --      ----',
     :   '        ----     ----     --' / )

*  If no scans in file.
      IF ( NSCAN .LE. 0 ) THEN
         WRITE ( ILOUT, * ) '  -- FV4_FILIDX -- No scans in file.'
         GO TO 500
      END IF

*  Loop through file, enquiring each scan in turn.
      DO 1001 ISCAN = 1, NSCAN

*     Locate the cell's SPECX extension.
         CALL DAT_CELL( SPXLOC(IFILE), 1, ISCAN, TLOC(1), STATUS )
         CALL DAT_FIND(  TLOC(1), 'MORE',  TLOC(2), STATUS )
         CALL DAT_FIND(  TLOC(2), 'SPECX', TLOC(3), STATUS )
         CALL DAT_ANNUL( TLOC(1), STATUS )
         CALL DAT_ANNUL( TLOC(2), STATUS )

*     Get and write header information.
         CALL FV4_SPECIX( TLOC(3), ISCAN, LENGTH, ILOUT, IFAIL, STATUS )
         CALL DAT_ANNUL( TLOC(3), STATUS )
         IF ( IFAIL .NE. 0 ) GO TO 500
 1001 CONTINUE

*  Tidy up.
 500  CONTINUE
      IF ( STATUS .NE. SAI__OK ) CALL ERR_FLUSH( STATUS )
      CALL ERR_RLSE

*  Return.
      END
