declare fscan     i4
declare lscan     i4
declare kscan     i4
declare spxfile   i4
declare fitsname  c21
declare cscan     c4
declare csscan    c4
declare mixid     c1
declare fmode     l4

write fitsname 'xxxxxxxxxxxxxxxxxxxxx'

print ' '
print 'TOFITS> This utility expects the scans to be in a SPECX file'
print 'TOFITS> It will read those scans and write them to disk as'
print 'TOFITS> fitsfiles: obs_jcmt_####.fits'
print 'TOFITS> #### can be set to be the original scan number or'
print 'TOFITS> the sequence number in the specx file.'
print ' '

! SPECX file to get spectra from
spxfile = 1
ask 'File to read? [1]:' spxfile ?
set-fil-ac \spxfile\rw\

! First spectrum to convert to FITS
fscan = 1
ask 'First scan? [1]:' fscan ?

! Last specctrum to convert to fits
info-file \spxfile\
lscan = no_file_spectra
print 'TOFITS> Assuming last scan to be ',lscan
ask 'Last scan? [last]:    ' lscan ?

! Give file name option
fmode = true
ask 'Use orig. scannr (else seqnr)?' fmode ?

! Give file name option
mixid = 'a'
ask 'Mixer id a,b,..? [a]' mixid ?
if (mixid = '' or mixid = ' ') then
  mixid = 'a'
endif

! Loop over spectra in file
do kscan fscan lscan

   ! Read spectrum
   read-sp \spxfile\kscan\

   ! Construct file name
   cscan = scan_title
   csscan = subscan_no
   fitsname = ' '
   if (fmode) then
     read scan_no cscan
     write fitsname 'jcmt_',cscan,'_',csscan,'_',mixid,'.fits'
   else
     write fitsname 'jcmt_',kscan:i4.4,'_',mixid,'.fits'
   endif     

   ! Actually write file
   open-fits-file \fitsname\ \
   write-fits-spectrum
   close-fits-file

   ! Inform user
   print 'TOFITS>    ',kscan:i4.4,'    ',cscan,'   ',fitsname,'    Written'
enddo
