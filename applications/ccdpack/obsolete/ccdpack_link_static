#!/bin/csh -f
#
#  Procedure to statically link ccdpack against ADAM libraries
#  checking for existence of all libraries in static form.
#  Output from this script is via standard output.
#
#  Stop failure to match a pattern from being an error.
     set nonomatch

#  Check that we have at least one directory to search.
     if ( $1 == "" ) then
        set p1 = "/star/lib"
     else
        set p1 = $1
     endif

#  Look for an LD_LIBRARY_PATH and extract the names
     if ( $?LD_LIBRARY_PATH ) then
        set other_dirs = "`echo $LD_LIBRARY_PATH | sed 's+:+ +g' `"
     else
        set other_dirs = ""
     endif

#  Get the names of the libraries we expect to find.
     set ccdpack_ext_libs = \
         "`./ccdpack_link_adam` `./ccdpack_graphics_link_adam`"

#  For each of them look for static version.
     foreach library ( $ccdpack_ext_libs )

#  Strip leading "-l" from names
        set this_lib = `echo $library | sed 's:-l::'`

#  Extract the name of the library and look for it in the named
#  directories. The named directories are the arguments to this 
#  script
        set found_stat = 0
        foreach directory ( $p1 $* $other_dirs )

#  Check for existence of libxxx.a 
           if ( -e ${directory}/lib${this_lib}.a ) then

#  Ok found library just echo it's name.
              set found_stat = 1
           endif

#  Switch pattern expansion back on
           unset noglob
        end

#  Ok have looked in every directory, did we locate a static version?
#  if we have just echo the name, if not make sure we try for a shareable
#  version.
        if ( $found_stat == 1 ) then
           echo -n "$library "
        else
           echo -n "-Bdynamic $library -Bstatic "
        endif
     end 

#  Add <CR> to end of stream
     echo " "
     exit
# $Id$
