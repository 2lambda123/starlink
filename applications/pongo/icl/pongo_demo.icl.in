{+
{  Name:
{     PONGO_DEMO.ICL

{  Purpose:
{     Tests PONGO installation.

{  Language:
{     ADAM ICL

{  Type of module:
{     ICL file

{  Arguments:

{  Invocation:
{     LOAD (PONGO_EXAMPLES&'')/pongo_demo

{  Description:
{     This routine runs the PONGO example scripts one-by-one to
{     test the correct installation. The tests are reasonably
{     comprehensive.

{  Authors:
{     PDRAPER: P.W. Draper (STARLINK - Durham University)

{  History:
{     11-JUL-1994 (PDRAPER):
{        Original version.

{  Bugs:

INPUT "Name of image display device: " (GDNAME)

{+
{  Name:
{     SPECTRUM.ICL

{  Purpose:
{     Plot the SPECTRUM example.

{  Language:
{     ADAM ICL

{  Type of module:
{     ICL file

{  Arguments:

{  Invocation:
{     LOAD (PONGO_EXAMPLES&'')/spectrum

{  Description:
{     Demonstrate plotting simple spectrum data using PONGO.

{  Authors:
{     PCTR: P.C.T. Rees (STARLINK)
{     PDRAPER: P.W. Draper (STARLINK - Durham University)

{  History:
{     15-DEC-1992 (PCTR):
{        Starlink release.
{     11-JUN-1994 (PDRAPER):
{        Ported to UNIX.

{  Bugs:

{-

{  Begin a PONGO plot.
BEGPLOT (GDNAME&'')

{  Reset PONGO.
RESETPONGO

{  Set the columns to read from the data file.
XCOLUMN Wave
YCOLUMN Flux
EXCOLUMN 0
EYCOLUMN 0

{  Read the data file.
READF (PONGO_EXAMPLES&'')/swp3196.lap SELCOND="Flux > 0" NOALL

{  Set the plotting limits.
WORLD DATA

{  Draw the box and plot the data.
CHANGE LINEWID=2 COLOUR=1
BOXFRAME BCNST BCNST 0 0 0 0
CHANGE LINEWID=1 COLOUR=4
GPOINTS C
CHANGE COLOUR=1

{  Draw the labels (use the global values set by READF).
CHANGE LINEWID=2
LABEL TITLE="SWP3196, LORES, LAP" COLUMNS

{  This line resets the plotting pen, font, point fill style, line style
{  and line width to their defaults.
CHANGE RESET

{  This line resets the character height to 1.0.
CHANGE CHEIGHT=1.0

{  End the PONGO plot.
ENDPLOT


{+
{  Name:
{     ERRORS.ICL

{  Purpose:
{     Plot the ERRORS example.

{  Language:
{     ADAM ICL

{  Type of module:
{     ICL file

{  Arguments:

{  Invocation:
{     LOAD (PONGO_EXAMPLES&'')/spectrum

{  Description:
{     Demonstrate the use of error bars and least squares fitting in PONGO.

{  Authors:
{     PCTR: P.C.T. Rees (STARLINK)
{     PDRAPER: P.W. Draper (STARLINK - Durham University)

{  History:
{     15-DEC-1992 (PCTR):
{        Starlink release.
{     11-JUN-1994 (PDRAPER):
{        Ported to UNIX.

{  Bugs:

{-

{  Begin a PONGO plot.
BEGPLOT (GDNAME)

{  Reset PONGO.
RESETPONGO

{  Set the columns to read from the data file.
XCOLUMN 1
EXCOLUMN 2
YCOLUMN 3
EYCOLUMN 4

{  Read the data file.
READF DATA=(PONGO_EXAMPLES&'')/tutorial.dat ALL

{  Set the plotting limits.
WORLD DATA

{  Draw the box and plot the data.
CHANGE LINEWID=2
BOXFRAME BCNST BCNVST
CHANGE LINEWID=1
ERRORBAR X 0.0
ERRORBAR Y 0.0

{  Draw the labels.
CHANGE LINEWID=2
CHANGE CHEIGHT=2.0
WTEXT M 0.5 0.5 "Interstellar data" SIDE=T JUSTIFICATION=0.5
CHANGE CHEIGHT=1.0
WTEXT M 3 0.5 "Colour excess E\dB-V\d" SIDE=B JUSTIFICATION=0.5
WTEXT M 3 0.5 "Equivalent width in m\A" SIDE=L JUSTIFICATION=0.5

{  Perform an unweighted least squares to a straight line.
LTYPE 2
FITLINE NOWEIGHT COLOUR=3

{  Perform a weighted least squares fit to a straight line.
LTYPE 1
FITLINE WEIGHT COLOUR=2

{  Perform a weighted least squares fit to a Chebyshev polynomial.
LTYPE 4
FITCURVE POLY NPOLY=2 WEIGHT COLOUR=4

{  This line resets the plotting pen, font, point fill style, line style
{  and line width to their defaults.
CHANGE RESET

{  This line resets the character height to 1.0.
CHANGE CHEIGHT=1.0

{  End the PONGO plot.
ENDPLOT


{+
{  Name:
{     HISTOGRAM.ICL

{  Purpose:
{     Plot the HISTOGRAM example.

{  Language:
{     ADAM ICL

{  Type of module:
{     ICL file

{  Arguments:

{  Invocation:
{     LOAD (PONGO_EXAMPLES&'')/histogram

{  Description:
{     A PONGO example command file to plot histograms.

{  Authors:
{     PAH: Paul Harrison (JBVAD::PAH):
{     PCTR: P.C.T. Rees (STARLINK)
{     P.W. Draper (STARLINK - Durham University)

{  History:
{     3-JUL-1990 (JBVAD::PAH):
{        Original version.
{     23-SEP-1992 (PCTR):
{        Starlink release.
{     11-JUL-1994 (PDRAPER):
{        Ported to UNIX.

{  Bugs:

{-

{  Begin a PONGO plot.
BEGPLOT (GDNAME)

{  Reset PONGO.
RESETPONGO

{  Set the line width to 2.
CHANGE LINEWID=2

{  Read the unbinned data into the XCOL data area, but only if their values
{  are greater than zero. Note that the XCOL column has been specified by
{  its label which is case-sensitive.
READF DATA=(PONGO_EXAMPLES&'')/ppdot.dat ~
   XCOL='Age' SELCOND='Age > 0' NOALL

{  Take logarithms of the X-axis data.
XLOGARITHM

{  Set up the limits "manually".
LIMITS 2.0 11.0 0.0 100.0

{  Draw a box, but don't draw the right-hand edge. Reset the X-axis options
{  so that they do not contain an "L" option (which has been automatically
{  added by XLOGARITHM).
BOX BCNST BNST

{  Change the line style.
CHANGE LINESTY=2

{  Bin the data in the XCOL data area between 2 and 11, with 18 bins.
{  Draw these data using the world coordinate limits already set up.
HISTOGRAM 2.0 11.0 18 NOAUTOSCALE

{  Write a label describing the histogram.
CHANGE LINESTY=1
LABEL 'Log [Characteristic age (yr)]' 'Number of old pulsars (dashed)' ' '
CHANGE CHEIGHT=2.0
MTEXT T 1.0 0.5 0.5 'Pulsar Age Distribution'
CHANGE CHEIGHT=1.0

{  Read a second data file (the data are already logarithmic).
READF DATA=(PONGO_EXAMPLES&'')/cljon.dat XCOL=1 YCOL=0 ZCOL=0 ~
   EYCOL=0 EXCOL=0 ALL

{  Set up new limits.
LIMIT 2.0 11.0 0.0 11.0

{  Draw a box, but this time do not draw the left-hand edge.
BOX  YOPT='CMST'
CHANGE LINESTY=4
HISTOGRAM 2.0 11.0 18 NOAUTOSCALE

{  Write a label describing the second histogram.
CHANGE LINESTY=1
MTEXT R 2.5 0.5 0.5 'Number of new pulsars (dotted)'

{  This line resets the plotting pen, font, point fill style, line style
{  and line width to their defaults.
CHANGE RESET

{  This line resets the character height to 1.0.
CHANGE CHEIGHT=1.0

{  End the PONGO plot.
ENDPLOT


{+
{  Name:
{     PPDOTDIAG.ICL

{  Purpose:
{     Plot the PPDOTDIAG example.

{  Language:
{     ADAM ICL

{  Type of module:
{     ICL file

{  Arguments:

{  Invocation:
{     LOAD (PONGO_EXAMPLES&'')/ppdotdiag

{  Description:
{     Demonstrate more complex data management using READF and labelling.

{  Authors:
{     PAH: Paul Harrison (JBVAD::PAH):
{     PCTR: P.C.T. Rees (STARLINK)
{     PDRAPER: P.W. Draper (STARLINK - Durham University)

{  History:
{     3-JUL-1990 (JBVAD::PAH):
{        Original version.
{     23-SEP-1992 (PCTR):
{        Starlink release.
{     11-JUN-1994 (PDRAPER):
{        Ported to UNIX.

{  Bugs:

{-

{  Begin a PONGO plot.
BEGPLOT (GDNAME)

{  Reset PONGO.
RESETPONGO

{  Set the columns to read from the data file.
XCOLUMN Perio
YCOLUMN pdot
EXCOLUMN 0
EYCOLUMN 0
LABCOLUMN 1
SYMCOLUMN 2

{  Read the data file.
READF DATA=(PONGO_EXAMPLES&'')/ppdot.dat SELCOND='pdot > 0' ~
      NOALL NOQUICK RESET

{  Convert the X- and Y-axis data to logarithms.
XLOGARITHM
YLOGARITHM

{  Set the Y-axis style and the plot limits.
SETGLOBAL PONGO_YOPT BCNST
LIMITS -2.95 1.0 -20.5 -11.0

{  Draw the box and plot the points.
LWEIGHT 2
BOXFRAME
LWEIGHT 1
POINTS

{  Create a title from the number of points that have been read in.
{  Note the string concatenation.
GETGLOBAL PONGO_NDATA (NPULSAR)
TITLESTRING=''''&(NPULSAR)&' Pulsars'&''''

{  The Y-axis label.
YLABEL='''log[Period Derivative (ss\u-1\d)]'''

{  Note how the X-axis label is picked up from the data file.
LWEIGHT 2
LABEL COLUMNS YLABEL=(YLABEL&'') TITLE=(TITLESTRING&'')
LTYPE 2
MOVE -1.325 -21.0
DRAW  1.0   -14.025
LTYPE 5
MOVE -3.0   -18.9549
DRAW  1.0   -13.626
LWEIGHT 1
CHANGE LINESTY=1 CHEIGHT=0.7
PTEXT      -0.7082780     -18.0945511        0 0.0 '0655+64'
PTEXT      -1.2338796     -17.0440712        0 0.0 '1913+16'

{  Note the use of the command line continuation character "~", used
{  gratuitously here for for illustration.
PTEXT      -0.2164239E+01  -0.1934648E+02    0 0.0 ~
   '1953+29'
PTEXT      -0.2336934E+01  -0.1979958E+02    0 1.0 ~
   '1855+09'
PTEXT      -1.9114622     -17.9879818        0 0.0 ~
   '1620-26'

CHANGE CHEIGHT=1.0
PTEXT      -1.0945635     -20.1498394       37 0.0 'Death Line'
PTEXT      -1.4808489     -16.7852573       19 0.0 'Spin-up Line'

{  This line resets the plotting pen, font, point fill style, line style
{  and line width to their defaults.
CHANGE RESET

{  This line resets the character height to 1.0.
CHANGE CHEIGHT=1.0

{  End the PONGO plot.
ENDPLOT


{+
{  Name:
{     ELLIPSES.ICL

{  Purpose:
{     Plot the ELLIPSES example.

{  Language:
{     ADAM ICL

{  Type of module:
{     ICL file

{  Arguments:

{  Invocation:
{     LOAD (PONGO_EXAMPLES&'')/ellipses

{  Description:
{     A PONGO ICL example file to plot error ellipses.

{  Authors:
{     PAH: Paul Harrison (JBVAD::PAH):
{     PCTR: P.C.T. Rees (STARLINK)
{     PDRAPER: P.W. Draper (STARLINK - Durham University)

{  History:
{     3-JUL-1990 (JBVAD::PAH):
{        Original version.
{     23-SEP-1992 (PCTR):
{        Starlink release.
{     11-JUN-1994 (PDRAPER):
{        Ported to UNIX.

{  Bugs:

{-

{  Begin a PONGO plot.
BEGPLOT (GDNAME)

{  Reset PONGO.
RESETPONGO

{  Change the character height to half its normal value.
EXPAND 0.5

{  Set the SCALE global parameter to scale the error ellipses.
SETGLOBAL PONGO_ERSCALE 2.3

{  Change the axis options global parameters to make the x=0 and y=0
{  lines appear.
SETGLOBAL PONGO_XOPT ABCNST
SETGLOBAL PONGO_YOPT ABCNST

{  Set up the data columns.
EXCOLUMN 5
EYCOLUMN 7
LABCOLUMN 1
SYMCOLUMN 2
XCOLUMN 4
YCOLUMN 6
ZCOLUMN 8

{  Alter the viewport to the bottom left corner.
VP_BL

{  Read the data file, selecting only lines where COLUMN 3 is equal to 11.
READF DATA=(PONGO_EXAMPLES&'')/ellipses.dat NOALL SELCOND='3 = 11'

{  Set up the world coordinate limits from the data including the origin.
WORLD DATA0

{  Adjust the viewport to make X- and Y-axis scales equal.
WNAD

{  Draw the box and axes
BOXFRAME

{  Write some text relative to the viewport.
MTEXT T 1.0 0.5 0.5 'Defford only'

{  Change the plotting pen.
PEN 2

{  Plot the points - note that because there is no command-line parameter,
{  and values have been read into the symbol column, these values are used
{  to determine the symbol type.
POINTS

{  Change the pen again - note that the full form of the command is being
{  used not its alias as above.
CHANGE COLOUR=4

{  Annotate the graph with labels (these happen to be numbers in this case)
ANNOTATE
CHANGE COLOUR=1

{  Draw the error ellipses without their principal axes.
ELLIPSES NOAXES

{  Find how many data points have been read (this was written out to the
{  global parameter PONGO_NDATA by the read command.
GETGLOBAL PONGO_NDATA (NDATA)

{  Use the radiate command to draw lines from each of the data points to
{  the origin.
RADIATE 0.0 0.0 (NDATA)

{  Draw the axis labels and the plot title.
LABEL '\gm\d\ga\u (mas yr\u-1\d)' '\gm\d\gd\u (mas yr\u-1\d)' 1112+50

{  Produce two more similar plots in different areas of the screen...
VP_BR
READF SELCOND='3 = 12'
WORLD DATA0
WNAD
BOXFRAME
MTEXT T 1.0 0.5 0.5 'Knockin only'
CHANGE COLOUR=2
POINTS
CHANGE COLOUR=4
ANNOTATE
CHANGE COLOUR=1
ELLIPSES NOAXES
GETGLOBAL PONGO_NDATA (NDATA)
RADIATE 0.0 0.0 (NDATA)

{  Note that LABEL remembers the previous axis labels.
LABEL
VP_TH
READF SELCOND='3 = 13'
WORLD DATA0
WNAD
BOXFRAME
MTEXT T 1.0 0.5 0.5  'Defford and Knockin'
CHANGE COLOUR=2
POINTS
CHANGE COLOUR=4
ANNOTATE
CHANGE COLOUR=1
ELLIPSES NOAXES
GETGLOBAL PONGO_NDATA (NDATA)
RADIATE 0.0 0.0 (NDATA)
LABEL '\gm\d\ga\u (mas yr\u-1\d)' '\gm\d\gd\u (mas yr\u-1\d)' 1112+50

{  Change the character height and viewport back to their default sizes.
CHANGE CHEIGHT=1.0
VSTAND

{  This line resets the plotting pen, font, point fill style, line style
{  and line width to their defaults.
CHANGE RESET

{  End the PONGO plot.
ENDPLOT



{+
{  Name:
{     PROJECTIONS.ICL

{  Purpose:
{     Plot the PROJECTIONS example.

{  Language:
{     ADAM ICL

{  Type of module:
{     ICL file

{  Arguments:

{  Invocation:
{     LOAD (PONGO_EXAMPLES&'')/projections

{  Description:
{     A PONGO example command file to illustrate different projections.

{  Authors:
{     PAH: Paul Harrison (JBVAD::PAH)
{     PCTR: P.C.T. Rees (STARLINK)
{     MJC: Malcolm J. Currie (STARLINK)

{  History:
{     3-JUL-1990 (JBVAD::PAH):
{        Original version.
{     23-SEP-1992 (PCTR):
{        Starlink release.
{     4-MAY-1994 (MJC):
{        Fixed two bugs (NOQUICK and READF) when reading the data file.
{     11-JUL-1994 (PDRAPER):
{        Ported to UNIX>

{  Bugs:

{-

{  Begin a PONGO plot.
BEGPLOT (GDNAME)

{  Reset PONGO.
RESETPONGO

{  Set the character height.
EXPAND 0.7

{  Read the postions of pulsars from the data file.
READF DATA=(PONGO_EXAMPLES&'')/ppdot.dat ~
  SYMCOL=2 LABCOL=1 XCOL=RA YCOL=Dec ZCOL=Dist SELCOND='Dist > 0' NOALL NOQUICK

{  Convert the XCOL and YCOL data areas from degrees to radians.
XSCALE 1.7453292E-2
YSCALE 1.7453292E-2

{  Select a viewport in the top left-hand corner.
VP_TL

{  Get the limits from the data using the AITOFF projection, note that the
{  RA centre is specified in the form of hh:mm:ss.sss and that below is
{  understood to mean 12 hours.
DLIMITS PROJECTION=AITOFF RACENTRE=12 DECCENTRE=0

{  Adjust the viewport.
WNAD
MTEXT T 1.0 0.5 0.5 'Aitoff centre \ga=12\uh\d \gd=0\(2729)'

{  Draw the coordinate grid.
GRID PROJECTION=AITOFF

{  Plot the points with various symbols.
PEN 6
POINTS

{  Plot the same data in the top right-hand corner in a sine projection.
PEN 1
VP_TR
LIMITS -1.1 1.1 -1.1 1.1 PROJECTION=SIN RACENTRE=18 DECCENTRE=30
WNAD
MTEXT T 1.0 0.5 0.5 'SIN centre \ga=18\uh\d \gd=30\(2729)'
GRID

{  Draw a circle around the projection to make it look prettier.
ARC 1.0 1.0 0.0 0.0 0.0 360.0 0.0 PROJECTION='NONE'
PEN 6
POINTS

{  Plot the data in the bottom left-hand corner in a tangent projection.
PEN 1
VP_BL
LIMITS -4.0 4.0 -4.0 4.0 PROJECTION=TAN RACENTRE=0 DECCENTRE=30
WNAD
MTEXT T 1.0 0.5 0.5 'TAN centre \ga=0\uh\d \gd=30\(2729)'
GRID
PEN 6
POINTS

{  Plot the data in the bottom right-hand corner in an arc projection.
PEN 1
VP_BR
LIMITS -3.1 3.1 -3.1 3.1 PROJECTION=ARC RACENTRE=12
WNAD
MTEXT T 1.0 0.5 0.5 'ARC centre \ga=12\uh\d \gd=30\(2729)'
GRID
PEN 6
POINTS

{  This line resets the viewport and the plotting pen, font, point fill style,
{  line style and line width to their defaults.
VSTAND
CHANGE RESET

{  This line resets the character height to 1.0.
CHANGE CHEIGHT=1.0

{  End the PONGO plot.
ENDPLOT
{+
{  Name:
{     RADEC.ICL

{  Purpose:
{     Plot the PPDOTDIAG example using RA DEC labels

{  Language:
{     ICL

{  Type of module:
{     ICL file

{  Arguments:

{  Invocation:
{     LOAD (PONGO_EXAMPLES&'')/radec.icl

{  Description:
{     Demonstrate RA and DEC labelling of axis.

{  Authors:
{     PDRAPER: P.W. Draper (STARLINK - Durham University)

{  History:
{     30-MAY-1996 (PDRAPER):
{        Original version.

{  Bugs:

{-

{  Begin a PONGO plot.
BEGPLOT (GDNAME)

{  Reset PONGO.
RESETPONGO

{  Set the columns to read from the data file.
XCOLUMN RA
YCOLUMN Declination
EXCOLUMN 0
EYCOLUMN 0
LABCOLUMN 1
SYMCOLUMN 2

{  Read the data file.
READF DATA=(PONGO_EXAMPLES&'')/ppdot.dat SELCOND='pdot > 0' ~
      NOALL NOQUICK RESET

{  Convert the X- and Y-axis data to radians.
DEGTOR X
DEGTOR Y

{  Set the Y-axis style and the plot limits.
SETGLOBAL PONGO_XOPT BCNSTZHG
SETGLOBAL PONGO_YOPT BCNSTZDG
LIMITS 5.5 4.5 -0.2 0.2

{  Draw the box and plot the points.
LWEIGHT 2
CHANGE FONT=3 COLOUR=3
BOXFRAME
LWEIGHT 1
CHANGE CHEIGHT=3 COLOUR=5
POINTS
CHANGE CHEIGHT=1 COLOUR=3

{  Note how the X-axis label is picked up from the data file.
LWEIGHT 2
LABEL COLUMNS TITLE='Pulsar Positions'

{  End the PONGO plot.
ENDPLOT
{


{+
{  Name:
{     COVER.ICL

{  Purpose:
{     Plot the SUN/137 cover example.

{  Language:
{     ADAM ICL

{  Type of module:
{     ICL file

{  Arguments:

{  Invocation:
{     LOAD (PONGO_EXAMPLES&'')/cover

{  Description:
{     A PONGO example command file to generate the cover of SUN/137.

{  Authors:
{     PCTR: P.C.T. Rees (STARLINK)
{     MJC: Malcolm J. Currie (STARLINK)
{     PDRAPER: P.W. Draper (STARLINK - Durham University)

{  History:
{     7-JAN-1993 (PCTR):
{        Starlink release.
{     1994 May 4 (MJC):
{        Fixed two bugs (NOQUICK and READF) when reading the data file.
{     22-JUN-1994 (PDRAPER):
{        Converted to work on UNIX.

{  Bugs:

{-
BEGPLOT (GDNAME) ACTION=B CLEAR

{  Reset PONGO.
RESETPONGO
PEN 2

{  Set the character size.
EXPAND 0.7

{  Select a viewport in the top left-hand corner.
VIEWPORT NDC 0.0 0.6 0.4 1.0
EXPAND 4
FONT 4
LWEIGHT 5
WTEXT P 0.65 0.35 "PONGO" JUSTIFICATION=0.5 ANGLE=45
PEN 1
LWEIGHT 2
EXPAND 2
WTEXT P 0.81 0.63 "\(2291)" JUSTIFICATION=0.5 ANGLE=45
EXPAND 18
WTEXT P 0.62 0.16 "\(0844)" JUSTIFICATION=0.5 ANGLE=0
FONT 1
LWEIGHT 1
EXPAND 0.5
PRIM K X=0.254 Y=0.808 SYMBOL=17
PRIM K X=0.551 Y=0.769 SYMBOL=17
PRIM K X=0.707 Y=0.761 SYMBOL=17
PRIM K X=0.864 Y=0.232 SYMBOL=17
PRIM K X=0.938 Y=0.162 SYMBOL=17
PRIM K X=0.265 Y=0.127 SYMBOL=17
PRIM K X=0.258 Y=0.260 SYMBOL=17
PRIM K X=0.903 Y=0.464 SYMBOL=17
PRIM K X=0.329 Y=0.385 SYMBOL=17
EXPAND 1.1
PRIM K X=0.393 Y=0.692 SYMBOL=17
PRIM K X=0.453 Y=0.692 SYMBOL=17
PRIM K X=0.445 Y=0.645 SYMBOL=17
PRIM K X=0.393 Y=0.629 SYMBOL=17
PRIM K X=0.354 Y=0.575 SYMBOL=17
EXPAND 0.8
PRIM K X=0.393 Y=0.711 SYMBOL=17
EXPAND 0.5
PRIM K X=0.343 Y=0.716 SYMBOL=17
PRIM K X=0.370 Y=0.700 SYMBOL=17
PRIM K X=0.346 Y=0.583 SYMBOL=17
PRIM K X=0.443 Y=0.700 SYMBOL=17
EXPAND 0.7

{  Select a viewport in the top right-hand corner.
VIEWPORT NDC 0.6 0.95 0.6 0.95

{  Read the unbinned data into the XCOL data area, but only if their values
{  are greater than zero. Note that the XCOL column has been specified by
{  its label which is case-sensitive.
READF DATA=(PONGO_EXAMPLES&'')/ppdot.dat ~
   XCOL='Age' SELCOND='Age > 0' NOALL NOQUICK

{  Take logarithms of the X-axis data.
XLOGARITHM

{  Set up the limits "manually".
LIMITS 2.0 11.0 0.0 100.0

{  Draw a box, but don't draw the right-hand edge. Reset the X-axis options
{  so that they do not contain an "L" option (which has been automatically
{  added by XLOGARITHM).
PEN 5
BOX BCNST BNST

{  Change the line style.
CHANGE LINESTY=2

{  Bin the data in the XCOL data area between 2 and 11, with 18 bins.
{  Draw these data using the world coordinate limits already set up.
HISTOGRAM 2.0 11.0 18 NOAUTOSCALE

{  Write a label describing the histogram.
CHANGE LINESTY=1
LABEL 'Log [Characteristic age (yr)]' 'Number of old pulsars (dashed)' ' '
MTEXT T 0.5 0.5 0.5 'Pulsar Age Distribution'

{  Read a second data file (the data are already logarithmic).
READF DATA=(PONGO_EXAMPLES&'')/cljon.dat ~
   XCOL=1 YCOL=0 ZCOL=0 EYCOL=0 EXCOL=0 ALL NOQUICK

{  Set up new limits.
LIMIT 2.0 11.0 0.0 11.0

{  Draw a box, but this time do not draw the left-hand edge.
BOX  YOPT='CMST'
CHANGE LINESTY=4
PEN 5
HISTOGRAM 2.0 11.0 18 NOAUTOSCALE

{  Write a label describing the second histogram.
CHANGE LINESTY=1
MTEXT R 2.5 0.5 0.5 'Number of new pulsars (dotted)'

{  This line resets the plotting pen, font, point fill style, line style
{  and line width to their defaults.
CHANGE RESET
PEN 7

{  Select a viewport in the bottom left-hand corner.
VIEWPORT NDC 0.1 0.45 0.1 0.45

{  Set the columns to read from the data file.
XCOLUMN 1
EXCOLUMN 2
YCOLUMN 3
EYCOLUMN 4
SYMCOLUMN 0
LABCOLUMN 0
ZCOLUMN 0

{  Read the data file.
READF DATA=(PONGO_EXAMPLES&'')/tutorial.dat ALL NOQUICK

{  Set the plotting limits.
WORLD DATA

{  Draw the box and plot the data.
BOXFRAME BCNST BCNVST
ERRORBAR X 0.0
ERRORBAR Y 0.0

{  Draw the labels.
WTEXT M 0.5 0.5 "Interstellar data" SIDE=T JUSTIFICATION=0.5
WTEXT M 3 0.5 "Colour excess E\dB-V\d" SIDE=B JUSTIFICATION=0.5
WTEXT M 3 0.5 "Equivalent width in m\A" SIDE=L JUSTIFICATION=0.5

{  Perform an unweighted least squares to a straight line.
PEN 5
LTYPE 1
FITLINE NOWEIGHT COLOUR=1 RESET

{  Perform a weighted least squares fit to a Chebyshev polynomial.
PEN 4
LTYPE 2
FITCURVE POLY NPOLY=2 WEIGHT COLOUR=1 RESET

{  This line resets the plotting pen, font, point fill style, line style
{  and line width to their defaults.
CHANGE RESET
PEN 8

{  Select a viewport in the bottom right-hand corner.
VP_BR

{  Read the postions of pulsars from the data file.
READF DATA=(PONGO_EXAMPLES&'')/ppdot.dat SYMCOL=2 LABCOL=1 ~
   XCOL=RA YCOL=Dec ZCOL=Dist SELCOND='Dist > 0' NOALL NOQUICK

{  Convert the XCOL and YCOL data areas from degrees to radians.
DEGTOR X
DEGTOR Y

{  Plot the data in the bottom right-hand corner in a sine projection.
LIMITS -1.1 1.1 -1.1 1.1 PROJECTION=SIN RACENTRE=18 DECCENTRE=30
WNAD
MTEXT T 1.0 0.5 0.5 'SIN centre \ga=18\uh\d \gd=30\(2729)'
GRID

{  Draw a circle around the projection to make it look prettier.
ARC 1.0 1.0 0.0 0.0 0.0 360.0 0.0 PROJECTION='NONE'
PEN 3
POINTS

{  This line resets the viewport and the plotting pen, font, point fill style,
{  line style and line width to their defaults.
VSTAND
CHANGE RESET

{  This line resets the character height to 1.0.
CHANGE CHEIGHT=1.0
ENDPLOT

{ $Id$
