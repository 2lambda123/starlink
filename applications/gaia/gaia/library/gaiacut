#!/bin/sh    
# The next line is executed by /bin/sh, but not Tcl \
exec $GAIA_DIR/gaia_stcl $0 ${1+"$@"}
#+
#   Name:
#      gaiacut

#   Purpose:
#      Apply intensity cuts to GAIA

#   Usage:
#      gaiacut clone_number lowcut|percentile [highcut]

#   Description:
#      Applies a given intensity cut to a displayed image in GAIA.
#      If two values are given then these are interpreted as simple
#      intensity limits, otherwise a single "percentile" cut is
#      applied.
#
#      Clones are specified by an integer numbers from 1 upwards.

#   Authors:
#      Peter W. Draper (PWD):

#  Copyright:
#     Copyright (C) 2002-2005 Central Laboratory of the Research Councils.
#     Copyright (C) 2006 Particle Physics & Astronomy Research Council.
#     All Rights Reserved.

#  Licence:
#     This program is free software; you can redistribute it and/or
#     modify it under the terms of the GNU General Public License as
#     published by the Free Software Foundation; either version 2 of the
#     License, or (at your option) any later version.
#
#     This program is distributed in the hope that it will be
#     useful, but WITHOUT ANY WARRANTY; without even the implied warranty
#     of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
#     GNU General Public License for more details.
#
#     You should have received a copy of the GNU General Public License
#     along with this program; if not, write to the Free Software
#     Foundation, Inc., 59 Temple Place,Suite 330, Boston, MA
#     02111-1307, USA

#   History:
#      12-DEC-2002 (PWD):
#         Original version.
#-
#.

#  Check the command-line arguments.
if { $argc >= 2 } { 
   set clone [lindex $argv 0]
   set lowcut [lindex $argv 1]
   set percentile 1
   if { $argc >= 3 } {
      set percentile 0
      set highcut [lindex $argv 2]
   }
} else {
   puts stderr {Usage: gaiacut clone_number lowcut|percentile [highcut]}
   exit
}

#  Open a socket to a GAIA application and return the file descriptor
#  for remote commands. If a GAIA isn't found then start one up.
proc connect_to_gaia {} {
   global env

   #  Get the hostname and port info from the file ~/.rtd-remote,
   #  which is created by rtdimage when the remote subcommand is
   #  used.
   set tries 0
   while { 1 } {
      set needed 0

      #  Open the file containing the GAIA process information and read it.
      if {[catch {set fd [open $env(HOME)/.rtd-remote]} msg]} {
         set needed 1
      } else {
         lassign [read $fd] pid host port
         close $fd
      }

      #  See if the process is listening to this socket.
      if { ! $needed } {
         if {[catch {socket $host $port} msg]} {
            set needed 1
         } else {
            fconfigure $msg -buffering line
            return $msg
         }
      }

      #  If the process doesn't exist and we've not been around the
      #  loop already, then start a new GAIA.
      if { $needed && $tries == 0 } {
         puts stderr "Failed to connect to GAIA, starting new instance..."
         exec $env(GAIA_DIR)/gaia.sh &
         #exec $env(GAIA_DIR)/tgaia &
      }

      #  Now either wait and try again or give up if waited too long.
      if { $needed && $tries < 500 } {
         #  Wait for a while and then try again.
         incr tries
         after 1000
      } elseif { $needed } {
         puts stderr "Sorry timed out: failed to display image in GAIA"
         exit 1
      }
   }
}

#  Send the command to GAIA and return the results or generate an error.
proc send_to_gaia {args} {
   global gaia_fd
   puts $gaia_fd "$args"
   lassign [gets $gaia_fd] status length
   set result {}
   if {$length > 0} {
      set result [read $gaia_fd $length]
   }
   if {$status != 0} {
      error "$result"
   }
   return "$result"
}

#  Open up connection to GAIA.
set gaia_fd [connect_to_gaia]

#  Command needs to performed by Skycat or derived object.
set cmd "skycat::SkyCat::get_skycat_images"
set images [send_to_gaia remotetcl $cmd]

#  Got list so select the right clone. These start with number 1...
if { $clone == 0 } { 
   puts stderr "Clone numbers start from 1. So assuming 1"
   set clone 1
}

#  The clone <n> is called .gaia<n>.
set clonewidget ".gaia$clone"

#  See if this exists.
set cmd "winfo exists $clonewidget"
set exists [send_to_gaia remotetcl $cmd]
if { ! $exists } {
   puts stderr "Failed to locate GAIA clone ($clone)"
   close $gaia_fd
   exit
}

#  Get the associated image name...
set cmd "$clonewidget get_image"
set image [send_to_gaia remotetcl $cmd]
set cmd "$image get_image"
set image [send_to_gaia remotetcl $cmd]

#  Construct the command needed to apply cut.
if { $percentile } { 
   set cmd "$image autocut -percent $lowcut"
} else {
   set cmd "$image cut $lowcut $highcut"
}
 
#  And send the command.
set ret [send_to_gaia remotetcl $cmd]
if { $ret != "" } { 
   puts stderr "Failed to apply cut to image: ($ret)"
}
close $gaia_fd
exit
