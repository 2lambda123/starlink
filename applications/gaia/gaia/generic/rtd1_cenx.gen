      SUBROUTINE RTD1_CEN<T>( XINIT, YINIT, IMAGE, SWAP, NCOL, NLINE, 
     :                        ISIZE, SIGN, MAXSHF, MAXIT, TOLER, XACC, 
     :                        YACC, STATUS )
*+
*  Name:
*     RTD1_CEN

*  Purpose:
*     Locates the centroids of star-like image features.

*  Language:
*     Starlink Fortran 77

*  Invocation:
*     CALL RTD1_CEN<T>( XINIT, YINIT, IMAGE, SWAP, NCOL, NLINE, ISIZE, SIGN,
*                       MAXSHF, MAXIT, TOLER, XACC, YACC, STATUS )

*  Description:
*     The routine locates the centroid of image features by forming
*     marginal profiles within a search square. A background estimate is
*     made within the search square by finding the lower quartile point
*     in the profiles. These values are then used to form the centroid.
*     This sequence is repeated until a given number of iterations is
*     exceeded or the centroid is located with the given accuracy.  

*  Arguments:
*     XINIT = DOUBLE PRECISION (Given)
*        Initial guess at x position of object feature.
*     YINIT = DOUBLE PRECISION (Given)
*        Initial guess at y position of object feature.
*     IMAGE( NCOL, NLINE ) = <COMM> (Given)
*        The data.
*     SWAP = LOGICAL (Given)
*        Data is byte swapped.
*     NCOL = INTEGER (Given)
*        First dimension of IMAGE.
*     NLINE = INTEGER (Given)
*        Second dimension of IMAGE.
*     ISIZE = INTEGER (Given)
*        The size of the search square side.
*     SIGN = LOGICAL (Given)
*        True of the image features have positive data values. False
*        if they have negative values.
*     MAXSHF = DOUBLE PRECISION (Given)
*        Maximum allowed shift in image centre.
*     MAXIT = INTEGER (Given)
*        Maximum number of refining iterations.
*     TOLER = DOUBLE PRECISION (Given)
*        Accuracy with which the centroid should be located.
*     XACC = DOUBLE PRECISION (Returned)
*        X position of centroid.
*     YACC = DOUBLE PRECISION (Returned)
*        X position of centroid.
*     STATUS = INTEGER (Given and Returned)
*        The global status.

*  Notes:
*     -  If no centroid is located STATUS will be set and an appropriate
*        message will reported. No coordinate values will be mentioned
*        in the message, this will need to be performed by the calling
*        application.

*  Copyright:
*     Copyright (C) 1997 Central Laboratory of the Research Councils
*     Copyright (C) 2006 Particle Physics & Astronomy Research Council.
*     All Rights Reserved.

*  Licence:
*     This program is free software; you can redistribute it and/or
*     modify it under the terms of the GNU General Public License as
*     published by the Free Software Foundation; either version 2 of the
*     License, or (at your option) any later version.
*
*     This program is distributed in the hope that it will be
*     useful, but WITHOUT ANY WARRANTY; without even the implied warranty
*     of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
*     GNU General Public License for more details.
*
*     You should have received a copy of the GNU General Public License
*     along with this program; if not, write to the Free Software
*     Foundation, Inc., 51 Franklin Street,Fifth Floor, Boston, MA
*     02110-1301, USA


*  Authors:
*     RFWS: Rodney Warren-Smith (Durham Polarimetry Group)
*     PWD: Peter Draper (STARLINK)
*     {enter_new_authors_here}

*  History:
*     - (RFWS):
*        Original version - Durham local version of EDRS.
*     9-JUL-1992 (PWD):
*        Changed prologue, converted to ADAM and made generic.
*     12-DEC-1997 (PWD):
*        Renamed RTD1_CENX (was CCG1_CEN) now part of GAIA.
*     20-JAN-2000 (PWD):
*        Added byte swap changes.
*     {enter_further_changes_here}

*  Bugs:
*     {note_any_bugs_here}

*-
      
*  Type Definitions:
      IMPLICIT NONE              ! No implicit typing

*  Global Constants:
      INCLUDE 'SAE_PAR'          ! Standard SAE constants
      INCLUDE 'PRM_PAR'          ! PRIMDAT constants

*  Arguments Given:
      INTEGER NCOL
      INTEGER NLINE
      <TYPE> IMAGE( NCOL, NLINE )
      LOGICAL SWAP
      INTEGER ISIZE
      LOGICAL SIGN
      DOUBLE PRECISION MAXSHF
      INTEGER MAXIT
      DOUBLE PRECISION TOLER
      DOUBLE PRECISION XINIT
      DOUBLE PRECISION YINIT

*  Arguments Returned:
      DOUBLE PRECISION XACC
      DOUBLE PRECISION YACC

*  Status:
      INTEGER STATUS             ! Global status

*  Local Constants:
      INTEGER MAXSID             ! Maximum size of search square side.
      PARAMETER ( MAXSID = 51 )

*  External References:
      EXTERNAL GAI1_BSWAP<T>     ! Byte swapping function.
      <TYPE> GAI1_BSWAP<T>

*  Local Variables:
      <TYPE> DVAL                ! Image data value
      DOUBLE PRECISION PSNCNG    ! Position change from initial
      DOUBLE PRECISION SHIFT     ! Shift from last position
      DOUBLE PRECISION VAL       ! Dummy value
      DOUBLE PRECISION X         ! Current X position
      DOUBLE PRECISION XAV( MAXSID ) ! X profile means
      DOUBLE PRECISION XDENOM    ! Sum of data values in profile
      DOUBLE PRECISION XLAST     ! Last X position
      DOUBLE PRECISION XNUMER    ! Weighted sum of X positions
      DOUBLE PRECISION XPOSN( MAXSID ) ! X position of means
      DOUBLE PRECISION Y         ! Current Y postion
      DOUBLE PRECISION YAV( MAXSID ) ! Y profile means
      DOUBLE PRECISION YDENOM    ! Sum of data values in profile
      DOUBLE PRECISION YLAST     ! Last Y position
      DOUBLE PRECISION YNUMER    ! Sum of weighted positions
      DOUBLE PRECISION YPOSN( MAXSID ) ! X position of means
      DOUBLE PRECISION ZLEVX     ! Quartile value in X means
      DOUBLE PRECISION ZLEVY     ! Quartile value in Y means
      INTEGER I                  ! Loop counter
      INTEGER IHALF              ! Half square side
      INTEGER IPOSN              ! X position in search square
      INTEGER ISTART             ! X starting point of search square
      INTEGER ITER               ! Number of iterations
      INTEGER J                  ! Loop counter
      INTEGER JPOSN              ! Y position in search square
      INTEGER JSTART             ! Y starting point of search square
      INTEGER NBIN               ! Loop counter 
      INTEGER NITER              ! Maximum number of interations
      INTEGER NSAMP              ! Number of bins in search side
      INTEGER NTH                ! Dummy
      INTEGER NVALX              ! Number of non-empty X profile
                                 ! bins
      INTEGER NVALY              ! Number of non-empty Y profile
                                 ! bins
      INTEGER NXAV( MAXSID )     ! Number of values contributed to
                                 ! X bin
      INTEGER NYAV( MAXSID )     ! Number of values contributed to
                                 ! Y bin

*  Internal References:
      INCLUDE 'NUM_DEC_CVT'      ! NUM_ type conversion functions
      INCLUDE 'NUM_DEF_CVT'      ! Define functions...

*.

*  Check inherited global status.
      IF ( STATUS .NE. SAI__OK ) RETURN

*  Set up the initial position
      X = XINIT
      Y = YINIT

*  Restrict search square size to lie from 3 to maxsid.
      NSAMP = MIN( MAX( 3, ISIZE ), MAXSID )
      IHALF = NSAMP / 2

*  Make number of iterations at least 1.
      NITER = MAX( 1, MAXIT )

*  Start counting iterations
*  -------------------------
      ITER=0
 63   CONTINUE
         ITER=ITER+1

*  Find starting edge of search square.
         ISTART = NINT( X ) - IHALF - 1
         JSTART = NINT( Y ) - IHALF - 1

*  Remember starting position this iteration.
         XLAST = X
         YLAST = Y

*  Initiallise arrays for forming marginal profiles.
         DO 10 NBIN = 1, NSAMP
            XAV( NBIN ) = 0.0D0
            YAV( NBIN ) = 0.0D0
            NXAV( NBIN ) = 0
            NYAV( NBIN ) = 0
            XPOSN( NBIN ) = 0.0D0
            YPOSN( NBIN ) = 0.0D0
 10      CONTINUE

*  Scan search the whole of the search area, forming x and y profiles
*  from all valid pixels.
         IF ( SWAP ) THEN 
            DO 30 J = 1, NSAMP
               JPOSN = JSTART + J
               IF ( ( JPOSN .GE. 1 ) .AND. ( JPOSN .LE. NLINE ) ) THEN
                  DO 20 I = 1, NSAMP
                     IPOSN = ISTART + I
                     IF ( ( IPOSN .GE. 1 ) .AND. ( IPOSN .LE. NCOL ) )
     :               THEN
                        DVAL = GAI1_BSWAP<T>( IMAGE( IPOSN, JPOSN ) )
                        IF ( DVAL .NE.VAL__BAD<T> ) THEN
                           VAL = NUM_<T>TOD( DVAL )
                           XAV( I ) = XAV( I ) + VAL
                           YAV( J ) = YAV( J ) + VAL
                           NXAV( I ) = NXAV( I ) + 1
                           NYAV( J ) = NYAV( J ) + 1
                        ENDIF
                     ENDIF
 20               CONTINUE
               ENDIF
 30         CONTINUE
         ELSE 
            DO 31 J = 1, NSAMP
               JPOSN = JSTART + J
               IF ( ( JPOSN .GE. 1 ) .AND. ( JPOSN .LE. NLINE ) ) THEN
                  DO 21 I = 1, NSAMP
                     IPOSN = ISTART + I
                     IF ( ( IPOSN .GE. 1 ) .AND. ( IPOSN .LE. NCOL ) ) 
     :               THEN
                        DVAL = IMAGE( IPOSN, JPOSN )
                        IF ( DVAL .NE.VAL__BAD<T> ) THEN
                           VAL = NUM_<T>TOD( DVAL )
                           XAV( I ) = XAV( I ) + VAL
                           YAV( J ) = YAV( J ) + VAL
                           NXAV( I ) = NXAV( I ) + 1
                           NYAV( J ) = NYAV( J ) + 1
                        ENDIF
                     ENDIF
 21                  CONTINUE
               ENDIF
 31         CONTINUE
         END IF

*  Evaluate those x profile bins which contain at least 1 valid pixel,
*  invert the results if sign is set false. Also determine the position
*  of each bin and record this before it is lost.
         NVALX = 0
         NVALY = 0
         DO 80 NBIN = 1, NSAMP
            IF ( NXAV( NBIN ) .GT. 0 ) THEN
               NVALX = NVALX + 1
               XAV( NVALX ) = XAV( NBIN ) / DBLE( NXAV( NBIN ) )
               IF( .NOT. SIGN ) THEN
                  XAV( NVALX ) = -XAV( NVALX )
               END IF

*  Record position.
               XPOSN( NVALX ) = DBLE( ISTART + NBIN )
            END IF

*  Repeat for the y profile.
            IF( NYAV( NBIN ) .GT. 0 ) THEN
               NVALY = NVALY + 1
               YAV( NVALY ) = YAV( NBIN ) / NYAV( NBIN )
               IF( .NOT. SIGN ) THEN
                  YAV( NVALY ) = -YAV( NVALY )
               END IF

*  Record position.
               YPOSN( NVALY ) = DBLE( JSTART + NBIN )
            END IF
 80      CONTINUE

*  If both profiles contain no data then abort.
         IF ( NVALX .EQ. 0 .AND. NVALY .EQ. 0 ) THEN
            STATUS = SAI__ERROR
            CALL ERR_REP( 'RTD1_CEN<T>_NODATA',
     :      '  Centroid - profiles contain no data', STATUS )
            GO TO 99
         END IF

*  Find the lower quartile point in each profile as a background
*  estimate.
         IF ( NVALX .GT. 0 ) THEN
            NTH = MAX( NVALX / 4, 2 )
            CALL RTD1_ISRT( XAV, XPOSN, NVALX, STATUS )
            ZLEVX = XAV( NTH )
         END IF
         IF ( NVALY .GT. 0 ) THEN
            NTH = MAX( NVALY / 4, 2 )
            CALL RTD1_ISRT( YAV, YPOSN, NVALY, STATUS )
            ZLEVY = YAV( NTH )
         END IF

*  Initiallise sums for forming centroids.
         XNUMER = 0.0D0
         XDENOM = 0.0D0
         YNUMER = 0.0D0
         YDENOM = 0.0D0

*  Scan the profiles, using all data above the background to form
*  sums for the centroids.
         IF ( NVALX .GT. 0 ) THEN 
            DO 110 NBIN = 1, NVALX
               VAL = MAX ( ( XAV( NBIN ) - ZLEVX ), 0.0D0 )
               XNUMER = XNUMER + XPOSN( NBIN ) * VAL
               XDENOM = XDENOM + VAL
 110        CONTINUE
         END IF
         IF ( NVALY .GT. 0 ) THEN 
            DO 111 NBIN = 1, NVALY
               VAL = MAX ( ( YAV( NBIN ) - ZLEVY ), 0.0D0 )
               YNUMER = YNUMER + YPOSN( NBIN ) * VAL
               YDENOM = YDENOM + VAL
 111        CONTINUE
         END IF

*  If a profile contained no data then leave the current position at its
*  last value. (This allows for linear features running directly along x
*  or y to be centroided).
         IF ( NVALX .EQ. 0 .AND. YDENOM .NE. 0.0D0 ) THEN 
            X = XLAST
            Y = YNUMER / YDENOM
         ELSE IF( NVALY .EQ. 0 .AND. XDENOM .NE. 0.0D0 )THEN
            X = XNUMER / XDENOM
            Y = YLAST
         ELSE IF ( XDENOM .NE. 0.0D0 .AND. YDENOM .NE. 0.0D0 ) THEN 

*  Otherwise form the x and y centroids and find the shift from
*  the initial position
            X = XNUMER / XDENOM
            Y = YNUMER / YDENOM
         ELSE

*  Both denominators are zero - spoil this position.
            X = XINIT + MAXSHF + 1.0D0
            Y = YINIT + MAXSHF + 1.0D0
         END IF
         SHIFT = SQRT( ( X - XINIT )**2 + ( Y - YINIT )**2 )

*  If max shift is exceeded abort with error message.
         IF ( SHIFT .GT. MAXSHF ) THEN
            X = XINIT
            Y = YINIT
            STATUS = SAI__ERROR
            CALL ERR_REP( 'RTD1_CEN<T>_MAXSHIFT',
     :      '  Centroid - maximum shift in object position exceeded',
     :      STATUS )
            GO TO 99
         ELSE

*  Otherwise find the position shift this iteration.
            PSNCNG = SQRT( ( X - XLAST )**2 + ( Y - YLAST )**2 )

*  If required accuracy has been met, exit. Otherwise if iterations
*  remain go around iteration loop again.
            IF( PSNCNG .GT. TOLER ) THEN
               IF( ITER .LT. NITER ) GO TO 63
            END IF
         END IF

*  Escape label.
 99   CONTINUE

*  Set output positions to centroid values.
      XACC = X
      YACC = Y

      END
