# E.S.O. - VLT project 
# $Id: configure.in,v 1.7 1998/07/28 21:23:08 abrighto Exp $
#
# This file is used with GNU autoconf to generate a configure script for
# the Gaia modules.
#
# usage: % autoconf  # only if configure.in changed, use version 2.7 or later
#        % configure
#        % make
# who             when       what
# --------------  --------   ---------------------------------------------
# Allan Brighton  13 Mar 98  Created
#
# -----------------------------------------------------------------------

AC_INIT(gaia/tkAppInit.C)

# -----------------------------------------------------------------------
#
# Set the Gaia version from the VERSION file in this directory
# (That is the place to set the Gaia version number for a new release)
#
# -----------------------------------------------------------------------
GAIA_VERSION=`cat VERSION | sed -e 's/gaia_plugin-//'`
AC_SUBST(GAIA_VERSION)

# -----------------------------------------------------------------------
#
# Set prefix variable if one wasn't provided
#
# -----------------------------------------------------------------------
if test "$prefix" = "NONE" ; then
    #prefix=`(cd ..; pwd)`/install
    prefix=/usr/local
fi
echo "using prefix = $prefix"
AC_SUBST(prefix)

# -----------------------------------------------------------------------
#
# Set PWD variable (/bin/sh may inherit the wrong value...)
#
# -----------------------------------------------------------------------
PWD=`pwd`

SRC_ROOT=$PWD
AC_SUBST(SRC_ROOT)

# -----------------------------------------------------------------------
#
# C compiler and debugging flags
#
# -----------------------------------------------------------------------
AC_PROG_INSTALL
AC_PROG_RANLIB
AC_PROG_CPP
AC_PROG_CXX
    
AC_ARG_WITH(gcc, [--with-gcc         use gcc compiler], CC=gcc; CXX=g++)
AC_ARG_WITH(cc,  [--with-cc          use cc compiler], CC=cc; CXX=CC)

# -----------------------------------------------------------------------
#
# Read the package config files to find out where the X and Tcl/Tk 
# and other files and libraries are and how to make shared libraries.
#
# -----------------------------------------------------------------------

# sh procedure to find a config file: arg: 
# $1 = name in lower case ("rtd")

getConfig() {
  name=${1}Config.sh
  echo "checking for ${name}"
  TCL_CONFIG_DIR=""
  places="\
   ../../tclutil \
   ../../astrotcl \
   ../../rtd \
   ../../cat \
   ../../skycat \
   ../tclutil \
   ../astrotcl \
   ../rtd \
   ../cat \
   ../skycat \
   $prefix/lib/itcl \
   $prefix/lib \
   /vlt/dflow/lib/itcl \
   /vlt/dflow/lib \
   $TCL_LIBRARY/.. \
   $PWD/../tcl \
   /usr/local/lib/itcl \
   /usr/local/lib"
  for dir in $places; do
    if test -r $dir/${name} ; then
      TCL_CONFIG_DIR=$dir
      break
    fi
  done
  while test -z "$TCL_CONFIG_DIR" ; do
    echo "What directory contains ${name} ?"
    read TCL_CONFIG_DIR
    if test ! -r $TCL_CONFIG_DIR/${name} ; then
      echo "can't read $TCL_CONFIG_DIR/${name}"
      TCL_CONFIG_DIR=""
    fi
  done
    
  # source the Tcl config file
  . $TCL_CONFIG_DIR/${name}
}

# source the config files
getConfig tclutil
getConfig astrotcl
getConfig rtd
getConfig cat
getConfig skycat

# get -D defines
DEFS=$TCLUTIL_DEFS
AC_SUBST(DEFS)

# for now, just use the compiler settings used to build rtd
CC=$TCLUTIL_CC
CXX=$TCLUTIL_CXX
AC_SUBST(CC)
AC_SUBST(CXX)

# pass these valaues on from the Tclutil package
LD_VARNAME=$TCLUTIL_LD_VARNAME
AC_SUBST(LD_VARNAME)

LIB_RUNTIME_DIR=$TCLUTIL_LIB_RUNTIME_DIR
AC_SUBST(LIB_RUNTIME_DIR)

TCLTK_ROOT=$TCLUTIL_TCLTK_ROOT
AC_SUBST(TCLTK_ROOT)

# versions
AC_SUBST(TCLUTIL_VERSION)
AC_SUBST(ASTROTCL_VERSION)
AC_SUBST(RTD_VERSION)
AC_SUBST(CAT_VERSION)
AC_SUBST(SKYCAT_VERSION)

TCL_VERSION=$TCLUTIL_TCL_VERSION
TK_VERSION=$TCLUTIL_TK_VERSION
ITCL_VERSION=$TCLUTIL_ITCL_VERSION
ITK_VERSION=$TCLUTIL_ITK_VERSION
TCLX_VERSION=$TCLUTIL_TCLX_VERSION
BLT_VERSION=$TCLUTIL_BLT_VERSION

AC_SUBST(TCL_VERSION)
AC_SUBST(TK_VERSION)
AC_SUBST(ITCL_VERSION)
AC_SUBST(ITK_VERSION)
AC_SUBST(TCLX_VERSION)
AC_SUBST(BLT_VERSION)

if test "$RTD_NO_TCL" = "1"; then
    echo "*** Error: Please configure 'rtd' first without the --with-notcl option"
    exit 1
fi
if test "$CAT_NO_TCL" = "1"; then
    echo "*** Error: Please configure the 'cat' package first without the --with-notcl option"
    exit 1
fi

# -----------------------------------------------------------------------
#
# "makedepend" info
#
# normally, "makedepend" wants to know all of the path names and will
# cause make errors if the include path to the C++ includes is not given.
# Here we try to extract that information from the C++ preprocessor output.
# The syntax of the output is different for different compilers...
#
# -----------------------------------------------------------------------
CXX_INCLUDE=$TCLUTIL_CXX_INCLUDE
AC_SUBST(CXX_INCLUDE)
AC_SUBST(CXX_DEPEND_FLAGS)

# -----------------------------------------------------------------------
#
# Try to find the location of the static C++ libraries for g++ or CC and
# make links so that the binary version will not be depending on the C++ 
# shared library, which might not always be installed.
#
# -----------------------------------------------------------------------
AC_MSG_CHECKING([static C++ library ])
tmpfile=/tmp/tmp$$.C
tmp1=/tmp/tmp1$$
tmp2=/tmp/tmp2$$
echo 'main(){}' > $tmpfile
$CXX -v $tmpfile > $tmp1 2>&1
echo > $tmp2
for i in `cat $tmp1`; do 
	echo $i >> $tmp2
done
dirs="`grep -- -L $tmp2` /usr/lib /lib"
for i in $dirs ; do
	dir=`echo $i | sed -e 's/-L//'`
	if test -f $dir/libg++.a ; then
		(cd gaia; rm -f libg++.a libstdc++.a; ln -s $dir/libg++.a $dir/libstdc++.a . )
		AC_MSG_RESULT($dir/libg++.a)
		break
	fi
done
for i in $dirs ; do
	dir=`echo $i | sed -e 's/-L//'`
	if test -f $dir/libC.a ; then
		(cd gaia; rm -f libC.a; ln -s $dir/libC.a . )
		AC_MSG_RESULT($dir/libC.a)
		break
	fi
done
rm -f $tmp1
rm -f $tmp2
rm -f $tmpfile

# -----------------------------------------------------------------------
#
# set OS version string
#
# -----------------------------------------------------------------------
OSVERSION="`uname -s`-`uname -r`"
AC_SUBST(OSVERSION)

# -----------------------------------------------------------------------
#
# if we are using gcc, we can get better make dependencies using gcc -MM 
# otherwise we have to use  "makedepend", which sometimes gets confused. 
# This sets the make target to use:
#
# -----------------------------------------------------------------------
DEPEND_TARGET=$TCLUTIL_DEPEND_TARGET
AC_SUBST(DEPEND_TARGET)

# -----------------------------------------------------------------------
#
# use CDEBUGFLAGS for C and CXXDEBUGFLAGS for C++ compiler options
#
# -----------------------------------------------------------------------

# Note: one g77 bug on i386 machines only shows up when -O is not used !
AC_ARG_WITH(debug, [--with-debug       use debugging flag], 
	CDEBUGFLAGS="-g -O"; CXXDEBUGFLAGS="-g"; FDEBUGFLAGS="-g -O", 
	CDEBUGFLAGS="-O"; CXXDEBUGFLAGS="-O"; FDEBUGFLAGS=-O)

# Note: on HP-UX, g77 crashes on the file gaia/src/rtd1_patch.f, if
# -O is used! (Thats right, we need -O for linux, but can't use it
# on HP...)
if test "`uname`" = "HP-UX" -a "$FDEBUGFLAGS" = "-O" ; then
    FDEBUGFLAGS=""
fi

AC_SUBST(CDEBUGFLAGS)
AC_SUBST(FDEBUGFLAGS)
AC_SUBST(CXXDEBUGFLAGS)

# -----------------------------------------------------------------------
#
# Check if we are using (and creating) shared libraries 
# If so, we use the information in tclConfig.sh to determine the compiler
# options. 
#
# -----------------------------------------------------------------------

AC_ARG_ENABLE(shared, [--enable-shared    build and use shared libraries],
	SHARED=1,
	SHARED=0)
AC_SUBST(SHARED)

if test $SHARED -eq 0 ; then
	echo "Don't build shared libraries"
else
	echo "Will build shared libraries"
fi

# Flags to pass to cc when compiling the components of a shared library:
SHLIB_CFLAGS=$TCLUTIL_SHLIB_CFLAGS

# Base command to use for combining object files into a shared library:
# Note: "-z text" on solaris causes problems here with g77 undefined symbols...
# We also need to ignore the error about "main" being multiply defined in libf2c.a.
SHLIB_LD=`echo $TCLUTIL_SHLIB_LD | sed -e 's/-z text//'`

# Need this flag for solaris when building a binary, since libf2c also defines main()
SHLIB_IGNORE_MULT_DEFS=""
if test "$SHLIB_LD" != "$TCLUTIL_SHLIB_LD" ; then
    SHLIB_IGNORE_MULT_DEFS="-z muldefs"
fi

# Suffix to use for the name of a shared library.
SHLIB_SUFFIX=$TCLUTIL_SHLIB_SUFFIX

# Either '$LIBS' (if dependent libraries should be included when linking
# shared libraries) or an empty string.  See Tcl's configure.in for more
SHLIB_LD_LIBS=$TCLUTIL_SHLIB_LD_LIBS

# Library file(s) to include in tclsh and other base applications
# in order to provide facilities needed by DLOBJ above.
DL_LIBS=$TCLUTIL_DL_LIBS

# Flags to pass to the compiler when linking object files into
# an executable tclsh or tcltest binary.
LD_FLAGS=$TCLUTIL_LD_FLAGS

# Flags to pass to cc, such as "-Wl,-R,/usr/local/tcl/lib", that tell the
# run-time dynamic linker where to look for shared libraries such as
# libtcl.so.  Used when linking applications.  Only works if there
# is a variable "LIB_RUNTIME_DIR" defined in the Makefile.
LD_SEARCH_FLAGS=$TCLUTIL_LD_SEARCH_FLAGS

AC_SUBST(SHLIB_CFLAGS)
AC_SUBST(SHLIB_LD)
AC_SUBST(SHLIB_IGNORE_MULT_DEFS)
AC_SUBST(SHLIB_SUFFIX)
AC_SUBST(SHLIB_LD_LIBS)
AC_SUBST(DL_LIBS)
AC_SUBST(LD_FLAGS)
AC_SUBST(LD_SEARCH_FLAGS)


# -----------------------------------------------------------------------
# Check the location of X libraries and includes.
# -----------------------------------------------------------------------

# -I switch(es) to use to make all of the X11 include files accessible:
XINCSW=$TCLUTIL_XINCSW

# Linker switch(es) to use to link with the X11 library archive.
XLIBSW=$TCLUTIL_XLIBSW

# Linker switch(es) to use to link with the static X11 library archive.
XLIBSW=$TCLUTIL_XLIBSW

AC_SUBST(XINCSW)
AC_SUBST(XLIBSW)

# -----------------------------------------------------------------------
#
# Tcl/Tk information
#
# -----------------------------------------------------------------------

# get the -L and -l library flags

TCL_LIB_SPEC=$TCLUTIL_TCL_LIB_SPEC
TK_LIB_SPEC=$TCLUTIL_TK_LIB_SPEC
TCLX_LIB_SPEC=$TCLUTIL_TCLX_LIB_SPEC
ITCL_LIB_SPEC=$TCLUTIL_ITCL_LIB_SPEC
ITK_LIB_SPEC=$TCLUTIL_ITK_LIB_SPEC
BLT_LIB_SPEC=$TCLUTIL_BLT_LIB_SPEC

AC_SUBST(TCL_LIB_SPEC)
AC_SUBST(TK_LIB_SPEC)
AC_SUBST(TCLX_LIB_SPEC)
AC_SUBST(ITCL_LIB_SPEC)
AC_SUBST(ITK_LIB_SPEC)
AC_SUBST(BLT_LIB_SPEC)

# get the -I include flags

TCLINCSW=$TCLUTIL_TCLINCSW
TKINCSW=$TCLUTIL_TKINCSW

AC_SUBST(TCLINCSW)
AC_SUBST(TKINCSW)

# -----------------------------------------------------------------------
# tcl package library files
# -----------------------------------------------------------------------
TCL_LIBRARY=$TCLUTIL_TCL_LIBRARY
AC_SUBST(TCL_LIBRARY)

TK_LIBRARY=$TCLUTIL_TK_LIBRARY
AC_SUBST(TK_LIBRARY)

BLT_LIBRARY=$TCLUTIL_BLT_LIBRARY
AC_SUBST(BLT_LIBRARY)

AC_SUBST(TCLUTIL_LIBRARY)
AC_SUBST(ASTROTCL_LIBRARY)
AC_SUBST(RTD_LIBRARY)
AC_SUBST(CAT_LIBRARY)
AC_SUBST(SKYCAT_LIBRARY)

# -----------------------------------------------------------------------
# system libs
# -----------------------------------------------------------------------
LIBS=$TCLUTIL_LIBS
AC_SUBST(LIBS)

# -----------------------------------------------------------------------
# Check if we are using the installed TCLUTIL library and include files
# or local development versions
# -----------------------------------------------------------------------
if test -d $TCLUTIL_SRC_ROOT; then
	TCLUTIL_LIB=$TCLUTIL_BUILD_LIB_SPEC
	TCLUTIL_LIB_STATIC=$TCLUTIL_BUILD_LIB_SPEC_STATIC
	TCLUTIL_BUILD_LIBRARY=$TCLUTIL_BUILD_LIBRARY
else
	TCLUTIL_LIB=$TCLUTIL_LIB_SPEC
	TCLUTIL_LIB_STATIC=$TCLUTIL_LIB_SPEC_STATIC
	TCLUTIL_BUILD_LIBRARY=$TCLUTIL_LIBRARY
fi
AC_SUBST(TCLUTIL_SRC_ROOT)
AC_SUBST(TCLUTIL_LIB)
AC_SUBST(TCLUTIL_LIB_SPEC)
AC_SUBST(TCLUTIL_LIB_STATIC)
AC_SUBST(TCLUTIL_BUILD_LIBRARY)
AC_SUBST(TCLUTIL_BUILD_INCLUDES)

# -----------------------------------------------------------------------
# Check if we are using the installed ASTROTCL library and include files
# or local development versions
# -----------------------------------------------------------------------
if test -d $ASTROTCL_SRC_ROOT; then
	ASTROTCL_LIB=$ASTROTCL_BUILD_LIB_SPEC
	ASTROTCL_LIB_STATIC=$ASTROTCL_BUILD_LIB_SPEC_STATIC
	ASTROTCL_BUILD_LIBRARY=$ASTROTCL_BUILD_LIBRARY
else
	ASTROTCL_LIB=$ASTROTCL_LIB_SPEC
	ASTROTCL_LIB_STATIC=$ASTROTCL_LIB_SPEC_STATIC
	ASTROTCL_BUILD_LIBRARY=$ASTROTCL_LIBRARY
fi
AC_SUBST(ASTROTCL_SRC_ROOT)
AC_SUBST(ASTROTCL_LIB)
AC_SUBST(ASTROTCL_LIB_SPEC)
AC_SUBST(ASTROTCL_LIB_STATIC)
AC_SUBST(ASTROTCL_BUILD_LIBRARY)
AC_SUBST(ASTROTCL_BUILD_INCLUDES)

# -----------------------------------------------------------------------
# Check if we are using the installed RTD library and include files
# or local development versions
# -----------------------------------------------------------------------
if test -d $RTD_SRC_ROOT; then
	RTD_LIB=$RTD_BUILD_LIB_SPEC
	RTD_LIB_STATIC=$RTD_BUILD_LIB_SPEC_STATIC
	RTD_BUILD_LIBRARY=$RTD_BUILD_LIBRARY
else
	RTD_LIB=$RTD_LIB_SPEC
	RTD_LIB_STATIC=$RTD_LIB_SPEC_STATIC
	RTD_BUILD_LIBRARY=$RTD_LIBRARY
fi
AC_SUBST(RTD_SRC_ROOT)
AC_SUBST(RTD_LIB)
AC_SUBST(RTD_LIB_SPEC)
AC_SUBST(RTD_LIB_STATIC)
AC_SUBST(RTD_BUILD_LIBRARY)
AC_SUBST(RTD_BUILD_INCLUDES)

# -----------------------------------------------------------------------
# Check if we are using the installed CAT library and include files
# or local development versions
# -----------------------------------------------------------------------
if test -d $CAT_SRC_ROOT; then
	CAT_LIB=$CAT_BUILD_LIB_SPEC
	CAT_LIB_STATIC=$CAT_BUILD_LIB_SPEC_STATIC
	CAT_BUILD_LIBRARY=$CAT_BUILD_LIBRARY
else
	CAT_LIB=$CAT_LIB_SPEC
	CAT_LIB_STATIC=$CAT_LIB_SPEC_STATIC
	CAT_BUILD_LIBRARY=$CAT_LIBRARY
fi
AC_SUBST(CAT_SRC_ROOT)
AC_SUBST(CAT_LIB)
AC_SUBST(CAT_LIB_SPEC)
AC_SUBST(CAT_LIB_STATIC)
AC_SUBST(CAT_BUILD_LIBRARY)
AC_SUBST(CAT_BUILD_INCLUDES)

# -----------------------------------------------------------------------
# Check if we are using the installed SKYCAT library and include files
# or local development versions
# -----------------------------------------------------------------------
if test -d $SKYCAT_SRC_ROOT; then
	SKYCAT_LIB=$SKYCAT_BUILD_LIB_SPEC
	SKYCAT_LIB_STATIC=$SKYCAT_BUILD_LIB_SPEC_STATIC
	SKYCAT_BUILD_LIBRARY=$SKYCAT_BUILD_LIBRARY
else
	SKYCAT_LIB=$SKYCAT_LIB_SPEC
	SKYCAT_LIB_STATIC=$SKYCAT_LIB_SPEC_STATIC
	SKYCAT_BUILD_LIBRARY=$SKYCAT_LIBRARY
fi
AC_SUBST(SKYCAT_SRC_ROOT)
AC_SUBST(SKYCAT_LIB)
AC_SUBST(SKYCAT_LIB_SPEC)
AC_SUBST(SKYCAT_LIB_STATIC)
AC_SUBST(SKYCAT_BUILD_LIBRARY)
AC_SUBST(SKYCAT_BUILD_INCLUDES)

#--------------------------------------------------------------------
#       Check for local installation point of Starlink s/w.
#--------------------------------------------------------------------
AC_MSG_CHECKING([Starlink installation])
STARLINK=${STARLINK=/star}
if test ! -x "${STARLINK}"; then
    echo "What directory is the root of the Starlink development system?"
    read STARLINK
fi
AC_MSG_RESULT($STARLINK)
AC_SUBST(STARLINK)

#  Name of Fortran compiler
if test "${FC}" = ""; then
   AC_CHECK_PROGS( FC, g77 f77, fort77 )
fi

#  Compilation flags, for g77 we also need -fno-second-underscore.
AC_MSG_CHECKING([Fortran flags])
if test "${FC}" = "g77"; then
   FFLAGS="$FDEBUGFLAGS -fno-second-underscore"
fi
AC_MSG_RESULT($FFLAGS)
AC_SUBST(FFLAGS)

#  How to link files.
AC_PROG_LN_S()
LINK=${LN_S}
AC_SUBST(LINK)

#  ALINK script location.
AC_CHECK_PROG(ALINK,alink,alink,${STARLINK}/bin/alink)

#--------------------------------------------------------------------
#  Try to determine the extra libraries required to link a
#  C/C++ program that has f77 libraries or subroutines.
#--------------------------------------------------------------------
#  Create a dummy program.
AC_MSG_CHECKING([mixed language libraries])

echo >  /tmp/tmp$$.f "      program tmp"
echo >> /tmp/tmp$$.f "      end"

#  Compile it and get the complete ld line used (assumes -v echos
#  all commands performed by compiler).
${FC} -v -o /tmp/tmp$$ /tmp/tmp$$.f 2> /tmp/tmp$$.log 1>&2

#  Now for the tricky bit.
#  First we need to strip out the ld line. First form assumes /xxx/ld
#  and second ' ld ' (g77).
ldline=`grep '^[[^\ ]]*ld[[\ ]]' /tmp/tmp$$.log`
if test "$ldline" = "" ; then
   ldline=`grep '^[[\ ]]ld[[\ ]]' /tmp/tmp$$.log`
fi

#  Now we try to keep words that are -lxxx -Lxxx */lib*.a,
#  i.e. library references.
F77_LIBS=""
for w in $ldline
do
   case $w in
      -l* | -L* | */lib*\.a ) F77_LIBS="$F77_LIBS $w";;
   esac
done
rm /tmp/tmp$$
rm /tmp/tmp$$.f
rm /tmp/tmp$$.log
AC_MSG_RESULT($F77_LIBS)
AC_SUBST(F77_LIBS)

#   If using g77, locate libf2c.a and remove main.o, this
#   clashes with main in tkAppinit.C.
if test "$FC" = "g77"; then
   AC_MSG_CHECKING([main in -lf2c])

#  Create and compile a dummy program. The compilation uses
#  -v to get all the information we should require.
   ffile=/tmp/tmp$$
   echo >  ${ffile}.f "      program test"
   echo >> ${ffile}.f "      end"
   $FC -v -o $ffile ${ffile}.f 2> ${ffile}.log 1>&2

#  Look for ld line.

   ldline=`grep '^[[^\ ]]*ld[[\ ]]' ${ffile}.log`
   if test "$ldline" = "" ; then
      ldline=`grep '^[\ ]ld[\ ]' ${ffile}.log`
   fi

#  Now we try each word of this line until we can concatenate it
#  with the string "libf2c.a" and locate the library.  i.e. directory
#  references for libraries
   for w in $ldline ""; do
      cw=`echo $w | sed -e 's:-L::'`
      if test -f "${cw}/libf2c.a"; then
         cp -p ${cw}/libf2c.a ./libf2c.a
         ar d ./libf2c.a main.o
         AC_MSG_RESULT([removed])
         break
      fi
   done

#  Clean up the mess.
   rm -f /tmp/tmp$$
   rm -f /tmp/tmp$$.f
   rm -f /tmp/tmp$$.log
fi

#--------------------------------------------------------------------
#
# We need to resolve some extra FORTRAN related references when making
# a shared library.
#
#--------------------------------------------------------------------


if test "${FC}" = "g77"; then
   AC_CHECK_LIB(f2c, main, [LIBS="$LIBS -lf2c"])

   if test "`uname`" = "Linux"; then
      AC_CHECK_LIB(gcc, main, [LIBS="$LIBS -lgcc"])
   fi

fi
AC_SUBST(LIBS)

#--------------------------------------------------------------------
#	Propagate prefix argument as installation directory.
#--------------------------------------------------------------------
DESTDIR="$prefix"
BINDIR="$DESTDIR/bin"
LIBDIR="$DESTDIR/lib"
INCDIR="$DESTDIR/include/gaia"
MANDIR="$DESTDIR/man"
GAIA_DIR="$LIBDIR/gaia"
GAIA_LIBRARY=$GAIA_DIR
GAIA_BUILD_LIBRARY=$SRC_ROOT/gaia/library
GAIA_BUILD_INCLUDES="-I$SRC_ROOT/gaia/include"

AC_SUBST(DESTDIR)
AC_SUBST(BINDIR)
AC_SUBST(LIBDIR)
AC_SUBST(INCDIR)
AC_SUBST(MANDIR)
AC_SUBST(GAIA_DIR)
AC_SUBST(GAIA_LIBRARY)
AC_SUBST(GAIA_BUILD_LIBRARY)
AC_SUBST(GAIA_BUILD_INCLUDES)

#--------------------------------------------------------------------
#	Set name of library to link
#--------------------------------------------------------------------

# String to pass to linker to pick up the Gaia library from its
# build directory.
GAIA_BUILD_LIB_SPEC="-L$SRC_ROOT -lgaia"

# static version of above
GAIA_BUILD_LIB_SPEC_STATIC="$SRC_ROOT/libgaia.a"

# String to pass to linker to pick up the Gaia library from its
# installed directory.
GAIA_LIB_SPEC="-L$LIBDIR -lgaia"

# static version of above
GAIA_LIB_SPEC_STATIC="$LIBDIR/libgaia.a"

# if we are not building shared libraries, make sure we use the static
# libraries and not get an outdated shared library by accident
if test "$SHARED" = "0"
then
    GAIA_BUILD_LIB_SPEC=$GAIA_BUILD_LIB_SPEC_STATIC
    GAIA_LIB_SPEC=$GAIA_LIB_SPEC_STATIC
fi

AC_SUBST(GAIA_BUILD_LIB_SPEC)
AC_SUBST(GAIA_BUILD_LIB_SPEC_STATIC)
AC_SUBST(GAIA_LIB_SPEC)
AC_SUBST(GAIA_LIB_SPEC_STATIC)

#--------------------------------------------------------------------
#	Print out some of the more important settings 
#--------------------------------------------------------------------
echo "
Configuration results (edit and run ./config.status to modify):

C   Compiler                           => $CC
C++ Compiler                           => $CXX
C++ include flag                       => $CXX_INCLUDE

X include files                        => $XINCSW
Tcl include files                      => $TCLINCSW
Tk include files                       => $TKINCSW

X libs                                 => $XLIBSW
Tcl lib                                => $TCL_LIB_SPEC
Tk lib                                 => $TK_LIB_SPEC
TclX lib                               => $TCLX_LIB_SPEC
Itcl lib                               => $ITCL_LIB_SPEC
Itk lib                                => $ITK_LIB_SPEC
BLT lib                                => $BLT_LIB_SPEC
Tclutil lib                            => $TCLUTIL_LIB
Astrotcl lib                           => $ASTROTCL_LIB
Rtd lib                                => $RTD_LIB
Cat lib                                => $CAT_LIB

static Tclutil library                 => $TCLUTIL_LIB_STATIC
static Astrotcl library                => $ASTROTCL_LIB_STATIC
static Rtd library                     => $RTD_LIB_STATIC
static Cat library                     => $CAT_LIB_STATIC

directory containing tcl library files => $TCL_LIBRARY
directory containing tk  library files => $TK_LIBRARY
directory containing blt library files => $BLT_LIBRARY

Take local tclutil library files from  => $TCLUTIL_BUILD_LIBRARY
Take local astrotcl library files from => $ASTROTCL_BUILD_LIBRARY
Take local rtd library files from      => $RTD_BUILD_LIBRARY
Take local cat library files from      => $CAT_BUILD_LIBRARY

install gaia binaries in               => $BINDIR
install gaia libraries in              => $LIBDIR
install gaia include files in          => $INCDIR
install tcl library files in           => $GAIA_DIR

directory containing Tclutil library files  => $TCLUTIL_LIBRARY
directory containing Astrotcl library files => $ASTROTCL_LIBRARY
directory containing Cat library files      => $CAT_LIBRARY
directory containing Rtd library files      => $RTD_LIBRARY
directory containing Skycat library files   => $SKYCAT_LIBRARY
"

#--------------------------------------------------------------------
#	Generate the Makefiles etc...
#--------------------------------------------------------------------
AC_OUTPUT(\
	Makefile \
        gaiaConfig.sh \
\
	gaia/Makefile \
	gaia/src/Makefile \
	gaia/include/Makefile \
	gaia/man/Makefile \
	gaia/library/Makefile \
	gaia/library/gaia_version.tcl \
	gaia/demos/Makefile \
	gaia/demos/gaia.sh \
	gaia/demos/gaia_env.sh \
	gaia/bitmaps/Makefile \
	gaia/library/pkgIndex.tcl \
\
	, chmod +x gaia/demos/gaia.sh; sh makelinks; make -k depend \
)

