      SUBROUTINE SCULIB_SKYDIP_XISQ (XISQ, N, FIT, STATUS)
*+
*  Name:
*     SCULIB_SKYDIP_XISQ

*  Purpose:
*     calculate chi-square of sky fit

*  Description:
*     If entered with good status this routine calculates the chi-squared
*     between a SKYDIP dataset and the theoretical curve generated by
*     the the sky and telescope parameters in FIT. Data points with bad
*     quality will be ignored. If no valid data points are found then an 
*     error will be reported and bad status returned. The data are passed
*     in via common.
*
*     The FIT vector is composed as follows:-
*           - x(1) = ETA_TEL
*           - x(2) = B
*           - x(3) = TAU
*
*     For a given airmass:-
*
*         Jtheory = (1 - ETA_TEL) * J_TEL + ETA_TEL * J_ATM *
*                   (1 - B * EXP (-TAU * Airmass(i))
*
*
*           J_ATM = J_AMB * X_G 
*
*             X_G = 1 + h1 * h2 * EXP (-TAU * Airmass(i) / X_Gconst)
*          .            -------
*                        J_AMB
*

*  Invocation:
*     CALL SCULIB_SKYDIP_XISQ (XISQ, N, FIT, STATUS)

*  Arguments:
*     XISQ                     = DOUBLE PRECISION (Returned)
*            the chi-squared value of the current fit
*     N                        = INTEGER (Given)
*            the number of parameters being fit, should be 3
*     FIT (N)                  = DOUBLE PRECISION (Given)
*            the fit parameters:- 
*                               -  FIT (1) = ETA_TEL
*                               -  FIT (2) = B
*                               -  FIT (3) = TAUZ
*     STATUS                   = INTEGER (Given and returned)
*            global status


*  Authors:
*     J.Lightfoot (REVAD::JFL)

*  Copyright:
*     Copyright (C) 1995,1996,1997,1998,1999 Particle Physics and Astronomy
*     Research Council. All Rights Reserved.

*  Method:

*  Deficiencies:

*  Bugs:


*  History:
*     11-MAR-1997: Original version.
*    endhistory

*-


*  Type Definitions:
      IMPLICIT NONE

*  Global constants:
      INCLUDE 'SAE_PAR'
      INTEGER MAX_FIT_DATA               ! max number of measurements
      PARAMETER (MAX_FIT_DATA = 500)        

*  Arguments Given:
      INTEGER N
      DOUBLE PRECISION FIT (N)

*  Arguments Given & Returned:

*  Arguments Returned:
      DOUBLE PRECISION XISQ

*  Status:
      INTEGER STATUS

*  External references:

*  Global variables:
      DOUBLE PRECISION AIRMASS (MAX_FIT_DATA)
                                         ! airmasses at which measurements
                                         ! were made
      DOUBLE PRECISION B_HI              ! upper limit for b
      DOUBLE PRECISION B_LO              ! lower limit for b
      DOUBLE PRECISION ETA_TEL_HI        ! upper limit for eta_tel
      DOUBLE PRECISION ETA_TEL_LO        ! lower limit for eta_tel
      DOUBLE PRECISION J_AMB             ! brightness temp of air
      DOUBLE PRECISION J_MEAS (MAX_FIT_DATA)
                                         ! measured sky temperatures
      INTEGER          J_QUALITY (MAX_FIT_DATA)
                                         ! quality on J_MEAS
      DOUBLE PRECISION J_TEL             ! brightness temp of telescope
      DOUBLE PRECISION J_VARIANCE (MAX_FIT_DATA)
      INTEGER          M                 ! number of skydip measurements
      COMMON /SCULIB_SKYDIP_FIT_DATA_I/ J_QUALITY,
     :                                  M
      COMMON /SCULIB_SKYDIP_FIT_DATA_D/ AIRMASS,
     :                                  B_HI,
     :                                  B_LO,
     :                                  ETA_TEL_HI,
     :                                  ETA_TEL_LO,
     :                                  J_AMB,
     :                                  J_MEAS,
     :                                  J_TEL,
     :                                  J_VARIANCE

*  Local Constants:
      DOUBLE PRECISION H1                ! temperature drop / km
      PARAMETER (H1 = -6.5D0)
      DOUBLE PRECISION H2                ! scale height of absorber
      PARAMETER (H2 = 2.0D0)
      DOUBLE PRECISION X_GCONST          ! fudge constant
      PARAMETER (X_GCONST = 3.669383D0)

*  Local variables:
      DOUBLE PRECISION B                 ! b parameter of fit
      DOUBLE PRECISION ETA_TEL           ! telescope transmission of fit
      INTEGER          I                 ! DO loop index
      DOUBLE PRECISION J_ATM             ! brightness temperature of atmosphere
      DOUBLE PRECISION J_THEORY          ! theoretical sky brightness 
                                         ! temperature at airmass measured
      INTEGER          N_ADDED           ! number of data elements added
                                         ! into chi-squared 
      DOUBLE PRECISION TAU               ! zenith optical depth of fit
      DOUBLE PRECISION X_G               ! fudge factor

*  Internal References:

*  Local data:

*.

*  initialise variables

      XISQ = 0.0D0
      N_ADDED = 0

*  unpack FIT vector

      ETA_TEL = FIT (1)
      B = FIT (2)
      TAU = FIT (3)

*  calculate theoretical function for each airmass and work out chi-squared

      DO I = 1, M
         IF (J_QUALITY(I) .EQ. 0) THEN
            IF (ABS(TAU * AIRMASS(I)) .LT. 20.0D0) THEN
               X_G = 1.0D0 + (H1 * H2 / J_AMB) * 
     :           EXP (-TAU * AIRMASS(I) / X_GCONST)

               J_ATM = J_AMB * X_G 

               J_THEORY = (1.0D0 - ETA_TEL) * J_TEL +
     :           ETA_TEL * J_ATM * (1.0D0 - B * EXP (-TAU * AIRMASS(I)))
            ELSE
               J_ATM = J_AMB

               J_THEORY = (1.0D0 - ETA_TEL) * J_TEL +
     :           ETA_TEL * J_ATM
            END IF

            IF (J_VARIANCE(I) .GT. 0.0D0) THEN
               XISQ = XISQ + (J_THEORY - J_MEAS(I)) **2 / 
     :           MAX (J_VARIANCE(I), 1.0D-2)
            END IF

            N_ADDED = N_ADDED + 1
         END IF
      END DO

*  adjust chi-squared if have crossed a boundary

      IF (ETA_TEL .LT. ETA_TEL_LO) THEN
         XISQ = XISQ + 10000.0D0 * (ETA_TEL_LO - ETA_TEL)
      ELSE IF (ETA_TEL .GT. ETA_TEL_HI) THEN
         XISQ = XISQ + 10000.0D0 * (ETA_TEL - ETA_TEL_HI)
      END IF      

      IF (B .LT. B_LO) THEN
         XISQ = XISQ + 10000.0D0 * (B_LO - B)
      ELSE IF (B .GT. B_HI) THEN
         XISQ = XISQ + 10000.0D0 * (B - B_HI)
      END IF
      
      IF (TAU .LT. 0.0D0) THEN
         XISQ = XISQ + 10000.0D0 * (-TAU)
      END IF

*  error?

      IF (N_ADDED .EQ. 0) THEN
         STATUS = SAI__ERROR
         CALL ERR_REP (' ', 'SCULIB_SKYDIP_XISQ: no valid data '//
     :     'points for chi-squared calculation', STATUS)
      ENDIF

      END
