      SUBROUTINE CCG1_MXD1<T>( BAD, NPIX, NLINES, STACK, VARNCE, 
     :                         RESULT, RESVAR, POSIND, WRK1, STATUS )
*+
*  Name:
*     CCG1_MXD1x

*  Purpose:
*     Combines data lines using the maximum value.

*  Language:
*     Starlink Fortran 77

*  Invocation:
*     CALL CCG1_MXD1x( BAD, NPIX, NLINES, STACK, VARNCE, 
*                       RESULT, RESVAR, POSIND, WRK1, STATUS )

*  Description:
*     This routine accepts an array consisting a series of (vectorised)
*     lines of data.  The data values in the lines are then collapsed
*     to the maximum value in each line.   The output maxima are 
*     returned in the array RESULT.  Each output variance value is the
*     corresponding variance of the maximum data value, propagated from
*     the input variances given in array VARNCE and are returned in the 
*     array RESVAR.  The vectorised pixel indices corresponding to the 
*     maxima are also returned in POSIND.

*  Arguments:
*     BAD = LOGICAL (Given)
*        If true, there may be bad pixels present in the array.  If
*        false, it is safe not to check for bad values.
*     NPIX = INTEGER (Given)
*        The number of pixels in a line of data.
*     NLINES = INTEGER (Given)
*        The number of lines of data in the stack.
*     STACK( NPIX, NLINES ) = <TYPE> (Given)
*        The array of lines which are to be combined into a single line.
*     VARNCE( NPIX, NLINES ) = <TYPE> (Given)
*        The data variances.
*     RESULT( NPIX ) = <TYPE> (Returned)
*        The output line of data.
*     RESVAR( NPIX ) = <TYPE> (Returned)
*        The output variances.
*     POSIND( NPIX ) = INTEGER (Returned)
*        The pixel indices of the maxima in each output pixel.
*     WRK1( NLINES ) = DOUBLE PRECISION (Returned)
*        Workspace for calculations.
*     STATUS = INTEGER (Given and Returned)
*        The global status.

*  Notes:
*     -  There is a routine for each numeric data type: replace "x" in
*     the routine name by D, R, I, W, UW, B or UB as appropriate.  The
*     STACK, VARNCE, RESULT, and RESVAR arguments supplied to the 
*     routine must have the data type specified.

*  Authors:
*     MJC: Malcolm J. Currie (STARLINK)
*     {enter_new_authors_here}

*  History:
*     2005 December 22 (MJC):
*        Original version.
*     {enter_changes_here}

*  Bugs:
*     {note_any_bugs_here}

*-

*  Type Definitions:
      IMPLICIT NONE              ! No implicit typing

*  Global Constants:
      INCLUDE 'SAE_PAR'          ! Standard SAE constants
      INCLUDE 'PRM_PAR'          ! PRIMDAT constants

*  Arguments Given:
      LOGICAL BAD
      INTEGER NPIX
      INTEGER NLINES
      <TYPE> STACK( NPIX, NLINES )
      <TYPE> VARNCE( NPIX, NLINES )

*  Arguments Given and Returned:
      DOUBLE PRECISION WRK1( NLINES )

*  Arguments Returned:
      <TYPE> RESULT( NPIX )
      <TYPE> RESVAR( NPIX )
      INTEGER POSIND( NPIX )

*  Status:
      INTEGER STATUS             ! Global status

*  Local Variables:
      INTEGER I                  ! Loop variable
      INTEGER J                  ! Loop variable
      DOUBLE PRECISION MAXMUM    ! Maximum value
      DOUBLE PRECISION MINMUM    ! Minimum value (dummy)
      INTEGER MAXPOS             ! Pixel index of maximum
      INTEGER MINPOS             ! Pixel index of minimum
      INTEGER NBAD               ! Number of bad values

*  Internal References:
      INCLUDE 'NUM_DEC_CVT'      ! NUM_ type conversion functions
      INCLUDE 'NUM_DEF_CVT'      ! Define functions...

*.

*  Check inherited global status.
      IF ( STATUS .NE. SAI__OK ) RETURN

*  Loop over for all possible output pixels.
      DO 1 I = 1, NPIX

*  Loop over all possible contributing pixels.
         DO 2 J = 1, NLINES

*  Convert input type to double-precision value.
            WRK1( J ) = NUM_<T>TOD( STACK( I, J ) )

    2    CONTINUE

*  Find the extreme values in the contributing pixels, retaining just
*  the maximum.  Start a new error context so we can annul any errors
*  arising the KAPLIBS routine.
         CALL ERR_MARK
         CALL KPG1_MXMND( BAD, NLINES, WRK1, NBAD, MAXMUM,
     :                    MINMUM, MAXPOS, MINPOS, STATUS )

*  We can cope with the error that all the supplied values are bad.
         IF ( STATUS .NE. SAI__OK ) THEN
            CALL ERR_ANNUL( STATUS )
            MAXMUM = VAL__BADD
         END IF

*  End the error context.
         CALL ERR_RLSE

*  Trap numeric errors.
         IF ( MAXMUM .EQ. VAL__BADD ) THEN
            RESULT( I ) = VAL__BAD<T>
            RESVAR( I ) = VAL__BAD<T>
            POSIND( I ) = VAL__BADI
         ELSE
            POSIND( I ) = MAXPOS
            RESULT( I ) = NUM_DTO<T>( MAXMUM )
            RESVAR( I ) = VARNCE( I, POSIND( I ) )
         END IF

    1 CONTINUE

      END
