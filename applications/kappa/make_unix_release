#!/bin/tcsh
# Name:
#    make_unix_release
#
# Purpose:
#    Do unix specific bits required to make a release of kappa.
#
# Type of Module:
#    Shell script
#
# Description:
#    This command creates a compressed tar file containing a release of the 
#    kappa package. It is called from within the make_release script. 
#    
# Invocation:
#    make_unix_release
#
# Authors:
#    DSB: David S. Berry (STARLINK)
#    {enter_new_authors_here}
#
# History:
#    4-DEC-1997 (DSB):
#       Original version, based on POLPACK equivalent.
#    {enter_changes_here}
#
# Bugs:
#    {note_any_bugs_here}
#-

#  Ensure the sdt grp command is available.
      source $SDT_DIR/startup.csh

#  Store the name of the release directory, and go there.
      set REL_DIR = ${TEMPDIR}/kappa_${SYSTEM}
      cd ${REL_DIR}

#  See if the hypertext documents are to be re-created.
#      echo -n "Re-create kappa.htx? (y/n): "
#      set ans = $<
#      if( $ans == "y" ) then
#         star2html sun95.tex
#         tar -cvhf ./sun95.htx_tar sun95.htx/*
#         rm -rf sun95.htx
#         mv ./sun95.htx_tar ${KAPPA_DEV}/..
#      endif

#  Copy the hypertext docs to the release directory.
      cp ${KAPPA_DEV}/../sun95.htx_tar ${REL_DIR}
      cp ${KAPPA_DEV}/../sun221.htx_tar ${REL_DIR}

#  Ensure all files are accessable.
      chmod 777 *

#  Edit in the current package version number into any required source files.
      set vers = `grep "PKG_VERS = " makefile`
      if ( "$vers" == "" ) then
         echo "Can't get package version number from makefile."
         exit
      endif

      foreach file (kappa_mon.f kapview_mon.f ndfpack_mon.f)
         sed -e "s#PKG_VERS#$vers[3]#g" $file > temp
         mv -f temp $file
      end

#  Create the doc directory for inclusion in kappa_iraf.tar and move all the 
#  iraf help files into it.
      mkdir doc
      foreach file (`grp iraf_docs`)
         mv ${file} doc
      end

#  Now that the iraf help file kappa.hlp has been moved, we can rename the 
#  starlink help file kappa.star-hlp to its correct name (kappa.hlp).
      mv kappa.star-hlp kappa.hlp

#  Create each of the tar files. First do the system-independant ones
#  listed in group TAR_FILES_A...
      foreach file (`grp tar_files_a`)
         tar -cvh -f ${TEMPDIR}/${file}.tar `grp ${file}`
         rm -f `grp ${file}`
         mv ${TEMPDIR}/${file}.tar ${REL_DIR}
      end

#  Now do the system-dependant ones listed in group TAR_FILES_B. The contents
#  of these tar files have the SYSTEM value appended to the end of each file 
#  name.
      foreach file (`grp tar_files_b`)
         setenv news " "
         foreach item (`grp ${file}`)
            setenv new `echo ${item} | awk -F. '{print $1"."$2}'` 
            mv $item $new
            setenv news "$news $new"
         end

         tar -cvh -f ${TEMPDIR}/${file}.tar $news
         rm -f $news
         mv ${TEMPDIR}/${file}.tar ${REL_DIR}
      end

#  Tar up all the release files and then remove them.
      tar -cvh -f ${TEMPDIR}/kappa.tar `grp unix_total` 
      rm `grp unix_total` 
      mv ${TEMPDIR}/kappa.tar ${REL_DIR}
      rm -rf doc

#  Compress it.
      compress kappa.tar

      exit
