      SUBROUTINE KPS1_RS1<T>( NDIM, IDIM, MAP, DIMI, DIMO, INTERP,
     :                        PARAMS, CONSRV, HASVAR, HASQUA, HASAXI,
     :                        DATI, VARI, QUAI, AXI, BAD, DATWKI,
     :                        VARWKI, QUAWKI, DATWKO, VARWKO, QUAWKO,
     :                        DATO, VARO, QUAO, AXO, STATUS )
*+
*  Name:
*     KPS1_RS1<T>

*  Purpose:
*     Resamples one dimension of an array by squashing or stretching.

*  Language:
*     Starlink Fortran 77

*  Invocation:
*     CALL KPS1_RS1<T>( NDIM, IDIM, MAP, DIMI, DIMO, INTERP, PARAMS,
*                       CONSRV, HASVAR, HASQUA, HASAXI, DATI, VARI,
*                       QUAI, AXI, BAD, DATWKI, VARWKI, QUAWKI,
*                       DATWKO, VARWKO, QUAWKO, DATO, VARO,
*                       QUAO, AXO, STATUS )

*  Description:
*     This routine resamples a data and associated variance, quality and
*     axis arrays by applying a linear transformation to it along a
*     single dimension.

*  Arguments:
*     NDIM = INTEGER (Given)
*        The number of dimensions in the arrays.
*     IDIM = INTEGER (Given)
*        The index of the dimension along which to resample.
*     MAP = INTEGER (Given)
*        The Mapping from input GRID co-ordinates to output GRID
*        co-ordsinates. This should have NDIM inputs and NDIM outputs.
*     DIMI( NDIM ) = INTEGER (Given)
*        The dimensions of the input arrays DATI and VARI.
*     DIMO( NDIM ) = INTEGER (Given)
*        The dimensions of the output arrays DATO and VARO.
*     INTERP = INTEGER (Given)
*        The resampling scheme as passed to AST_RESAMPLE<T>.
*     PARAMS( * ) = DOUBLE PRECISION (Given)
*        The auxiliary resampling parameters as passed to
*        AST_RESAMPLE<T>.
*     CONSRV = LOGICAL (Given)
*        Whether to conserve flux when resampling.
*     HASVAR = LOGICAL (Given)
*        Whether the variance is to be resampled.
*     HASQUA = LOGICAL (Given)
*        Whether the quality is to be resampled.
*     HASAXI = LOGICAL (Given)
*        Whether the AXIS Centres are to be resampled.
*     DATI( * ) = <TYPE> (Given)
*        The input data array.
*     VARI( * ) = <TYPE> (Given)
*        The input variance array (only used if HASVAR is true).
*     QUAI( * ) = BYTE (Given)
*        The input quality array (only used if HASQUA is true).
*     AXI( * ) = DOUBLE PRECISION (Given)
*        The input AXIS Centre array (only used if HASAXI is true).
*     BAD = LOGICAL (Given and Returned)
*        On input, a .TRUE. value indicates that the input data
*        (and variance) arrays may contain bad pixels.  On exit, it will
*        be set only if the output data (and variance) arrays actually
*        do contain bad pixels.
*     DATWKI( * ) = <TYPE> (Returned)
*        A workspace array, with DIMI( IDIM ) elements.
*     VARWKI( * ) = <TYPE> (Returned)
*        A workspace array, with DIMI( IDIM ) elements.  Only used if
*        HASVAR is true.
*     QUAWKI( * ) = BYTE (Returned)
*        A workspace array, with DIMI( IDIM ) elements.  Only used if
*        HASQUA is true.
*     DATWKO( * ) = <TYPE> (Returned)
*        A workspace array, with DIMO( IDIM ) elements.
*     VARWKO( * ) = <TYPE> (Returned)
*        A workspace array, with DIMO( IDIM ) elements.  Only used if
*        HASVAR is true.
*     QUAWKO( * ) = BYTE (Returned)
*        A workspace array, with DIMO( IDIM ) elements.  Only used if
*        HASQUA is true.
*     DATO( * ) = <TYPE> (Returned)
*        The output data array.
*     VARO( * ) = <TYPE> (Returned)
*        The output variance array (only used if HASVAR is true).
*     QUAO( * ) = BYTE (Returned)
*        The output quality array (only used if HASQUA is true).
*     AXO( * ) = DOUBLE PRECISION (Returned)
*        The output AXIS Centre array (only used if HASAXI is true).
*     STATUS = INTEGER (Given and Returned)
*        The global status.

*  Copyright:
*     Copyright (C) 2002 Central Laboratory of the Research Councils.
*     Copyright (C) 2006 Particle Physics & Astronomy Research Council.
*     Copyright (C) 2007 Science & Technology Facilities Council.
*     Copyright (C) 2020 East Asian Observatory.
*     All Rights Reserved.

*  Licence:
*     This program is free software; you can redistribute it and/or
*     modify it under the terms of the GNU General Public License as
*     published by the Free Software Foundation; either version 2 of
*     the License, or (at your option) any later version.
*
*     This program is distributed in the hope that it will be
*     useful, but WITHOUT ANY WARRANTY; without even the implied
*     warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
*     PURPOSE. See the GNU General Public License for more details.
*
*     You should have received a copy of the GNU General Public License
*     along with this program; if not, write to the Free Software
*     Foundation, Inc., 51 Franklin Street,Fifth Floor, Boston, MA
*     02110-1301, USA

*  Authors:
*     MBT: Mark Taylor (Starlink)
*     DSB: David Berry (STARLINK)
*     MJC: Malcolm J. Currie (STARLINK)
*     {enter_new_authors_here}

*  History:
*     3-JAN-2002 (MBT):
*        Original version.
*     16-JAN-2002 (DSB):
*        Added propagation of Quality and AXIS Centres.
*     2006 April 12 (MJC):
*        Remove unused variable and wrapped long lines.
*     18-MAY-2007 (DSB):
*        Added argument FACTS.
*     3-JULY-2007 (DSB):
*        Corrected calculation of the Mapping parameters.
*     30-AUG-2012 (DSB):
*        Added CONSRV argument.
*     27-NOV-2020 (DSB):
*        Replace argument FACTS with MAP. This is to ensure that the same
*        Mapping that is used to modify the WCS FrameSet (as created in
*        the main SQORST routine) is also used to do the resampling.
*     {enter_further_changes_here}

*-

*  Type Definitions:
      IMPLICIT NONE              ! No implicit typing

*  Global Constants:
      INCLUDE 'SAE_PAR'          ! Standard SAE constants
      INCLUDE 'PRM_PAR'          ! PRIMDAT constants
      INCLUDE 'NDF_PAR'          ! NDF system constants
      INCLUDE 'AST_PAR'          ! AST system declarations and constants

*  Arguments Given:
      INTEGER NDIM
      INTEGER IDIM
      INTEGER MAP
      INTEGER DIMI( NDIM )
      INTEGER DIMO( NDIM )
      INTEGER INTERP
      DOUBLE PRECISION PARAMS( * )
      LOGICAL CONSRV
      LOGICAL HASVAR
      LOGICAL HASQUA
      LOGICAL HASAXI
      <TYPE> DATI( * )
      <TYPE> VARI( * )
      BYTE QUAI( * )
      DOUBLE PRECISION AXI( * )

*  Arguments Given and Returned:
      LOGICAL BAD

*  Arguments Returned:
      <TYPE> DATWKI( * )
      <TYPE> VARWKI( * )
      BYTE QUAWKI( * )
      <TYPE> DATWKO( * )
      <TYPE> VARWKO( * )
      BYTE QUAWKO( * )
      <TYPE> DATO( * )
      <TYPE> VARO( * )
      BYTE QUAO( * )
      DOUBLE PRECISION AXO( * )

*  Status:
      INTEGER STATUS             ! Global status

*  Local Variables:
      DOUBLE PRECISION TOL       ! Tolerance for AST_RESAMPLE<T>
      INTEGER FLAGS              ! Flags for AST_RESAMPLE<T>
      INTEGER I                  ! Loop variable
      INTEGER IMAP               ! AST Mapping for 1d resample
      INTEGER IO                 ! Offset into array
      INTEGER J                  ! Loop variable
      INTEGER MAXPIX             ! Maximum linear approximation scale
      INTEGER NBAD               ! Number of bad pixels
      INTEGER ODIM               ! Output axis corresponding to input IDIM
      INTEGER OFFI               ! Offset into input arrays
      INTEGER OFFO               ! Offset into output arrays
      INTEGER POS( NDF__MXDIM )  ! Position in arrays
      INTEGER STRIDI( NDF__MXDIM ) ! Per-dimension strides, input arrays
      INTEGER STRIDO( NDF__MXDIM ) ! Per-dimension strides, output arrays
      LOGICAL DONE               ! Loop completion flag

*.

*  Check the inherited status.
      IF ( STATUS .NE. SAI__OK ) RETURN

*  Extract the Mapping for the single axis being transformed from the
*  supplied NDIM-dimensional Mapping. This goes from input GRID axis
*  IDIM to output GRID axis ODIM.
      CALL AST_MAPSPLIT( MAP, 1, IDIM, ODIM, IMAP, STATUS )

*  Sanity check. Input IDIM should always feed output IDIM for the sort
*  of linear Mappings expected here.
      IF( ODIM .NE. IDIM .AND. STATUS .EQ. SAI__OK ) THEN
         STATUS = SAI__ERROR
         CALL MSG_SETI( 'I', IDIM )
         CALL MSG_SETI( 'O', ODIM )
         CALL ERR_REP( ' ', 'KPS1_RS1: Invalid Mapping supplied: '//
     :                 'Input ^I feed output ^O (programming error).',
     :                 STATUS )
      ELSE

*  Get the strides along each dimension of the input and output arrays.
         STRIDI( 1 ) = 1
         STRIDO( 1 ) = 1
         DO I = 2, NDIM
            STRIDI( I ) = STRIDI( I - 1 ) * DIMI( I - 1 )
            STRIDO( I ) = STRIDO( I - 1 ) * DIMO( I - 1 )
         END DO

*  Resample the Data and Variance arrays.
*  ======================================

*  Initialise the offsets into the input and output arrays.
         OFFI = 1
         OFFO = 1

*  Set the initial position in the array.
         DO I = 1, NDIM
            POS( I ) = 1
         END DO

*  Set constant values for the resampling routine.
         FLAGS = 0
         IF ( CONSRV ) FLAGS = FLAGS + AST__CONSERVEFLUX
         IF ( HASVAR ) FLAGS = FLAGS + AST__USEVAR
         IF ( BAD ) FLAGS = FLAGS + AST__USEBAD
         TOL = 0.1D0
         MAXPIX = DIMI( IDIM ) + DIMO( IDIM )

*  Loop over all the lines along the resampling dimension.
         NBAD = 0
         DONE = .FALSE.
         DO WHILE ( .NOT. DONE )

*  Copy the line we will resample from the input arrays into work
*  buffers.
            IO = OFFI
            IF ( HASVAR ) THEN
               DO I = 1, DIMI( IDIM )
                  DATWKI( I ) = DATI( IO )
                  VARWKI( I ) = VARI( IO )
                  IO = IO + STRIDI( IDIM )
               END DO
            ELSE
               DO I = 1, DIMI( IDIM )
                  DATWKI( I ) = DATI( IO )
                  IO = IO + STRIDI( IDIM )
               END DO
            END IF

*  Perform the one-dimensional resampling.
            NBAD = NBAD + AST_RESAMPLE<T>( IMAP, 1, 1, DIMI( IDIM ),
     :                                     DATWKI, VARWKI, INTERP,
     :                                     AST_NULL,PARAMS, FLAGS, TOL,
     :                                     MAXPIX,VAL__BAD<T>, 1, 1,
     :                                     DIMO( IDIM ),1, DIMO( IDIM ),
     :                                     DATWKO, VARWKO, STATUS )

*  Copy the resampled line from work buffers into the output arrays.
            IO = OFFO
            IF ( HASVAR ) THEN
               DO I = 1, DIMO( IDIM )
                  DATO( IO ) = DATWKO( I )
                  VARO( IO ) = VARWKO( I )
                  IO = IO + STRIDO( IDIM )
               END DO
            ELSE
               DO I = 1, DIMO( IDIM )
                  DATO( IO ) = DATWKO( I )
                  IO = IO + STRIDO( IDIM )
               END DO
            END IF

*  Locate the start of the next line in the input arrays.
            J = 1
   1        CONTINUE
            IF ( J .LE. NDIM ) THEN
               IF ( J .EQ. IDIM ) THEN
                  J = J + 1
                  GO TO 1
               ELSE
                  IF ( POS( J ) .LT. DIMI( J ) ) THEN
                     OFFI = OFFI + STRIDI( J )
                     OFFO = OFFO + STRIDO( J )
                     POS( J ) = POS( J ) + 1
                  ELSE
                     OFFI = OFFI - ( DIMI( J ) - 1 ) * STRIDI( J )
                     OFFO = OFFO - ( DIMO( J ) - 1 ) * STRIDO( J )
                     POS( J ) = 1
                     J = J + 1
                     GO TO 1
                  END IF
               END IF
            ELSE
               DONE = .TRUE.
            END IF
         END DO

*  Update information about whether the output array contains bad
*  pixels.
         BAD = NBAD .GT. 0

*  Resample the Quality array.
*  ===========================
         IF( HASQUA ) THEN

*  Initialise the offsets into the input and output arrays.
            OFFI = 1
            OFFO = 1

*  Set the initial position in the array.
            DO I = 1, NDIM
               POS( I ) = 1
            END DO

*  Set constant values for the resampling routine.
            FLAGS = 0
            TOL = 0.1D0
            MAXPIX = DIMI( IDIM ) + DIMO( IDIM )

*  Loop over all the lines along the resampling dimension.
            NBAD = 0
            DONE = .FALSE.
            DO WHILE ( .NOT. DONE )

*  Copy the line we will resample from the input arrays into work
*  buffers.
               IO = OFFI
               DO I = 1, DIMI( IDIM )
                  QUAWKI( I ) = QUAI( IO )
                  IO = IO + STRIDI( IDIM )
               END DO

*  Perform the one-dimensional resampling.
               NBAD = NBAD + AST_RESAMPLEUB( IMAP, 1, 1, DIMI( IDIM ),
     :                                       QUAWKI, QUAWKI, INTERP,
     :                                       AST_NULL, PARAMS, FLAGS,
     :                                       TOL, MAXPIX, VAL__BADUB, 1,
     :                                       1, DIMO( IDIM ), 1,
     :                                       DIMO( IDIM ), QUAWKO,
     :                                       QUAWKO, STATUS )

*  Copy the resampled line from work buffers into the output arrays.
               IO = OFFO
               DO I = 1, DIMO( IDIM )
                  QUAO( IO ) = QUAWKO( I )
                  IO = IO + STRIDO( IDIM )
               END DO

*  Locate the start of the next line in the input arrays.
               J = 1
   2           CONTINUE
               IF ( J .LE. NDIM ) THEN
                  IF ( J .EQ. IDIM ) THEN
                     J = J + 1
                     GO TO 2
                  ELSE
                     IF ( POS( J ) .LT. DIMI( J ) ) THEN
                        OFFI = OFFI + STRIDI( J )
                        OFFO = OFFO + STRIDO( J )
                        POS( J ) = POS( J ) + 1
                     ELSE
                        OFFI = OFFI - ( DIMI( J ) - 1 ) * STRIDI( J )
                        OFFO = OFFO - ( DIMO( J ) - 1 ) * STRIDO( J )
                        POS( J ) = 1
                        J = J + 1
                        GO TO 2
                     END IF
                  END IF
               ELSE
                  DONE = .TRUE.
               END IF
            END DO

         END IF

*  Resample the Axis array.
*  ========================
         IF( HASAXI ) THEN
            FLAGS = AST__USEBAD
            NBAD = AST_RESAMPLED( IMAP, 1, 1, DIMI( IDIM ), AXI, AXI,
     :                            INTERP, AST_NULL, PARAMS, FLAGS, TOL,
     :                            MAXPIX, VAL__BADD, 1, 1, DIMO( IDIM ),
     :                            1, DIMO( IDIM ), AXO, AXO, STATUS )
         END IF
      END IF

*  Free resources
      CALL AST_ANNUL( IMAP, STATUS )

      END
