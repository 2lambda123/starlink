dnl    Process this file with autoconf to produce a configure script
AC_REVISION($Revision$)

dnl    Initialisation: package name and version number
AC_INIT(extractor, 2.3, extractor@star.rl.ac.uk)

dnl    Require autoconf-2.50 at least
AC_PREREQ(2.50)
dnl    Require Starlink automake
AM_INIT_AUTOMAKE(1.8.2-starlink)

dnl    Sanity-check: name a file in the source directory -- if this
dnl    isn't found then configure will complain
AC_CONFIG_SRCDIR([extractor.news])

dnl    Include defaults for Starlink configurations
STAR_DEFAULTS

dnl    Need to build an atask, depends on much the same as a full monolith.
STAR_MONOLITHS

dnl    To configure the two components we just run ./configure in the
dnl    extract and sextractor directories. Do not invoke AC_CONFIG_SUBDIRS,
dnl    since that prompts autoreconf to try to reconfigure that directory, and
dnl    automake to assume it's allowed to play there, too.
(
    cd extract
    ./configure --prefix=$prefix --cache-file=config.cache STARLINK=$STARLINK
    cd ..
    cd sextractor
    ./configure --prefix=$prefix --cache-file=config.cache
)

dnl    Find required versions of the programs we need for configuration
AC_PROG_CC
AC_PROG_FC
AC_PROG_LIBTOOL

dnl    Declare the build and use dependencies for this package
STAR_DECLARE_DEPENDENCIES(build, [ast ndf sae])
STAR_DECLARE_DEPENDENCIES(link,  ndf)

dnl    List the sun/ssn/... numbers which document this package and
dnl    which are present as .tex files in this directory.
STAR_LATEX_DOCUMENTATION(sun226, [sun226.tex sun226.ps sun226.htx_tar])
STAR_CHECK_PROGS( star2html )

STAR_DOC_FILES([mud165.ps extractor.news sun226fig.ps])

dnl    If you wish to configure extra files, you can add them to this
dnl    declaration.
AC_CONFIG_FILES([Makefile component.xml])

dnl    This is the bit that does the actual work
AC_OUTPUT
