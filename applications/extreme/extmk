#!/usr/bin/perl -w

#+
#  Name:
#     extmk.pl
#
#  Purpose:
#     Modify mk file for use with EXTREME data sets.
#
#  Usage:
#     extmk.pl < mk > mk.new
#
#  Description:
#     This is a simple filter which takes a Starlink mk file as input and
#     writes as output a file which should provide the additional 
#     functionality required by the EXTREME package.  It adds stanzas
#     for the two new values of the SYSTEM variable `alpha_OSF1_64' and
#     `sun4_Solaris_64', and provides for the new environment 
#     variable/macro INTEGER8.  Changes are also made to the values of
#     the CFLAGS, FFLAGS and SOURCE_VARIANT macros where appropriate.
#
#     This script is not extremely intelligent - unless the mk file
#     follows the usual pattern quite closely, the output may be in
#     error, or complete rubbish.  It is intended as a convenience, 
#     and if it does not work, then it will be necessary to make the
#     modifications by hand; this is not too arduous.
#
#  Authors:
#     MBT: Mark Taylor (Starlink)
#
#  History:
#     22-FEB-2000 (MBT):
#        Initial version.
#-

#  Safe programming.
      use strict;

#  Read input.
      my( @mk );
      while (<>) {
         push( @mk, $_ );
      }
      my( $mk ) = join( '', @mk );

#  Perform some rudimentary checks on the input.
      if ( $mk =~ /alpha_OSF1_64|sun4_Solaris_64/i ) {
         print( STDERR 
                "Warning: mk file already contains 64-bit adaptations.\n",
                "         Proceeding anyway.\n" );
      }
      if ( $mk !~ /\n *mk *$/ || $mk !~ /TAR_OUT/ ) {
         print( STDERR
                "Warning: input doesn't look exactly like a mk file.\n",
                "         Proceeding anyway.\n" );
      }

#  Perform the edits.

#  Change the comment block describing supported systems.
      $mk =~ s{
                (   ^\#  \ +  alpha_OSF1 )        \ * \n
                (   ^\#  \ +     DEC .*? )        \ * \n
                (   ^\#                           \ * \n )?
             }{
                my( $a, $b, $blank ) = ( $1, $2, ( $3 || '' ) );
                "$a\n" . "$b\n" . $blank .
                $a . "_64" . "\n" .
                $b . ", long integers" . "\n" .
                $blank;
             }mxie;
      $mk =~ s{
                (   ^\#                           \ * \n )?
                (   ^\#  \ +  sun4_Solaris )      \ * \n
                (   ^\#  \ +     SUN .*?   )      \ * \n
             }{
                my( $blank, $a, $b ) = ( ( $1 || '' ), $2, $3 );
                my( $bx ) = $b;
                $bx =~ s/running /running 64-bit /i;
                $blank . "$a\n" . "$b\n" .
                $blank .
                $a . "_64" . "\n" .
                $bx . ", long integers" . "\n";
             }mxie;

#  Change the comment block describing macro defaults; modify the entry
#  for CFLAGS and insert a new entry for INTEGER8.
      $mk =~ s{ 
                (   ^\#  \ +  CFLAGS  \(   [^)]*   \)  \ * \n )
             }{
                my( $a ) = $1;
                $a =~ s/\)/ -DINT_BIG=int)/;
                $a;
             }mxe;

      $mk =~ s{
                (   ^\#                                 \ * \n )?
                (   ^\#  \ +  LINK  \ *  \(  [^)]*  \)  \ * \n )
             }{
                my( $blank, $a ) = ( ( $1 || '' ), $2 );
                my( $ax ) = $a;
                $ax =~ s/LINK/INTEGER8/;
                $ax =~ s/\([^)]*\)/(INTEGER    )/;
                my( $descrip ) = q(
                 Replacement text for 'INTEGER * 8' declarations in the original
                 original Fortran source code.  If set to the empty string,
                 INTEGER * 8 declarations will not be modified.  For 64-bit
                 systems which must be able to deal with very large images
                 this should be set to the empty string (or 'INTEGER*8').
                 Otherwise, more efficient code may be generated by setting
                 it to 'INTEGER' or 'INTEGER*4'.  The trailing whitespace is
                 optional but may make source code more readable. );
                $descrip =~ s/^\n//;
                $descrip =~ s/^ */#           /mg;
                $blank . $ax . $descrip . "\n" .
                $blank . $a;
             }mxie;

#  Change environment variable exports; add a new entry for INTEGER8.
      $mk =~ s{
                (   ^  \ +  export  \ +  LINK         \ * \n )
             }{
                my( $a ) = $1;
                my( $ax ) = $a;
                $ax =~ s/LINK/INTEGER8/;
                $a . $ax;
             }mxe;
                

#  We have now dealt with everything apart from the case stanzas 
#  dealing with each SYSTEM in turn.  Output the initial part of the
#  text and forget about it.
      $mk =~ s{ 
                (   .*                                   \n
                      \ + case [^\n] \$ SYSTEM  [^\n]*   \n )
             }{
                print( $1 );
                '';
             }sxe;


#  Now identify each 'case "$SYSTEM"' stanza and modify it.
#  In general these just get INTEGER8 and CFLAGS modified to reflect the 
#  new defaults, but for SYSTEM values of alpha_OSF1 and sun4_Solaris,
#  a new stanza is written for the corresponding 64-bit system.
      $mk =~ s{
                ( ^\# .*                                  \n )
                ( ^\# \ *  -----*    \ *                  \n )
                ( ^   \ *  \w+  \ *  \)   \ *             \n )
                (  (?: ^  .*   \S   .*   \n )+?              )
                ( ^   \ * ;;  \ *                         \n )
             }{
                my( $a, $b, $c, $d, $e ) = ( $1, $2, $3, $4, $5 );
                $c =~ /^ *(\w+) *\)/;
                my( $sys ) = $1;
                $d =~ s/(CFLAGS=.*)'/$1 -DINT_BIG=int'/;
                $d =~ s{^( *)(LINK=.*\n)}{$1$2$1INTEGER8='INTEGER    '\n}m;
                my( $block ) = "$a$b$c$d$e";
                my( $blockx ) = '';
                if ( $sys eq 'alpha_OSF1' || $sys eq 'sun4_Solaris' ) {
                   $blockx = $block;
                   $blockx =~ s/-DINT_BIG=int/-DINT_BIG=long/;
                   $blockx =~ s/INTEGER8='INTEGER    '/INTEGER8=''/;
                   $blockx =~ s/$sys/${sys}_64/g;
                   $blockx =~ s/(\.?) *\n/, long integers$1\n/;
                   $blockx =~ s/^(\# *---)/$1---------------/;
                   $blockx = "\n" . $blockx;
                   if ( $sys eq 'sun4_Solaris' ) {
                      $blockx =~ s/(^.* running)/$1 64-bit/;
                      $blockx =~ s/^(\# *---)/$1-------/;
                      $blockx =~ s/(CFLAGS=.*)'/$1 -xarch=v9'/;
                      $blockx =~ s/(FFLAGS=.*)'/$1 -xarch=v9'/;
                   }
                }
                $block . $blockx;
             }mxeg;
                

#  Output the remainder of the modified text.
      print $mk;



# $Id$
