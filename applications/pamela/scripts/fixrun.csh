#!/bin/csh -f
#
# !!begin 
# !!title  Fix run number of SAAO data
# !!author T.R. Marsh
# !!created  ?
# !!revised  08 December 2005
# !!root   fixrun 
# !!index  fixrun.csh
# !!descr  Script to fix run numbers of SAAO data
# !!head1  Debiassing script
#
# This inserts run numbers into the .more.pamela structure generated by
# !!ref{fixhead.html}{fixhead}. It is specific to SAAO data.
#
# !!head2 Invocation
#
# fixrun file1 file2 ...
#
# !!end

source $STARLINK_DIR/etc/cshrc
source $STARLINK_DIR/etc/login
figaro > /dev/null

if($#argv == 0) then
  echo "usage: fixruns file1 file2 ..."
  exit
endif

foreach file ($*)
  if($file:e != "sdf") then
    echo "Skipped $file as it does not end with .sdf"
  else if(-e "$file") then
    set root = $file:r
    echo $root | egrep '[0-9][0-9][0-9]$' > /dev/null
    if( $status ) then
       echo "File: $root does not end in a 3 digit number"
    else
      set numrun  = `echo $root | sed 's/^.*\([0-9][0-9][0-9]\)/\1/'`
      setobj $numrun $root.more.pamela.numrun
    endif
  endif

end
