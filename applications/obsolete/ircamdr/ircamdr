#!/bin/csh -f

#+
# file:        IRCAMDR_ROOT/ircamdr
# type:        C-shell script
# author:      Phil Daly
# description: Invokes the Unix-IRCAMDR command line software
# history:
#   29-may-95: original version (pnd@jach.hawaii.edu)
#   05-Jul-95: modified (caa@jach.hawaii.edu)
#   07-Nov-95: modified for Starlink use (caa@jach.hawaii.edu)
#   05-Dec-95: modified for ukirt use (caa@jach.hawaii.edu)
# endhistory
# parameters: $1 is a directory /$HOME/
#             $2 is a UT-date   /UT=today/
#-

# If IRCAMDR_ROOT is not set, abort
if ( ${?IRCAMDR_ROOT} == 0 ) then
  setenv IRCAMDR_ROOT /ukirt_sw/ircamdr
endif

# Print out a welcome message
echo ""
echo "Welcome to IRCAM_CLRED/Portable-IRCAMDR V1.0-1"

# Set some default values
echo ""
set defdatadir = "`pwd`"
set defutdate  = "`date -u +%y%m%d`"

# If not arguments supplied, get them all
if ( ${#argv} == 0 ) then
  # Get data directory
  echo -n "Working directory /${defdatadir}/ > "
  set datadir = $<
  if ( "${datadir}" == "" ) set datadir = "${defdatadir}"
  # Get the UT date
#  echo -n "UT date?  /${defutdate}/ > "
#  set utdate = $<
  set utdate = ""
  if ( "${utdate}" == "" ) set utdate = "${defutdate}"

else if ( ${#argv} == 1 ) then
  # Get data directory
  set datadir = "${argv[1]}"
  # Get the UT date
#  echo -n "UT date?  /${defutdate}/ > "
#  set utdate = $<
  set utdate = ""
  if ( "${utdate}" == "" ) set utdate = "${defutdate}"

else if ( ${#argv} >= 2 ) then
  # Get data directory
  set datadir = "${argv[1]}"
  # Get the UT date
  set utdate = "${argv[2]}"
endif

# Check the UT date is of the form YYMMDD or YYYYMMDD
if ! ( "${utdate}" =~ [0-9][0-9][0-9][0-9][0-9][0-9] ) then
  if ! ( "${utdate}" =~ [0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9] ) then
    echo "${0}-f-invutdate, invalid UT date\!"
    exit 1
  endif
endif

# Set the IRCAM data directories
setenv ODIR  ../odir
setenv IDIR  ../idir
setenv RODIR ../rodir
setenv RIDIR ../ridir
setenv RGDIR ../rgdir

# Check the data directory is readable and writeable
if ( -r "${datadir}"  && -w "${datadir}" ) then
#  echo "cd ${datadir}"
  cd ${datadir}
else
  echo "${0}-f-norread, ${datadir} is not readable or writeable from `uname -n`\!"
  exit 1
endif

# Make sure ADAM_USER is defined
if ( ! -e ${HOME}/adam ) mkdir ${HOME}/adam
setenv ADAM_USER "`echo ${HOME}`/adam"

# Environment variables for ircam-clred operation
setenv LIRCAMDIR      ${IRCAMDR_ROOT}
#setenv LIRCAMDIR_BIN  ${IRCAMDR_ROOT} 
#setenv LIRCAMDIR_DOCS ${IRCAMDR_ROOT}
#setenv LIRCAMDIR_ICL  ${IRCAMDR_ROOT}
#setenv LIRCAMDIR_IFL  ${IRCAMDR_ROOT}
#setenv LIRCAMDIR_MISC ${IRCAMDR_ROOT}

# More specific env vars
if ( "`uname -m`" =~ [Ss][Uu][Nn]* ) then
  setenv SYSNODE SUN
else if ( "`uname -m`" =~ [Aa][Ll][Pp][Hh][Aa]* ) then
  setenv SYSNODE ALPHA
else
  setenv SYSNODE UNKNOWN
endif
setenv IRCAM_ENG ${HOME}
setenv TODAY "`date`"
setenv UTD ${defutdate}
setenv ROUTD "ro${defutdate}_"
setenv HDS_MAP 1

# Change path instead of aliasing commands 
set path = ( $path ${IRCAMDR_ROOT} )
rehash

# Start icl and invoke ircamdr
icl ${IRCAMDR_ROOT}/god_ircamdr_starlink.icl
echo ""
