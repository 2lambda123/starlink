#!/bin/csh
#+
# Create soft links for all modules in IFL library to executable monolith.
# Arguments are,
#   
#  $1	- The root directory containg the current dev and bin subdirectories
#  $2   - The monolith executable name
#  $3.. - The interface file libraries
#
#-

#
# Extract the source listing command
#
set scmd = "$SOURCE_LIST"
if ( "$scmd" == '$(TAR_LIST)' ) then
  set scmd = "$TAR_LIST"
endif
#
echo "#+ This is a make file generated by GEN_task"
echo "#"
echo "#-"
echo '.SUFFIXES:'
echo '.SUFFIXES: .c'
echo "PKG_NAME      = asterix"
echo "ROOT        = $1"
echo 'BIN           = $(ROOT)/$(SYSTEM)/bin'
#
# Installation directories
#
echo " "
echo '\
\
#  Pathnames of directories into which files may be placed when the\
#  package is installed.\
\
INSTALL_BIN = $(INSTALL)/bin/$(PKG_NAME)\
INSTALL_DATES = $(INSTALL)/dates\
INSTALL_HELP = $(INSTALL)/help/$(PKG_NAME)\
'
#
#
#
shift argv
set exec="$1"
shift argv
set files=" "
echo "EXEFILE     = ${exec:t}"

echo "\
EXE_FILES = \"
set lf=0
set osfile=" "

#
# Loop over interface file libraries
#
set niflibs=$#argv
set ilib=0
foreach iflib ( $* )
#
# Loop over contents of library. If the library name contains braces then they
# contain a list of the names of the modules to use.
#
  @ ilib++
  set plib=`echo "${iflib}"| tr '(' ' ' | tr ')' ' ' | tr ',' ' '`
  if ( "$plib" == "$iflib" ) then
    set thelib="$iflib"
    set modules="`$scmd ${thelib}|fgrep '.ifl'`"
  else
    set thelib=`echo ${plib} | awk '{print $1}'`
    set modules=`echo ${plib} | awk '{for(x=2;x<=NF;x++)print $x}'`
  endif
#
# Loop over required modules
#
  set nmod=$#modules
  set imod=0
  foreach module ( ${modules} )

    set osfile="$osfile $module:r"
    @ lf++
    @ imod++
    if ( $lf > 5 ) then
      if ( ($nmod == $imod) && ($ilib == $niflibs) ) then
        echo "$osfile"
      else
        echo "$osfile \"
      endif
      set lf=0
      set osfile=" "
    endif
  end
end
echo "$osfile"

echo " \
"
echo '$(EXE_FILES):\
	@ if test -r $(BIN)/$@; then \'
echo '           rm -f $(BIN)/$@ ; else :; fi\
	@ $(LINK) ./$(EXEFILE) $(BIN)/$@\
	@ echo "Made task : $@"'

echo " "
echo 'build:	$(EXE_FILES)'
echo " "
echo 'install:\
	@ for file in $(EXE_FILES); do \'
echo '	  if test ! -f $(INSTALL_BIN)/$$file; then \'
echo '	    $(LINK) ./$(EXEFILE) $(INSTALL_BIN)/$$file;\'
echo '	  else :; fi; \'
echo '	done'
echo " "
echo 'deinstall:\
	@- for file in $(EXE_FILES); do \'
echo '	   rm -f $(INSTALL_BIN)/$$file;\'
echo '	done'
echo " "
echo 'unbuild:\
	@- for file in $(EXE_FILES); do \'
echo '	   rm -f $(BIN)/$$file 1>/dev/null 2>/dev/null;\'
echo '	done'
echo " "
