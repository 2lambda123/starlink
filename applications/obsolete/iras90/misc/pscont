#!/bin/csh 
#+
#  Name:
#     pscont

#  Purpose:
#     Produce a contour map with sky coordinate grid for output on a 
#     Postscript printer.

#  Type of module:
#     C-shell script.

#  Description:
#     This procedure uses the KAPPA application TURBOCONT to generate
#     an encapsulated postscript file containing the contour plot. 
#     IRAS90 application SKYGRID is then used to produce another 
#     encapsulated postscript file containing the sky coordinates grid.
#     These two files are then merged into one using the PSMERGE 
#     utility. The resulting file (called pscont.ps) can be printed as
#     normal on a Postscript printer.

#  Usage:
#     PSCONT NDF MODE NCONT

#  Arguments:
#     NDF = NDF (Given)
#        The NDF structure containing the 2-d image to be contoured.
#     MODE = LITERAL (Given)
#        The method by which contour levels are to be selected. See
#        TURBOCONT parameter MODE.
#     NCONT = INTEGER (Given)
#        The number contours required. See TURBOCONT parameter NCONT.

#  Notes:
#     - PSCONT is a command procedure, or script.
#     - This procedure provides limited facilities for customising the 
#     plot using the other parameter of TURBOCONT and SKYGRID. To do 
#     this the first argument ("NDF") should be given the value PROMPT
#     (any values supplied for MODE and NCONT are ignored). This causes
#     the keyword PROMPT to be supplied to SKYGRID and TURBOCONT, thus 
#     causing the user to be prompted for all parameters used by these
#     two applications (including the NDF parameter). If other 
#     facilities are required, then the applications TURBOCONT, 
#     SKYGRID and PSMERGE must be run individually rather than from 
#     within this command procedure.

#  Prior requirements:
#     - The KAPPA, IRAS90 and PSMERGE packages must have been 
#     initiated.

#  Authors:
#     DSB: D. S. Berry (STARLINK)
#     DCP: Diana Parsons (FIIS/RAL)
#     {enter_new_authors_here}

#  History:
#     1-FEB-1993 (DSB):
#	 Original version.
#     23-JUN-1994 (DCP):
#        Modified to include checks for pre existing work files
#     {enter_changes_here}

#-
#  Give introductory details
   echo " pscont - gives a postscript file containing a contour map with"
   echo "          overlaid grid for IRAS images"
   echo " "
   echo " Priliminary work can be done in MODE auto with the reqired number"
   echo " of contour levels. For other types of contour plot you should enter"
   echo " the word prompt to the request for the NDF_file_name "
   echo " and consult KAPPA documentation on TURBOCONT, and IRAS90 "
   echo " documentation on SKYGRID, for replies to prompts"

#  Check whether there are files with the same name as those used for work
#  files preexisting and if so report and exit
  echo " "
  if ( -e turbocont.ps || -e turbocont.ps.1 || -e skygrid.ps || -e junk.ps ) \
  then
     echo "You have a file(s) named turbocont.ps*, skygrid.ps, and/or junk.ps"
     echo "Please rename or delete them before running this program"
     echo "pscont is now terminating for you to do this"
     goto last
  endif 

#  Run KAPPA and IRAS90 file
   echo"just about to run KAPPA"
   kappa
   iras90
   

#  Get a value for the first argument, mimicking the NDF parameter 
#  prompt issued by TURBOCONT.
  if ( $1 == "" ) then 
     echo -n "NDF - NDF to be contoured > "
     set ndf = $<
  else
     set ndf = $1
  endif

#  If the supplied value is not PROMPT, ensure that the other required 
#  arguments have been given. If not inquire what they are mimicking 
#  the TURBOCONT prompts for the corresponding parameters.
  echo $ndf
  if ( $ndf != "prompt" &&  $ndf != "PROMPT" ) then

     if ( $2 == "" ) then 
        echo -n "MODE - Method for selecting contour heights >"
        set mode = $<
     else
        set mode = $2
     endif

     if ( $3 == "" ) then 
        echo -n "NCONT - Give the number of contour heights >"
        set ncont = $<
     else
        set ncont = $3
     endif

  endif

#  Ensure that the full paper size will be used by clearing the
#  AGI database for the Encapsulated Postscript (Landscape) device
#  2703.
  echo "  "
  echo "  Deleting any existing Postscript pictures (GDCLEAR) ..."
  gdclear device=2703\;junk.ps

#  Create a picture which is slightly smaller than the paper so that
#  there is room for the annotations around the edge of the sky
#  coordinate grid.
  echo "  Creating a new Postscript picture (PICDEF) ..."
  picdef mode=cc fraction=0.75 outline=f device=2703\;junk.ps

#  Create the contour plot, putting the output in file turbocont.ps.
#  Suppress the normal axis annotation generated by TURBOCONT. 
  echo "  Creating the contour plot (TURBOCONT) ..."
  if ( $ndf != "prompt" && $ndf != "PROMPT" ) then
     turbocont nokey ndf=$ndf mode=$mode ncont=$ncont axes=f device=2703\;turbocont.ps
  else
     turbocont axes=f device=2703\;turbocont.ps prompt
  endif

#  Now plot the sky coordinate grid, putting the results in SKYGRID.PS.
  echo "  Creating the sky coordinate grid (SKYGRID) ..."
  if ( $ndf != "prompt" && $ndf != "PROMPT" ) then
     skygrid clear=no device=2703\;skygrid.ps
  else
     skygrid clear=no device=2703\;skygrid.ps prompt
  endif

#  Merge the two encapsulated Postscript files into a single (normal)
#  Postscript file by simply overlaying one on the other. 
  echo "  Merging the contour plot and sky coordinate grid (PSMERGE) ..."
  psmerge -r90 -t560x72 turbocont.ps.1 -r90 -t560x72 skygrid.ps > pscont.ps

#  Delete the unwanted Postscript files.
  rm -f junk.ps*
  rm -f turbocont.ps*
  rm -f skygrid.ps*

#  Exit the procedure.
  echo "  The output is in file pscont.ps"
  echo "  "
#  Exit from error 
  last:
  echo "  pscont finished "
  exit
